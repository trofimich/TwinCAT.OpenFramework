<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1">
  <POU Name="DemoWorkflowAutomationController" Id="{dc096c7a-49dd-4129-a1d0-54dcfc137d76}" SpecialFunc="None">
    <Declaration><![CDATA[(*

.. <legal notes>

legal notes
=================
| SPDX-FileCopyrightText: © 2025 Oleksandr Tiutyk <trofimich@gmail.com>
| SPDX-License-Identifier: LGPL-3.0-only
| For details check: LGPL-3.0-only_

.. _LGPL-3.0-only: https://www.gnu.org/licenses/lgpl-3.0.en.html

.. </legal notes>

*)

{attribute 'no_explicit_call' := 'do not call this function block directly'} 
FUNCTION_BLOCK DemoWorkflowAutomationController EXTENDS TOF_AutomationEngine.AutomationController<0>
VAR_OUTPUT
	DemoWorkflow : DemoWorkflow;	
END_VAR
VAR	
	_ClassName : STRING := __POUNAME();
	
	_Started : BOOL;
END_VAR]]></Declaration>
    <Implementation>
      <ST><![CDATA[]]></ST>
    </Implementation>
    <Folder Name="Service methods" Id="{1828c457-8a78-48a2-9b87-64cb67bc8882}" />
    <Method Name="addMoveFromToActivity" Id="{5aa5ff3a-92c3-4d8d-9a23-b811cd230c9d}" FolderPath="Service methods\">
      <Declaration><![CDATA[METHOD PRIVATE addMoveFromToActivity 
VAR_INPUT
	workflow : TOF_Workflow.IWorkflow;
	parentSequence : TOF_Workflow.IWhileActivity;
	cornerNumber : BYTE;
END_VAR
VAR
	waitAndPickActivity : POINTER TO TOF_Workflow.WaitAndPickActivity;
	
	trueValue : BOOL := TRUE;
	
	needMoveVariableName : STRING;	
	targetXPosition : LREAL;
	targetYPosition : LREAL;
	
	waitButtonPressActivity : POINTER TO TOF_Workflow.WaitVariableValueActivity;
	
	moveActivity : POINTER TO MoveFromToActivity;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[// calculate variable values depending of current position (corner number 1-4)
CASE cornerNumber OF 
	1:
		needMoveVariableName := 'MoveFrom1To2';
		targetXPosition := 400;
		targetYPosition := 0;
		
	2:
		needMoveVariableName := 'MoveFrom2To3';
		targetXPosition := 400;
		targetYPosition := 300;
		
	3:
		needMoveVariableName := 'MoveFrom3To4';
		targetXPosition := 0;
		targetYPosition := 300;
		
	4:
		needMoveVariableName := 'MoveFrom4To1';
		targetXPosition := 0;
		targetYPosition := 0;
END_CASE

waitButtonPressActivity := __NEW(TOF_Workflow.WaitVariableValueActivity(workflow := workflow));
waitButtonPressActivity^.Name := WideStringHelper.ConcatStrings255("Wait", TO_WSTRING(needMoveVariableName), "Activity");
waitButtonPressActivity^.Configure(needMoveVariableName, trueValue);

parentSequence.AddChildActivity(waitButtonPressActivity^);

moveActivity := __NEW(MoveFromToActivity(workflow := workflow));
moveActivity^.Name := WideStringHelper.ConcatStrings255(TO_WSTRING(needMoveVariableName), "Activity");
moveActivity^.Configure(targetXPosition, targetYPosition);

parentSequence.AddChildActivity(moveActivity^);]]></ST>
      </Implementation>
    </Method>
    <Property Name="ClassName" Id="{ad6b5264-2d84-464d-8021-1d07f9d49884}">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
PROPERTY ClassName : STRING]]></Declaration>
      <Get Name="Get" Id="{897a4b58-f1c6-4661-8c68-f7b7183bfc21}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[ClassName := _ClassName;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Property Name="Devices" Id="{b7272ce5-6ecc-4fa4-bb5c-6b5e453591e8}">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
PROPERTY Devices : REFERENCE TO ARRAY [0.._DeviceCount - 1] OF TOF_Automation.IDevice]]></Declaration>
      <Get Name="Get" Id="{7ac2f615-f965-4470-afb8-029e1a112b89}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[Devices REF= 0;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Method Name="OnInitialize" Id="{b3bfbc25-ec55-4a48-bb17-81d8eba45dce}">
      <Declaration><![CDATA[METHOD PROTECTED OnInitialize : BOOL
VAR
	whileActivity : POINTER TO TOF_Workflow.WhileActivity;		
	trueCondition : POINTER TO TOF_Workflow.TrueCondition;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[whileActivity := __NEW(TOF_Workflow.WhileActivity(workflow := DemoWorkflow));
trueCondition := __NEW(TOF_Workflow.TrueCondition(workflow := DemoWorkflow, activity := whileActivity^));

whileActivity^.Configure(trueCondition^);

addMoveFromToActivity(DemoWorkflow, whileActivity^, 1);
addMoveFromToActivity(DemoWorkflow, whileActivity^, 2);
addMoveFromToActivity(DemoWorkflow, whileActivity^, 3);
addMoveFromToActivity(DemoWorkflow, whileActivity^, 4);

DemoWorkflow.SetRootActivity(whileActivity^);

OnInitialize := TRUE;]]></ST>
      </Implementation>
    </Method>
    <Method Name="OnRun" Id="{59d6ffe3-77b0-4f4a-8326-62224dd497be}">
      <Declaration><![CDATA[METHOD PROTECTED OnRun
VAR_IN_OUT CONSTANT
	executionContext : TOF_Automation.AUTOMATION_EXECUTION_CONTEXT;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF _Started THEN
	DemoWorkflow.Execute();
ELSE
	DemoWorkflow.Start();
	
	_Started := TRUE;
END_IF]]></ST>
      </Implementation>
    </Method>
    <Method Name="OnStop" Id="{5d7a8a2a-bb17-458b-b196-f49d8eecab4b}">
      <Declaration><![CDATA[METHOD PROTECTED OnStop
VAR_IN_OUT CONSTANT
	executionContext	: TOF_AutomationEngine.AUTOMATION_EXECUTION_CONTEXT;
END_VAR
VAR
	testException : TOF_Core.StandardException;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF DemoWorkflow.Running THEN
	DemoWorkflow.StartAbort();
END_IF]]></ST>
      </Implementation>
    </Method>
    <Property Name="Size" Id="{e4928d85-ffb2-44cd-81d8-5da5452d15ff}">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
PROPERTY Size : ULINT]]></Declaration>
      <Get Name="Get" Id="{f290be1b-b375-45a7-a2dc-21a7680125fb}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[Size := XSIZEOF(THIS^);]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Property Name="StartRequest" Id="{1e43dc08-5c6f-43fa-8043-e77bc3e183c3}">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
PROPERTY StartRequest : BOOL]]></Declaration>
      <Get Name="Get" Id="{22609e63-872f-4e8a-8b38-595be3a2ffa6}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[StartRequest := TRUE;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Property Name="StopRequest" Id="{3b9be127-2d2d-4e1c-8337-0b2322700b8b}">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
PROPERTY StopRequest : BOOL]]></Declaration>
      <Get Name="Get" Id="{ef4223e6-dc24-45ae-b593-31786ae2a856}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[StopRequest := FALSE;]]></ST>
        </Implementation>
      </Get>
    </Property>
  </POU>
</TcPlcObject>