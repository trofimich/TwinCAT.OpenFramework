<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1">
  <POU Name="TaskQueue" Id="{57bc5bc8-8897-4089-a74c-40a635526959}" SpecialFunc="None">
    <Declaration><![CDATA[(*

.. <legal notes>

legal notes
=================
| SPDX-FileCopyrightText: © 2025 Oleksandr Tiutyk <trofimich@gmail.com>
| SPDX-License-Identifier: LGPL-3.0-only
| For details check: LGPL-3.0-only_

.. _LGPL-3.0-only: https://www.gnu.org/licenses/lgpl-3.0.en.html

.. </legal notes>

*)

{attribute 'no_explicit_call' := 'do not call this function block directly'} 
FUNCTION_BLOCK TaskQueue EXTENDS Task IMPLEMENTS ITaskQueue
VAR
	_ClassName : STRING := __POUNAME();

	_Tasks : TOF_Collections.List(0);
	
	_CurrentTask : ITask;
END_VAR]]></Declaration>
    <Implementation>
      <ST><![CDATA[]]></ST>
    </Implementation>
    <Folder Name="Overriden members" Id="{5464d593-1143-48ec-b23a-fc34784f0588}" />
    <Folder Name="Service members" Id="{019de1b8-3752-4282-a5b4-415cf2cc5833}" />
    <Property Name="ClassName" Id="{2edfd268-df4f-41f9-aef6-5aec377fffed}">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
PROPERTY ClassName : STRING]]></Declaration>
      <Get Name="Get" Id="{bdddd8f6-e658-400e-a4a5-16ccab27e38b}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[ClassName := _ClassName;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Method Name="Clear" Id="{0bd33d82-460a-48e6-9f1b-76e8791ee152}">
      <Declaration><![CDATA[METHOD FINAL Clear]]></Declaration>
      <Implementation>
        <ST><![CDATA[_Tasks.Clear();]]></ST>
      </Implementation>
    </Method>
    <Method Name="Enqueue" Id="{cdf8e544-88b0-466c-84c0-eef4bbb8ce8c}">
      <Declaration><![CDATA[METHOD Enqueue
VAR_INPUT
	task : ITask;
	//deleteOnFinishOrClear : BOOL := FALSE;
END_VAR
VAR
	taskTypeNotSpecifiedException : TOF_Core.ArgumentNotSpecifiedException;
	
	taskAsGeneric : TOF_Core.GENERIC_VALUE;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF task = 0 THEN
	taskTypeNotSpecifiedException.Throw(Namespacename, ClassName, __POUNAME(), __POSITION(), 'task');
END_IF

(*IF deleteOnFinishOrClear THEN
	taskAsGeneric := TOF_Core.GenericValueFactory.FromParts(GENERIC_TYPE_CLASS.OBJECT, task.Address, task.Size, GENERIC_MEMORY_MANAGEMENT_BEHAVIOR.STORE_ORIGINAL_REFERENCE_AND_OWNS_MEMORY);
ELSE*)
	taskAsGeneric := TOF_Core.GenericValueFactory.FromParts(GENERIC_TYPE_CLASS.OBJECT, task.Address, task.Size, GENERIC_MEMORY_MANAGEMENT_BEHAVIOR.STORE_ORIGINAL_REFERENCE_AND_NOT_OWNS_MEMORY);
//END_IF

_Tasks.AppendGeneric(taskAsGeneric);]]></ST>
      </Implementation>
    </Method>
    <Method Name="getTaskByIndex" Id="{ae537d72-62f8-44f8-ba8a-8facdbe57dc1}" FolderPath="Service members\">
      <Declaration><![CDATA[METHOD PRIVATE getTaskByIndex : ITask
VAR_INPUT
	index : DINT;
END_VAR
VAR
	taskInterface : TOF_Core.IObject;
	
	toITaskConversionNotSupportedException : TOF_Core.StandardException;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[taskInterface := _Tasks.Get(index).AsIObject();

IF NOT __QUERYINTERFACE(taskInterface, getTaskByIndex) THEN
	toITaskConversionNotSupportedException.Throw(NamespaceName, ClassName, __POUNAME(), __POSITION(), "Can't convert task from _Tasks collection to ITask type");
END_IF]]></ST>
      </Implementation>
    </Method>
    <Property Name="NamespaceName" Id="{09dc716e-5580-4c60-8504-8027d84a23b2}">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
PROPERTY NamespaceName : STRING]]></Declaration>
      <Get Name="Get" Id="{0c9b30bd-5848-4c38-b284-a2579e632128}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[NamespaceName := CurrentNamespace.Name;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Method Name="OnReset" Id="{1e2349bd-0e4a-44d4-bd19-a7c6fff55858}" FolderPath="Overriden members\">
      <Declaration><![CDATA[METHOD PROTECTED OnReset 
VAR
	i : DINT;
	task : ITask;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[FOR i := 0 TO _Tasks.Count - 1 DO
	task := getTaskByIndex(i); 
	task.Reset();
END_FOR]]></ST>
      </Implementation>
    </Method>
    <Method Name="OnRunning" Id="{066e94bd-bc47-4abc-aae9-9ea45cfa5e9e}" FolderPath="Overriden members\">
      <Declaration><![CDATA[METHOD PROTECTED OnRunning 
VAR_OUTPUT
	stopped : BOOL;
	stopReason : TASK_STOP_REASON;
END_VAR
VAR_IN_OUT
	stopReasonMessage : TOF_Core.ERROR_MESSAGE;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[__TRY
	IF _CurrentTask <> 0 THEN
		_CurrentTask.Execute();	
	ELSE
		IF _Tasks.Count > 0 THEN
			_CurrentTask := getTaskByIndex(0);
			
			_CurrentTask.RequestStart();
		END_IF 
	END_IF
__CATCH
	(*IF _CurrentTask <> 0 AND_THEN _Tasks.Count > 0 THEN
		_Tasks.RemoveAt(0);
	END_IF
	
	TOF_Core.LogManager.TryLogMessage(WideStringHelper.ConcatStrings255("Task '", _CurrentTask.Name, "' error in queue"), LOG_CATEGORY.INFORMATION, "TaskQueue", "TaskQueue.Task", "TaskQueue.Task.Error");
	*)
	TOF_Core.ExceptionManager.ReThrowLastException();
__ENDTRY

IF _CurrentTask <> 0 THEN
	CASE _CurrentTask.State OF
		TASK_STATE.IDLE:
			removeCurrentTask();		
			
		TASK_STATE.STOPPED:
			stopped := TRUE;
			stopReason := _CurrentTask.StopReason;
			stopReasonMessage := _CurrentTask.StopReasonMessage;
			
			IF _CurrentTask.StopReason = TASK_STOP_REASON.SUCCESS THEN
				stopped := FALSE;
				removeCurrentTask();		
			END_IF
	END_CASE
END_IF]]></ST>
      </Implementation>
    </Method>
    <Method Name="OnStopped" Id="{750dd30f-901d-4c61-8142-3a91e1a3470a}" FolderPath="Overriden members\">
      <Declaration><![CDATA[METHOD PROTECTED OnStopped]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF _CurrentTask <> 0 THEN
	CASE _CurrentTask.State OF
		TASK_STATE.RUNNING:
			_CurrentTask.RequestCancel();
		
		TASK_STATE.STOPPED:
			_CurrentTask.Execute();	
	END_CASE
END_IF]]></ST>
      </Implementation>
    </Method>
    <Property Name="PendingTaskCount" Id="{506ca59d-f7bd-406c-81e6-98121bc542b7}">
      <Declaration><![CDATA[PROPERTY FINAL PendingTaskCount : DINT]]></Declaration>
      <Get Name="Get" Id="{f57b43d5-cca3-449b-a2c8-9889c92c4e06}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[PendingTaskCount := _Tasks.Count;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Method Name="removeCurrentTask" Id="{9a4c76e5-778e-4ec7-ae72-12fd4eb0b6a8}" FolderPath="Service members\">
      <Declaration><![CDATA[METHOD PRIVATE removeCurrentTask]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF _Tasks.Count > 0 THEN
	_Tasks.RemoveAt(0);
END_IF

_CurrentTask := 0;]]></ST>
      </Implementation>
    </Method>
    <Property Name="Size" Id="{27f24e8d-93c2-4b63-beaf-bbb71cead191}">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
PROPERTY Size : ULINT]]></Declaration>
      <Get Name="Get" Id="{aca67082-7f76-4409-a726-dcf164bb235b}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[Size := XSIZEOF(THIS^);]]></ST>
        </Implementation>
      </Get>
    </Property>
  </POU>
</TcPlcObject>