<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1">
  <POU Name="Task" Id="{2881da81-273b-4545-b65d-3babc98f9385}" SpecialFunc="None">
    <Declaration><![CDATA[(*

.. <legal notes>

legal notes
=================
| SPDX-FileCopyrightText: © 2025 Oleksandr Tiutyk <trofimich@gmail.com>
| SPDX-License-Identifier: LGPL-3.0-only
| For details check: LGPL-3.0-only_

.. _LGPL-3.0-only: https://www.gnu.org/licenses/lgpl-3.0.en.html

.. </legal notes>

*)

{attribute 'no_explicit_call' := 'do not call this function block directly'} 
FUNCTION_BLOCK ABSTRACT Task EXTENDS TOF_Core.Object IMPLEMENTS ITask
VAR
	_Name : WSTRING;
	
	_StateChanged : BOOL;

	_Busy : BOOL;
	
	_Done : BOOL;

	_Cancelled : BOOL;
	
	_Aborted : BOOL;
	
	_Error : BOOL;	
	_ErrorMessage : ERROR_MESSAGE;

	_Subscribers : TOF_Collections.UniqueSet(0);
END_VAR]]></Declaration>
    <Implementation>
      <ST><![CDATA[]]></ST>
    </Implementation>
    <Folder Name="Members to override" Id="{db2f5785-7161-42e1-b407-390855af59c1}" />
    <Folder Name="Service members" Id="{edd1c5da-fac4-4215-94b9-14921518865c}" />
    <Property Name="Aborted" Id="{b2172a66-cad7-44ab-a8dc-427f49ebd268}">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
PROPERTY FINAL Aborted : BOOL]]></Declaration>
      <Get Name="Get" Id="{a78a43a3-570b-489f-bd04-29243d1a7c8f}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[Aborted := _Aborted;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Property Name="Busy" Id="{fab33f98-0ed7-4643-9981-d11ace573ad2}">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
PROPERTY FINAL Busy : BOOL]]></Declaration>
      <Get Name="Get" Id="{9ff142fc-5a57-459a-bde1-1d87510abf01}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[Busy := _Busy;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Method Name="Cancel" Id="{2a9e1292-e28a-4dd3-94e3-162c35bf20f1}">
      <Declaration><![CDATA[METHOD FINAL Cancel
VAR
	exceptionCode : __SYSTEM.ExceptionCode;
	
	exception : IException;
	
	i : DINT;
	taskSubscriber : ITaskSubscriber;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF Busy THEN
	RETURN;
END_IF

__TRY
	FOR i := 0 TO _Subscribers.Count - 1 DO
		taskSubscriber := getTaskSubscriberByIndex(i);

		taskSubscriber.OnCancelling(THIS^);			
	END_FOR
	
	OnCancel();
	
	setCancelledState();

	FOR i := 0 TO _Subscribers.Count - 1 DO
		taskSubscriber := getTaskSubscriberByIndex(i);

		taskSubscriber.OnCancelled(THIS^);
	END_FOR
__CATCH(exceptionCode)
	handleFault(exceptionCode);
__ENDTRY]]></ST>
      </Implementation>
    </Method>
    <Property Name="Cancelled" Id="{d8f2410e-7331-4065-b42e-13b1b0639c40}">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
PROPERTY FINAL Cancelled : BOOL]]></Declaration>
      <Get Name="Get" Id="{460992a7-d725-4730-a114-76e3daecd06e}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[Cancelled := _Cancelled;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Property Name="Done" Id="{7e5f0a83-b992-41e6-9163-87ca788c0f3e}">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
PROPERTY FINAL Done : BOOL]]></Declaration>
      <Get Name="Get" Id="{83a8a930-cc7c-41cb-9370-dcac04c38e70}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[Done := _Done;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Property Name="Error" Id="{a6bbc4c5-e385-4f5a-b21f-dbbd96832f40}">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
PROPERTY FINAL Error : BOOL]]></Declaration>
      <Get Name="Get" Id="{4d43e9a8-4537-4194-8c10-0ed24298f440}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[Error := _Error;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Property Name="ErrorMessage" Id="{740a325e-10c1-4390-9de5-b8133b6a6c0e}">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
PROPERTY FINAL ErrorMessage : REFERENCE TO ERROR_MESSAGE]]></Declaration>
      <Get Name="Get" Id="{dc832da6-7e67-4865-98a3-e89008cbf074}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[ErrorMessage REF= _ErrorMessage;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Method Name="Execute" Id="{7d3bbbc2-d8e4-48ed-be78-b8da5dd67256}">
      <Declaration><![CDATA[METHOD FINAL Execute 
VAR
	runningState : TASK_RUNNING_STATE;
	
	exceptionCode : __SYSTEM.ExceptionCode;
	
	exception : IException;	
	
	errorMessage : ERROR_MESSAGE;

	i : DINT;
	taskSubscriber : ITaskSubscriber;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[_StateChanged := FALSE;

__TRY
	IF Busy THEN
		OnRunning(FALSE, state => runningState);
		
		CASE runningState OF
			TASK_RUNNING_STATE.DONE:
				setDoneState();
				
				FOR i := 0 TO _Subscribers.Count - 1 DO
					taskSubscriber := getTaskSubscriberByIndex(i);

					taskSubscriber.OnDone(THIS^);
				END_FOR
			
			TASK_RUNNING_STATE.ABORTED:
				setAbortedState();
				
				FOR i := 0 TO _Subscribers.Count - 1 DO
					taskSubscriber := getTaskSubscriberByIndex(i);

					taskSubscriber.OnAborted(THIS^);
				END_FOR
		END_CASE
	END_IF
__CATCH(exceptionCode)
	handleFault(exceptionCode);
__ENDTRY]]></ST>
      </Implementation>
    </Method>
    <Method Name="FB_exit" Id="{421906d5-7cb4-4cd4-b2bd-fbe5519bf3ad}">
      <Declaration><![CDATA[METHOD FB_exit : BOOL
VAR_INPUT
	(* if TRUE, the exit method is called for exiting an instance that is copied afterwards (online change).*)
	bInCopyCode	: BOOL;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[_Subscribers.Clear();]]></ST>
      </Implementation>
    </Method>
    <Method Name="getTaskSubscriberByIndex" Id="{86ad2a6c-0257-476b-83de-8dc8bd0b5648}" FolderPath="Service members\">
      <Declaration><![CDATA[METHOD PRIVATE getTaskSubscriberByIndex : ITaskSubscriber
VAR_INPUT
	taskSubscriberIndex : DINT;
END_VAR
VAR
	taskSubscriberInterface : TOF_Core.IObject;
	
	taskIndexOutOfRangeException : TOF_Core.ArgumentOutOfRangeException;
	
	toITaskConversionNotSupportedException : TOF_Core.StandardException;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF taskSubscriberIndex < 0 OR_ELSE taskSubscriberIndex >= _Subscribers.Count THEN
	taskIndexOutOfRangeException.Throw(NamespaceName, ClassName, __POUNAME(), __POSITION(), 'taskSubscriberIndex', taskSubscriberIndex);
END_IF

taskSubscriberInterface := _Subscribers.GetByIndex(taskSubscriberIndex).AsIObject();

IF NOT __QUERYINTERFACE(taskSubscriberInterface, getTaskSubscriberByIndex) THEN
	toITaskConversionNotSupportedException.Throw(NamespaceName, ClassName, __POUNAME(), __POSITION(), "Can't convert task subscriber from _Subscribers collection to ITaskSubscriber type");
END_IF
]]></ST>
      </Implementation>
    </Method>
    <Method Name="handleFault" Id="{d87e2b0c-4c9d-420b-9a85-b297eb6c9479}" FolderPath="Service members\">
      <Declaration><![CDATA[METHOD PRIVATE handleFault
VAR_INPUT
	exceptionCode : __SYSTEM.ExceptionCode;
END_VAR
VAR
	exception : IException;

	i : DINT;
	taskSubscriber : ITaskSubscriber;
	
	errorMesageBuilder : TOF_Core.WideStringBuilder; 
	errorMessage : TOF_Core.ERROR_MESSAGE;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[exception := ExceptionManager.GetLastException(exceptionCode);

__TRY
	OnFault();
__CATCH
__ENDTRY
	
IF NOT _Error THEN
	IF exception <> 0 THEN
		errorMessage := exception.GetMessage();

		errorMesageBuilder
			.AppendString255("Task '")
			.AppendString255(Name)
			.AppendString255("' failed: ")
			.Append(errorMessage);
		
		errorMesageBuilder.AssignToString(errorMessage);
		
		LogManager.TryLogMessage(errorMessage, LOG_CATEGORY.SUCCESS, TO_WSTRING(ClassName), "Task", "TaskError");
	ELSE
		LogManager.TryLogMessage(WideStringHelper.ConcatStrings255("Task '", Name, "' failed with unknown error"), LOG_CATEGORY.SUCCESS, TO_WSTRING(ClassName), "Task", "TaskError");		
	END_IF
	
	_StateChanged := TRUE;
END_IF

_Busy := FALSE;

_Done := FALSE;

_Cancelled := FALSE;

_Aborted := FALSE;

_Error := TRUE;		
_ErrorMessage := exception.GetMessage();

FOR i := 0 TO _Subscribers.Count - 1 DO
	taskSubscriber := getTaskSubscriberByIndex(i);

	taskSubscriber.OnError(THIS^, exception);
END_FOR	

ExceptionManager.Throw(exception);]]></ST>
      </Implementation>
    </Method>
    <Property Name="Name" Id="{6e123c98-9b28-4ea6-9d42-c3fcc2d44b13}">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
PROPERTY FINAL Name : WSTRING]]></Declaration>
      <Get Name="Get" Id="{cc73f3bf-4ae8-4386-8341-9813f1a49820}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[Name := SEL(_Name = "", _Name, SUPER^.Name);]]></ST>
        </Implementation>
      </Get>
      <Set Name="Set" Id="{eeffa16a-a40c-49e7-a894-c1df81dfe2e7}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[_Name := Name;]]></ST>
        </Implementation>
      </Set>
    </Property>
    <Method Name="OnCancel" Id="{50ea53b3-200a-461c-ab11-2f0c2b1dae40}" FolderPath="Members to override\">
      <Declaration><![CDATA[METHOD PROTECTED ABSTRACT OnCancel
]]></Declaration>
      <Implementation>
        <ST><![CDATA[]]></ST>
      </Implementation>
    </Method>
    <Method Name="OnFault" Id="{abf09786-ac1d-4415-b752-6f32f4f74ef2}" FolderPath="Members to override\">
      <Declaration><![CDATA[METHOD PROTECTED OnFault ]]></Declaration>
      <Implementation>
        <ST><![CDATA[]]></ST>
      </Implementation>
    </Method>
    <Method Name="OnRunning" Id="{6dd0e9d8-af4c-4ffa-bd19-f8adf74f282d}" FolderPath="Members to override\">
      <Declaration><![CDATA[METHOD PROTECTED ABSTRACT OnRunning 
VAR_INPUT
	startRequest : BOOL;
END_VAR
VAR_OUTPUT
	state : TASK_RUNNING_STATE;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[]]></ST>
      </Implementation>
    </Method>
    <Method Name="setAbortedState" Id="{701711d6-d83b-4c51-bbb9-f891989ade15}" FolderPath="Service members\">
      <Declaration><![CDATA[METHOD PRIVATE FINAL setAbortedState]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF NOT _Aborted THEN
	LogManager.TryLogMessage(WideStringHelper.ConcatStrings255("Task '", Name, "' aborted"), LOG_CATEGORY.SUCCESS, TO_WSTRING(ClassName), "Task", "TaskAborted");

	_StateChanged := TRUE;
END_IF

_Busy := FALSE;

_Done := FALSE;

_Cancelled := FALSE;

_Aborted := TRUE;

_Error := FALSE;	
_ErrorMessage := "";	]]></ST>
      </Implementation>
    </Method>
    <Method Name="setBusyState" Id="{129b7bf1-6245-4cb5-8474-1ec71cd6cfe9}" FolderPath="Service members\">
      <Declaration><![CDATA[METHOD PRIVATE FINAL setBusyState
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF NOT _Busy THEN
	LogManager.TryLogMessage(WideStringHelper.ConcatStrings255("Task '", Name, "' started"), LOG_CATEGORY.SUCCESS, TO_WSTRING(ClassName), "Task", "TaskBusy");
	
	_StateChanged := TRUE;
END_IF

_Busy := TRUE;

_Done := FALSE;

_Cancelled := FALSE;

_Aborted := FALSE;

_Error := FALSE;	
_ErrorMessage := "";	]]></ST>
      </Implementation>
    </Method>
    <Method Name="setCancelledState" Id="{947617f0-569c-4a47-bd27-5972666e4170}" FolderPath="Service members\">
      <Declaration><![CDATA[METHOD PRIVATE FINAL setCancelledState]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF NOT _Aborted THEN
	LogManager.TryLogMessage(WideStringHelper.ConcatStrings255("Task '", Name, "' cancelled"), LOG_CATEGORY.SUCCESS, TO_WSTRING(ClassName), "Task", "TaskCancelled");

	_StateChanged := TRUE;
END_IF

_Busy := FALSE;

_Done := FALSE;

_Cancelled := TRUE;

_Aborted := FALSE;

_Error := FALSE;	
_ErrorMessage := "";	]]></ST>
      </Implementation>
    </Method>
    <Method Name="setDoneState" Id="{9a5f7516-9b40-4b14-b66b-993f8b0f3cd7}" FolderPath="Service members\">
      <Declaration><![CDATA[METHOD PRIVATE FINAL setDoneState]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF NOT _Done THEN
	LogManager.TryLogMessage(WideStringHelper.ConcatStrings255("Task '", Name, "' done"), LOG_CATEGORY.SUCCESS, TO_WSTRING(ClassName), "Task", "TaskDone");
	
	_StateChanged := TRUE;
END_IF

_Busy := FALSE;

_Done := TRUE;

_Cancelled := FALSE;

_Aborted := FALSE;

_Error := FALSE;	
_ErrorMessage := "";	]]></ST>
      </Implementation>
    </Method>
    <Method Name="Start" Id="{9882b806-c3e8-4a3f-aaf2-ed11ba23d42b}">
      <Declaration><![CDATA[METHOD FINAL Start : BOOL
VAR
	runningState : TASK_RUNNING_STATE;

	exceptionCode : __SYSTEM.ExceptionCode;
	
	exception : IException;
	
	i : DINT;
	taskSubscriber : ITaskSubscriber;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF Busy THEN
	RETURN;
END_IF

__TRY
	FOR i := 0 TO _Subscribers.Count - 1 DO
		taskSubscriber := getTaskSubscriberByIndex(i);

		taskSubscriber.OnStarting(THIS^);
	END_FOR
	
	setBusyState();
	
	OnRunning(TRUE, state => runningState);

	CASE runningState OF
		TASK_RUNNING_STATE.DONE:
			setDoneState();
		
		TASK_RUNNING_STATE.ABORTED:
			setAbortedState();	
	END_CASE
	
	Start := TRUE;
__CATCH(exceptionCode)
	handleFault(exceptionCode);
__ENDTRY]]></ST>
      </Implementation>
    </Method>
    <Property Name="StateChanged" Id="{600cdb42-d3b2-455a-b48f-83e96606c782}">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
PROPERTY FINAL StateChanged : BOOL]]></Declaration>
      <Get Name="Get" Id="{a6557db5-61fd-4ccb-85a4-b417b3791f73}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[StateChanged := _StateChanged;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Method Name="Subscribe" Id="{055a6be1-f463-4449-b64d-c71c9c2307e3}">
      <Declaration><![CDATA[METHOD FINAL Subscribe
VAR_INPUT
	subscriber : ITaskSubscriber;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF subscriber <> 0 THEN
	_Subscribers.SetGeneric(GenericValueFactory.FromIObjectReference(subscriber));
END_IF]]></ST>
      </Implementation>
    </Method>
    <Method Name="Unsubscribe" Id="{ca975673-694f-476f-a523-5046a18ca6e7}">
      <Declaration><![CDATA[METHOD FINAL Unsubscribe
VAR_INPUT
	subscriber : ITaskSubscriber;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF subscriber <> 0 THEN
	_Subscribers.TryRemoveGeneric(GenericValueFactory.FromIObjectReference(subscriber));
END_IF]]></ST>
      </Implementation>
    </Method>
  </POU>
</TcPlcObject>