<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1">
  <POU Name="Task" Id="{2881da81-273b-4545-b65d-3babc98f9385}" SpecialFunc="None">
    <Declaration><![CDATA[(*

.. <legal notes>

legal notes
=================
| SPDX-FileCopyrightText: © 2025 Oleksandr Tiutyk <trofimich@gmail.com>
| SPDX-License-Identifier: LGPL-3.0-only
| For details check: LGPL-3.0-only_

.. _LGPL-3.0-only: https://www.gnu.org/licenses/lgpl-3.0.en.html

.. </legal notes>

*)

{attribute 'no_explicit_call' := 'do not call this function block directly'} 
FUNCTION_BLOCK ABSTRACT Task EXTENDS TOF_Core.Object IMPLEMENTS ITask
VAR
	_Name : WSTRING;
	
	_ResourceManager : TOF_Core.IResourceManager;
	
	_State : TASK_STATE;	
	_StateChanged : BOOL;
	
	_RunningSubstate : TASK_RUNNING_SUBSTATE;

	_StopReason : TASK_STOP_REASON;
	_StopReasonMessage : TOF_Core.ERROR_MESSAGE;
END_VAR]]></Declaration>
    <Implementation>
      <ST><![CDATA[]]></ST>
    </Implementation>
    <Folder Name="Members to override" Id="{db2f5785-7161-42e1-b407-390855af59c1}" />
    <Folder Name="Service members" Id="{edd1c5da-fac4-4215-94b9-14921518865c}" />
    <Folder Name="State flags" Id="{a31826e6-4b24-43fb-be0b-e8f69f304016}" />
    <Method Name="Execute" Id="{7d3bbbc2-d8e4-48ed-be78-b8da5dd67256}">
      <Declaration><![CDATA[METHOD FINAL Execute 
VAR
	stopped : BOOL;

	exceptionCode : __SYSTEM.ExceptionCode;

	exception : IException;
END_VAR
VAR_INST
	stopReason : TASK_STOP_REASON;
	stopReasonMessage : TOF_Core.ERROR_MESSAGE;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[_StateChanged := FALSE;

CASE _State OF
	TASK_STATE.RUNNING:
		__TRY
			CASE _RunningSubstate OF
				TASK_RUNNING_SUBSTATE.RESOURCE_ACQUIRING:
					IF _ResourceManager.AcquireResource() THEN
						setRunningSubstate(TASK_RUNNING_SUBSTATE.PROCESSING);
					END_IF
				
				TASK_RUNNING_SUBSTATE.PROCESSING:
					OnRunning(stopped => stopped, stopReason => stopReason, stopReasonMessage);

					IF stopped THEN
						IF _ResourceManager = 0 OR_ELSE NOT _ResourceManager.ResourceAcquired THEN
							setStoppedState(stopReason, stopReasonMessage);				
						ELSE
							setRunningSubstate(TASK_RUNNING_SUBSTATE.RESOURCE_RELEASING);
						END_IF
					END_IF
				
				TASK_RUNNING_SUBSTATE.RESOURCE_RELEASING:
					IF _ResourceManager.ReleaseResource() THEN
						setStoppedState(stopReason, stopReasonMessage);				
					END_IF				
			END_CASE
			
		__CATCH(exceptionCode)
			exception := ExceptionManager.GetLastException(exceptionCode);
			
			stopReason := TASK_STOP_REASON.FAILURE;
			stopReasonMessage := exception.GetMessage();
			
			IF _ResourceManager = 0 OR_ELSE NOT _ResourceManager.ResourceAcquired THEN
				setStoppedState(stopReason, stopReasonMessage);				
			ELSE
				setRunningSubstate(TASK_RUNNING_SUBSTATE.RESOURCE_RELEASING);
			END_IF
		__ENDTRY
	
	TASK_STATE.STOPPED:
		OnStopped();
END_CASE]]></ST>
      </Implementation>
    </Method>
    <Method Name="FB_init" Id="{7090956e-c27d-4671-ab2f-a1b87ff6921d}">
      <Declaration><![CDATA[METHOD FB_init : BOOL
VAR_INPUT
	(* if TRUE, the retain variables are initialized (warm start / cold start)*)
	bInitRetains	: BOOL;
	(* if TRUE, the instance afterwards gets moved into the copy code (online change)*)
	bInCopyCode	: BOOL;
	resourceManager : IResourceManager;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[_ResourceManager := resourceManager;]]></ST>
      </Implementation>
    </Method>
    <Method Name="handleException" Id="{d87e2b0c-4c9d-420b-9a85-b297eb6c9479}" FolderPath="Service members\">
      <Declaration><![CDATA[METHOD PRIVATE handleException
VAR_INPUT
	exceptionCode : __SYSTEM.ExceptionCode;
END_VAR
VAR
	exception : IException;
	
	errorMessage : TOF_Core.ERROR_MESSAGE;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[]]></ST>
      </Implementation>
    </Method>
    <Property Name="Idle" Id="{752972e5-29e8-4d66-9b9b-bacbd95bc807}" FolderPath="State flags\">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
PROPERTY FINAL Idle : BOOL
]]></Declaration>
      <Get Name="Get" Id="{be7b15e3-99cc-422a-a1f5-1bf7586205fb}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[Idle := _State = TASK_STATE.IDLE;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Property Name="Name" Id="{6e123c98-9b28-4ea6-9d42-c3fcc2d44b13}">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
PROPERTY FINAL Name : WSTRING]]></Declaration>
      <Get Name="Get" Id="{cc73f3bf-4ae8-4386-8341-9813f1a49820}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[Name := SEL(_Name = "", _Name, SUPER^.Name);]]></ST>
        </Implementation>
      </Get>
      <Set Name="Set" Id="{eeffa16a-a40c-49e7-a894-c1df81dfe2e7}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[_Name := Name;]]></ST>
        </Implementation>
      </Set>
    </Property>
    <Method Name="OnReset" Id="{bb27ab21-b4ba-4764-8874-8c81ccfebf0c}" FolderPath="Members to override\">
      <Declaration><![CDATA[METHOD PROTECTED OnReset ]]></Declaration>
      <Implementation>
        <ST><![CDATA[]]></ST>
      </Implementation>
    </Method>
    <Method Name="OnRunning" Id="{6dd0e9d8-af4c-4ffa-bd19-f8adf74f282d}" FolderPath="Members to override\">
      <Declaration><![CDATA[METHOD PROTECTED ABSTRACT OnRunning 
VAR_OUTPUT
	stopped : BOOL;
	stopReason : TASK_STOP_REASON;
END_VAR
VAR_IN_OUT
	stopReasonMessage : TOF_Core.ERROR_MESSAGE;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[]]></ST>
      </Implementation>
    </Method>
    <Method Name="OnStopped" Id="{50ea53b3-200a-461c-ab11-2f0c2b1dae40}" FolderPath="Members to override\">
      <Declaration><![CDATA[METHOD PROTECTED OnStopped
]]></Declaration>
      <Implementation>
        <ST><![CDATA[]]></ST>
      </Implementation>
    </Method>
    <Method Name="RequestCancel" Id="{2a9e1292-e28a-4dd3-94e3-162c35bf20f1}">
      <Declaration><![CDATA[METHOD FINAL RequestCancel : BOOL
VAR_OUTPUT
	refuseReason : WSTRING(255);
END_VAR
VAR
	exceptionCode : __SYSTEM.ExceptionCode;
	
	exception : IException;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF _State <> TASK_STATE.RUNNING THEN
	refuseReason := TOF_Core.WideStringHelper.ConcatStrings255("Expected state is '", TO_WSTRING(TASK_STATE.RUNNING), "' but current state is '", TO_WSTRING(_State), "'");
	RETURN;
END_IF

setStoppedState(TASK_STOP_REASON.EXTERNAL_STOP, "Cancelled by external logic");

RequestCancel := TRUE;]]></ST>
      </Implementation>
    </Method>
    <Method Name="RequestStart" Id="{9882b806-c3e8-4a3f-aaf2-ed11ba23d42b}">
      <Declaration><![CDATA[METHOD FINAL RequestStart : BOOL
VAR_OUTPUT
	refuseReason : WSTRING(255);
END_VAR
VAR
	stopped : BOOL;
	stopReason : TASK_STOP_REASON;
	exceptionCode : __SYSTEM.ExceptionCode;
END_VAR
VAR_INST
	stopReasonMessage : TOF_Core.ERROR_MESSAGE;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF _State <> TASK_STATE.IDLE THEN
	refuseReason := TOF_Core.WideStringHelper.ConcatStrings255("Expected state is '", TO_WSTRING(TASK_STATE.IDLE), "' but current state is '", TO_WSTRING(_State), "'");
	RETURN;
END_IF

setRunningState();

RequestStart := TRUE;]]></ST>
      </Implementation>
    </Method>
    <Method Name="Reset" Id="{0f2a4a27-8629-40c4-9efd-77f7df39221c}">
      <Declaration><![CDATA[METHOD FINAL Reset : BOOL
VAR_OUTPUT
	refuseReason : WSTRING(255);
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF _State = TASK_STATE.IDLE THEN
	Reset := TRUE;
	RETURN;
END_IF

IF _State = TASK_STATE.RUNNING THEN
	refuseReason := TOF_Core.WideStringHelper.ConcatStrings255("Expected state is '", TO_WSTRING(TASK_STATE.RUNNING), "' but current state is '", TO_WSTRING(_State), "'");
	RETURN;
END_IF
	
OnReset();
	
setIdleState();

Reset := TRUE;]]></ST>
      </Implementation>
    </Method>
    <Property Name="ResourceManager" Id="{3ffa0dab-be77-444f-aac1-3f851c0303ab}">
      <Declaration><![CDATA[PROPERTY ResourceManager : TOF_Core.IResourceManager]]></Declaration>
      <Get Name="Get" Id="{8259ec91-69bd-4d5f-a200-41ce2936c280}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[ResourceManager := _ResourceManager;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Property Name="Running" Id="{d518eb01-4363-42cc-ba1f-054fa911674c}" FolderPath="State flags\">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
PROPERTY FINAL Running : BOOL
]]></Declaration>
      <Get Name="Get" Id="{ec791bbc-65ee-464b-aed3-27af2a7e3b0d}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[Running := _State = TASK_STATE.RUNNING;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Method Name="setIdleState" Id="{a1fa6764-5ade-425d-9893-0d955ee5f698}" FolderPath="Service members\">
      <Declaration><![CDATA[METHOD PRIVATE FINAL setIdleState
VAR CONSTANT
	NEW_STATE : TASK_STATE := TASK_STATE.IDLE;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF _State <> NEW_STATE THEN
	LogManager.TryLogMessage(
		WideStringHelper.ConcatStrings255("Task '", Name, "' changed state from '", TO_WSTRING(_State), "' to '", TO_WSTRING(NEW_STATE), "'"), 
		LOG_CATEGORY.SUCCESS, 
		TO_WSTRING(ClassName), 
		"Task", 
		WideStringHelper.ConcatStrings255("Task.", TO_WSTRING(NEW_STATE))
	);

	_State := NEW_STATE;
	_StateChanged := TRUE;	
END_IF 

_RunningSubstate := TASK_RUNNING_SUBSTATE.PROCESSING;

_StopReason := TASK_STOP_REASON.SUCCESS;
_StopReasonMessage := "";	]]></ST>
      </Implementation>
    </Method>
    <Method Name="setRunningState" Id="{129b7bf1-6245-4cb5-8474-1ec71cd6cfe9}" FolderPath="Service members\">
      <Declaration><![CDATA[METHOD PRIVATE FINAL setRunningState
VAR CONSTANT
	NEW_STATE : TASK_STATE := TASK_STATE.RUNNING;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF _State <> NEW_STATE THEN
	LogManager.TryLogMessage(
		WideStringHelper.ConcatStrings255("Task '", Name, "' changed state from '", TO_WSTRING(_State), "' to '", TO_WSTRING(NEW_STATE), "'"), 
		LOG_CATEGORY.SUCCESS, 
		TO_WSTRING(ClassName), 
		"Task", 
		WideStringHelper.ConcatStrings255("Task.", TO_WSTRING(NEW_STATE))
	);

	_State := NEW_STATE;
	_StateChanged := TRUE;	
END_IF 

IF _ResourceManager <> 0 AND_THEN NOT _ResourceManager.ResourceAcquired THEN
	_RunningSubstate := TASK_RUNNING_SUBSTATE.RESOURCE_ACQUIRING;
ELSE
	_RunningSubstate := TASK_RUNNING_SUBSTATE.PROCESSING;
END_IF

_StopReason := TASK_STOP_REASON.SUCCESS;
_StopReasonMessage := "";	]]></ST>
      </Implementation>
    </Method>
    <Method Name="setRunningSubstate" Id="{86c45bf8-c257-4bb6-9508-60b7fb98931b}" FolderPath="Service members\">
      <Declaration><![CDATA[METHOD PRIVATE setRunningSubstate
VAR_INPUT
	runningSubState : TASK_RUNNING_SUBSTATE;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[_RunningSubstate := runningSubState;]]></ST>
      </Implementation>
    </Method>
    <Method Name="setStoppedState" Id="{701711d6-d83b-4c51-bbb9-f891989ade15}" FolderPath="Service members\">
      <Declaration><![CDATA[METHOD PRIVATE FINAL setStoppedState
VAR_INPUT
	stopReason : TASK_STOP_REASON;
END_VAR
VAR_IN_OUT CONSTANT
	stopReasonMessage : TOF_Core.ERROR_MESSAGE;
END_VAR
VAR CONSTANT
	NEW_STATE : TASK_STATE := TASK_STATE.STOPPED;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF _State <> NEW_STATE THEN
	LogManager.TryLogMessage(
		WideStringHelper.ConcatStrings255("Task '", Name, "' changed state from '", TO_WSTRING(_State), "' to '", TO_WSTRING(NEW_STATE), "'"), 
		LOG_CATEGORY.SUCCESS, 
		TO_WSTRING(ClassName), 
		"Task", 
		WideStringHelper.ConcatStrings255("Task.", TO_WSTRING(NEW_STATE))
	);

	_State := NEW_STATE;
	_StateChanged := TRUE;	
END_IF 

_StopReason := stopReason;

IF stopReasonMessage <> "" THEN
	_StopReasonMessage := stopReasonMessage;
ELSE
	CASE stopReason OF
		TASK_STOP_REASON.SUCCESS:
			_StopReasonMessage := "Successfully completed";
			
		TASK_STOP_REASON.EXTERNAL_STOP:
			_StopReasonMessage := "Stopped by external logic";

		TASK_STOP_REASON.INTERNAL_STOP:
			_StopReasonMessage := "Stopped by internal logic";

		TASK_STOP_REASON.FAILURE:
			_StopReasonMessage := "Exception occured";

		TASK_STOP_REASON.USER_DEFINED:		
			_StopReasonMessage := "User defined";
	END_CASE
END_IF	]]></ST>
      </Implementation>
    </Method>
    <Property Name="State" Id="{aea1c285-89e6-43e8-af45-f9790e917087}">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
PROPERTY State : TASK_STATE
]]></Declaration>
      <Get Name="Get" Id="{41100485-bb0e-4be9-be0c-0ed2f158a278}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[State := _State;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Property Name="StateChanged" Id="{600cdb42-d3b2-455a-b48f-83e96606c782}">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
PROPERTY FINAL StateChanged : BOOL]]></Declaration>
      <Get Name="Get" Id="{a6557db5-61fd-4ccb-85a4-b417b3791f73}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[StateChanged := _StateChanged;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Property Name="Stopped" Id="{3c595d3f-f42d-49d4-acc1-b271a058369a}" FolderPath="State flags\">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
PROPERTY FINAL Stopped : BOOL
]]></Declaration>
      <Get Name="Get" Id="{746f2357-c572-4244-992a-04736fd48af0}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[Stopped := _State = TASK_STATE.STOPPED;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Property Name="StopReason" Id="{67946b75-ded8-468e-a4e3-0a11a6a9934d}">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
PROPERTY FINAL StopReason : TASK_STOP_REASON]]></Declaration>
      <Get Name="Get" Id="{9c409cdb-d955-4a97-8035-cec1dbac6e4c}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[StopReason := _StopReason;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Property Name="StopReasonMessage" Id="{44677661-4a52-4007-92cf-654b73072a50}">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
PROPERTY FINAL StopReasonMessage : REFERENCE TO TOF_Core.ERROR_MESSAGE]]></Declaration>
      <Get Name="Get" Id="{7fd445a6-8c2c-4dde-9f29-ace905d6bc1b}">
        <Declaration><![CDATA[
]]></Declaration>
        <Implementation>
          <ST><![CDATA[StopReasonMessage REF= _StopReasonMessage;]]></ST>
        </Implementation>
      </Get>
    </Property>
  </POU>
</TcPlcObject>