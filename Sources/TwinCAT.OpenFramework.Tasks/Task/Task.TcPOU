<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1">
  <POU Name="Task" Id="{4f3cf4ac-1772-4e42-a725-ba88b4bb1e14}" SpecialFunc="None">
    <Declaration><![CDATA[(*

.. <legal notes>

legal notes
=================
| SPDX-FileCopyrightText: © 2026 Oleksandr Tiutyk <trofimich@gmail.com>
| SPDX-License-Identifier: LGPL-3.0-only
| For details check: LGPL-3.0-only_

.. _LGPL-3.0-only: https://www.gnu.org/licenses/lgpl-3.0.en.html

.. </legal notes>

*)

{attribute 'no_explicit_call' := 'do not call this function block directly'} 
FUNCTION_BLOCK ABSTRACT Task EXTENDS TOF_Core.Object IMPLEMENTS ITask
VAR
	_Name : WSTRING;
	
	_State : TASK_STATE;	
	_StateChanged : BOOL;
	
	_RunningSubstate : TASK_RUNNING_SUBSTATE;

	_OriginalStopReason : TASK_STOP_REASON;
	_OriginalStopReasonMessage : TOF_Core.ERROR_MESSAGE;
	_OriginalStopReasonObject : TaskStopReason(_OriginalStopReason, _OriginalStopReasonMessage);
	
	_FinalStopReason : TASK_STOP_REASON;	
	_FinalStopReasonMessage : TOF_Core.ERROR_MESSAGE;
	_FinalStopReasonObject : TaskStopReason(_FinalStopReason, _FinalStopReasonMessage);
	
	_StateObject : TaskState(THIS^);
	_RunningSubstateObject : TaskRunningSubstate(THIS^);
END_VAR]]></Declaration>
    <Implementation>
      <ST><![CDATA[]]></ST>
    </Implementation>
    <Folder Name="Members to override" Id="{e6ebeb0e-7c81-4b67-af2e-12cd935145f5}" />
    <Folder Name="Service members" Id="{fcccf993-7a8e-4ed0-ab1b-398389fd2b3c}" />
    <Method Name="Cancel" Id="{70542690-0d14-4f0e-bd58-570de50ff500}">
      <Declaration><![CDATA[METHOD FINAL Cancel : BOOL
VAR_OUTPUT
	refuseReason : TOF_Core.ERROR_MESSAGE;
END_VAR
VAR
	tmpResourceManager : TOF_Core.IResourceManager;
	
	exceptionCode : __SYSTEM.ExceptionCode;	
	exception : IException;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF _State <> TASK_STATE.RUNNING THEN
	refuseReason := TOF_Core.WideStrings.ConcatStrings255("Expected state is '", TO_WSTRING(TASK_STATE.RUNNING), "' but current state is '", TO_WSTRING(_State), "'");
	RETURN;
END_IF

IF _RunningSubstate = TASK_RUNNING_SUBSTATE.RESOURCE_RELEASING THEN
	refuseReason := "Task already stopping";
	RETURN;
END_IF

_OriginalStopReason := TASK_STOP_REASON.EXTERNAL_CANCEL;
_OriginalStopReasonMessage := "Cancelled by external logic";

safeCallOnBeforeStop();

tmpResourceManager := ResourceManager;

CASE _RunningSubstate OF
	TASK_RUNNING_SUBSTATE.RESOURCE_ACQUIRING:						
		__TRY
			setRunningSubstate(TASK_RUNNING_SUBSTATE.RESOURCE_RELEASING);

			tmpResourceManager.ReleaseResource();
			
			IF NOT tmpResourceManager.ResourceAcquired THEN
				setStoppedState(_OriginalStopReason, _OriginalStopReasonMessage);
				
				safeCallOnAfterStop();
			END_IF
		__CATCH(exceptionCode)
			exception := ExceptionManager.GetLastException(exceptionCode);

			IF tmpResourceManager = 0 OR_ELSE NOT tmpResourceManager.ResourceAcquired THEN				
				setStoppedState(TASK_STOP_REASON.FAILURE, exception.GetMessage());
				
				safeCallOnAfterStop();
			ELSE
				setRunningSubstate(TASK_RUNNING_SUBSTATE.RESOURCE_RELEASING);
			END_IF
		__ENDTRY
			
	TASK_RUNNING_SUBSTATE.PROCESSING:
		IF tmpResourceManager = 0 OR_ELSE NOT tmpResourceManager.ResourceAcquired THEN
			setStoppedState(_OriginalStopReason, _OriginalStopReasonMessage);
			
			safeCallOnAfterStop();
		ELSE				
			setRunningSubstate(TASK_RUNNING_SUBSTATE.RESOURCE_RELEASING);	
		END_IF			
END_CASE	
	
Cancel := TRUE;]]></ST>
      </Implementation>
    </Method>
    <Method Name="Execute" Id="{0c44d2a0-6aaa-455c-bf4e-d065a6e8ebfd}">
      <Declaration><![CDATA[METHOD FINAL Execute ]]></Declaration>
      <Implementation>
        <ST><![CDATA[_StateChanged := FALSE;

IF _State = TASK_STATE.RUNNING THEN
	handleRunning();
END_IF]]></ST>
      </Implementation>
    </Method>
    <Property Name="FinalStopReason" Id="{2700e19c-2cd2-4878-bd36-35fddbc83862}">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
PROPERTY FINAL FinalStopReason : REFERENCE TO TaskStopReason]]></Declaration>
      <Get Name="Get" Id="{4683eaba-a5e9-4ae8-8977-247101cb64d5}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[FinalStopReason REF= _FinalStopReasonObject;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Method Name="GetProcessingStateByStopReason" Id="{2c7b7363-a7d0-4c80-8f77-2fdbbb832cf9}" FolderPath="Service members\">
      <Declaration><![CDATA[METHOD PROTECTED GetProcessingStateByStopReason : TASK_PROCESSING_STATE
VAR_INPUT
	taskStopReason : TASK_STOP_REASON;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[CASE taskStopReason OF
	TASK_STOP_REASON.WORK_DONE:
		GetProcessingStateByStopReason := TASK_PROCESSING_STATE.DONE;
	
    TASK_STOP_REASON.INTERNAL_ABORT, TASK_STOP_REASON.EXTERNAL_CANCEL:
		GetProcessingStateByStopReason := TASK_PROCESSING_STATE.ABORTED;
	
    TASK_STOP_REASON.FAILURE:
		GetProcessingStateByStopReason := TASK_PROCESSING_STATE.ERROR;
END_CASE ]]></ST>
      </Implementation>
    </Method>
    <Method Name="GetStopReasonByProcessingState" Id="{c77bcf33-94ab-426f-a1b5-c4dac370b49f}" FolderPath="Service members\">
      <Declaration><![CDATA[METHOD PROTECTED GetStopReasonByProcessingState : TASK_STOP_REASON
VAR_INPUT
	taskpPocessingState : TASK_PROCESSING_STATE;
END_VAR
VAR
	invalidTaskRunningResult : TOF_Core.ArgumentOutOfRangeException;
	
	taskRunningResultName : STRING(16);
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[CASE taskpPocessingState OF
	TASK_PROCESSING_STATE.DONE:
		GetStopReasonByProcessingState := TASK_STOP_REASON.WORK_DONE;
	
    TASK_PROCESSING_STATE.ABORTED:
		GetStopReasonByProcessingState := TASK_STOP_REASON.INTERNAL_ABORT;
		
    TASK_PROCESSING_STATE.ERROR:
		GetStopReasonByProcessingState := TASK_STOP_REASON.FAILURE;
	
	ELSE
		taskRunningResultName := TO_STRING(taskpPocessingState);
		invalidTaskRunningResult.ThrowByObject(THIS^, __POUNAME(), __POSITION(), 'taskpPocessingState', taskRunningResultName);
END_CASE
 ]]></ST>
      </Implementation>
    </Method>
    <Method Name="handleRunning" Id="{0c0b3c2d-2039-40cf-90c1-1bc48ceedfa2}" FolderPath="Service members\">
      <Declaration><![CDATA[METHOD PRIVATE handleRunning
VAR
	runningResult : TASK_PROCESSING_STATE;
	
	tmpResourceManager : TOF_Core.IResourceManager;

	exceptionCode : __SYSTEM.ExceptionCode;	
	exception : TOF_Core.IException;
	
	errorMessage : TOF_Core.ERROR_MESSAGE;	
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[tmpResourceManager := ResourceManager;

CASE _RunningSubstate OF
	TASK_RUNNING_SUBSTATE.RESOURCE_ACQUIRING:
		__TRY
			tmpResourceManager.AcquireResource();
			
			IF tmpResourceManager.ResourceAcquired THEN
				setRunningSubstate(TASK_RUNNING_SUBSTATE.PROCESSING);
			END_IF
		__CATCH(exceptionCode)
			exception := ExceptionManager.GetLastException(exceptionCode);
			errorMessage := exception.GetMessage();
			
			IF _OriginalStopReason = TASK_STOP_REASON.WORK_DONE THEN
				_OriginalStopReason := TASK_STOP_REASON.FAILURE;
				_OriginalStopReasonMessage := errorMessage;				
			END_IF

			safeCallOnBeforeStop();
					
			IF tmpResourceManager = 0 OR_ELSE NOT tmpResourceManager.ResourceAcquired THEN				
				setStoppedState(TASK_STOP_REASON.FAILURE, errorMessage);
				
				safeCallOnAfterStop();
			ELSE
				setRunningSubstate(TASK_RUNNING_SUBSTATE.RESOURCE_RELEASING);
			END_IF
		__ENDTRY
	
	TASK_RUNNING_SUBSTATE.PROCESSING:
		__TRY
			OnRunning(processingState => runningResult, _OriginalStopReasonMessage);
	
			IF runningResult <> TASK_PROCESSING_STATE.BUSY THEN
				_OriginalStopReason := GetStopReasonByProcessingState(runningResult);
				
				safeCallOnBeforeStop();
				
				IF tmpResourceManager = 0 OR_ELSE NOT tmpResourceManager.ResourceAcquired THEN
					setStoppedState(_OriginalStopReason, _OriginalStopReasonMessage);
	
					safeCallOnAfterStop();
				ELSE
					setRunningSubstate(TASK_RUNNING_SUBSTATE.RESOURCE_RELEASING);
				END_IF
			END_IF
		__CATCH(exceptionCode)
			exception := ExceptionManager.GetLastException(exceptionCode);
			errorMessage := exception.GetMessage();
			
			IF _OriginalStopReason = TASK_STOP_REASON.WORK_DONE THEN
				_OriginalStopReason := TASK_STOP_REASON.FAILURE;
				_OriginalStopReasonMessage := errorMessage;				
			END_IF
					
			safeCallOnBeforeStop();
			
			IF tmpResourceManager = 0 OR_ELSE NOT tmpResourceManager.ResourceAcquired THEN
				setStoppedState(_OriginalStopReason, _OriginalStopReasonMessage);	

				safeCallOnAfterStop();
			ELSE
				setRunningSubstate(TASK_RUNNING_SUBSTATE.RESOURCE_RELEASING);
			END_IF
		__ENDTRY
	
	TASK_RUNNING_SUBSTATE.RESOURCE_RELEASING:
		__TRY
			tmpResourceManager.ReleaseResource();
			
			IF NOT tmpResourceManager.ResourceAcquired THEN
				setStoppedState(_OriginalStopReason, _OriginalStopReasonMessage);
				
				safeCallOnAfterStop();
			END_IF				
		__CATCH(exceptionCode)
			exception := ExceptionManager.GetLastException(exceptionCode);
			errorMessage := exception.GetMessage();
			
			IF _OriginalStopReason = TASK_STOP_REASON.WORK_DONE THEN
				_OriginalStopReason := TASK_STOP_REASON.FAILURE;
				_OriginalStopReasonMessage := errorMessage;				
			END_IF
					
			setStoppedState(TASK_STOP_REASON.FAILURE, errorMessage);
				
			safeCallOnAfterStop();
		__ENDTRY
END_CASE	]]></ST>
      </Implementation>
    </Method>
    <Property Name="Name" Id="{4e451f29-f53d-4bcf-8f0a-083cf67f553b}">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
PROPERTY FINAL Name : WSTRING]]></Declaration>
      <Get Name="Get" Id="{758bcc09-602f-41b4-b11f-e8268c4f3c3a}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[Name := SEL(_Name = "", _Name, SUPER^.Name);]]></ST>
        </Implementation>
      </Get>
      <Set Name="Set" Id="{2b105bad-2d16-451e-8bbb-21108ae21d6d}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[_Name := Name;]]></ST>
        </Implementation>
      </Set>
    </Property>
    <Method Name="OnAfterStop" Id="{86e85a1b-93eb-4520-a6b2-faaa5a3db20e}" FolderPath="Members to override\">
      <Declaration><![CDATA[METHOD PROTECTED OnAfterStop
]]></Declaration>
      <Implementation>
        <ST><![CDATA[]]></ST>
      </Implementation>
    </Method>
    <Method Name="OnBeforeStart" Id="{5fb86c1f-5c4e-4feb-b7c4-caeef1bc2031}" FolderPath="Members to override\">
      <Declaration><![CDATA[METHOD PROTECTED OnBeforeStart]]></Declaration>
      <Implementation>
        <ST><![CDATA[]]></ST>
      </Implementation>
    </Method>
    <Method Name="OnBeforeStop" Id="{698786bc-ab3a-40d9-bc3c-8b44edd3a764}" FolderPath="Members to override\">
      <Declaration><![CDATA[METHOD PROTECTED OnBeforeStop
]]></Declaration>
      <Implementation>
        <ST><![CDATA[]]></ST>
      </Implementation>
    </Method>
    <Method Name="OnReset" Id="{a7e8974b-7e43-4ce5-8790-6b98c2babadd}" FolderPath="Members to override\">
      <Declaration><![CDATA[METHOD PROTECTED OnReset ]]></Declaration>
      <Implementation>
        <ST><![CDATA[]]></ST>
      </Implementation>
    </Method>
    <Method Name="OnRunning" Id="{fa525f37-9567-4dcb-9fc7-ad672f39a641}" FolderPath="Members to override\">
      <Declaration><![CDATA[METHOD PROTECTED ABSTRACT OnRunning 
VAR_OUTPUT
	processingState : TASK_PROCESSING_STATE;
END_VAR
VAR_IN_OUT
	stopReasonMessage : TOF_Core.ERROR_MESSAGE;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[]]></ST>
      </Implementation>
    </Method>
    <Property Name="OriginalStopReason" Id="{584c09b9-cc15-4863-851e-295e1f3855a3}">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
PROPERTY FINAL OriginalStopReason : REFERENCE TO TaskStopReason]]></Declaration>
      <Get Name="Get" Id="{f76dfe1f-2858-4323-89c5-fdbe686b4c10}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[OriginalStopReason REF= _OriginalStopReasonObject;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Method Name="Reset" Id="{98e72552-cae8-4b2c-b8c6-8047c79b882a}">
      <Declaration><![CDATA[METHOD FINAL Reset : BOOL
VAR_OUTPUT
	refuseReason : TOF_Core.ERROR_MESSAGE;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF _State = TASK_STATE.IDLE THEN
	Reset := TRUE;
	RETURN;
END_IF

IF _State = TASK_STATE.RUNNING THEN
	refuseReason := TOF_Core.WideStrings.ConcatStrings255("Expected state is '", TO_WSTRING(TASK_STATE.RUNNING), "' but current state is '", TO_WSTRING(_State), "'");
	RETURN;
END_IF
	
IF safeCallOnReset(refuseReason) THEN
	setIdleState();

	Reset := TRUE;
END_IF]]></ST>
      </Implementation>
    </Method>
    <Property Name="ResourceManager" Id="{d940cf50-89bd-4d74-bb1c-dbd9609c4f14}">
      <Declaration><![CDATA[PROPERTY ResourceManager : TOF_Core.IResourceManager]]></Declaration>
      <Get Name="Get" Id="{eb1a2806-2821-4891-9a94-be06e63069cb}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Property Name="RunningSubstate" Id="{66935ac7-34a7-4c86-b67d-8cd08feeed21}">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
PROPERTY FINAL RunningSubstate : REFERENCE TO TaskRunningSubstate
]]></Declaration>
      <Get Name="Get" Id="{7f57d865-76a7-4394-ab4e-08fffeb4344f}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[RunningSubstate REF= _RunningSubstateObject;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Method Name="safeCallOnAfterStop" Id="{0c327d25-c1a4-490a-bffb-4b7b77fe9c81}" FolderPath="Service members\">
      <Declaration><![CDATA[METHOD PRIVATE safeCallOnAfterStop]]></Declaration>
      <Implementation>
        <ST><![CDATA[__TRY
	OnAfterStop();
__CATCH
__ENDTRY]]></ST>
      </Implementation>
    </Method>
    <Method Name="safeCallOnBeforeStart" Id="{4d5ec1be-1747-41c1-b234-c3cc18f33f59}" FolderPath="Service members\">
      <Declaration><![CDATA[METHOD PRIVATE safeCallOnBeforeStart : BOOL
VAR_IN_OUT
	refuseReason : TOF_Core.ERROR_MESSAGE;
END_VAR
VAR
	exceptionCode : __SYSTEM.ExceptionCode;	
	exception : TOF_Core.IException;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[__TRY
	OnBeforeStart();
	
	safeCallOnBeforeStart := TRUE;
__CATCH(exceptionCode)
	exception := ExceptionManager.GetLastException(exceptionCode);
	refuseReason := exception.GetMessage();
__ENDTRY]]></ST>
      </Implementation>
    </Method>
    <Method Name="safeCallOnBeforeStop" Id="{793a8a6d-c6e3-4043-9860-ceb426a80e28}" FolderPath="Service members\">
      <Declaration><![CDATA[METHOD PRIVATE safeCallOnBeforeStop]]></Declaration>
      <Implementation>
        <ST><![CDATA[__TRY
	OnBeforeStop();
__CATCH
__ENDTRY]]></ST>
      </Implementation>
    </Method>
    <Method Name="safeCallOnReset" Id="{15a9e5da-1c2d-41c3-b3ce-bb3ffe792a82}" FolderPath="Service members\">
      <Declaration><![CDATA[METHOD PRIVATE safeCallOnReset : BOOL
VAR_IN_OUT
	refuseReason : TOF_Core.ERROR_MESSAGE;
END_VAR
VAR
	exceptionCode : __SYSTEM.ExceptionCode;	
	exception : TOF_Core.IException;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[__TRY
	OnReset();
	
	safeCallOnReset := TRUE;
__CATCH(exceptionCode)
	exception := ExceptionManager.GetLastException(exceptionCode);
	refuseReason := exception.GetMessage();
__ENDTRY]]></ST>
      </Implementation>
    </Method>
    <Method Name="setIdleState" Id="{d671c618-6e83-4250-9051-fb8450c86902}" FolderPath="Service members\">
      <Declaration><![CDATA[METHOD PRIVATE FINAL setIdleState
VAR CONSTANT
	NEW_STATE : TASK_STATE := TASK_STATE.IDLE;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF _State <> NEW_STATE THEN
	LogManager.TryLogMessage(
		WideStrings.ConcatStrings255("Task '", Name, "' changed state from '", TO_WSTRING(_State), "' to '", TO_WSTRING(NEW_STATE), "'"), 
		LOG_CATEGORY.SUCCESS, 
		TO_WSTRING(ClassName), 
		"Task", 
		WideStrings.ConcatStrings255("Task.", TO_WSTRING(NEW_STATE))
	);

	_State := NEW_STATE;
	_StateChanged := TRUE;	
END_IF 

_RunningSubstate := TASK_RUNNING_SUBSTATE.PROCESSING;

_OriginalStopReason := TASK_STOP_REASON.WORK_DONE;
_OriginalStopReasonMessage := "";	

_FinalStopReason := TASK_STOP_REASON.WORK_DONE;
_FinalStopReasonMessage := "";	]]></ST>
      </Implementation>
    </Method>
    <Method Name="setRunningState" Id="{1d4e1480-16f7-42b7-b601-af024b62cfc3}" FolderPath="Service members\">
      <Declaration><![CDATA[METHOD PRIVATE FINAL setRunningState
VAR CONSTANT
	NEW_STATE : TASK_STATE := TASK_STATE.RUNNING;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF _State <> NEW_STATE THEN
	LogManager.TryLogMessage(
		WideStrings.ConcatStrings255("Task '", Name, "' changed state from '", TO_WSTRING(_State), "' to '", TO_WSTRING(NEW_STATE), "'"), 
		LOG_CATEGORY.SUCCESS, 
		TO_WSTRING(ClassName), 
		"Task", 
		WideStrings.ConcatStrings255("Task.", TO_WSTRING(NEW_STATE))
	);

	_State := NEW_STATE;
	_StateChanged := TRUE;	
END_IF 

IF ResourceManager <> 0 AND_THEN NOT ResourceManager.ResourceAcquired THEN
	_RunningSubstate := TASK_RUNNING_SUBSTATE.RESOURCE_ACQUIRING;
ELSE
	_RunningSubstate := TASK_RUNNING_SUBSTATE.PROCESSING;
END_IF

_OriginalStopReason := TASK_STOP_REASON.WORK_DONE;
_OriginalStopReasonMessage := "";	

_FinalStopReason := TASK_STOP_REASON.WORK_DONE;
_FinalStopReasonMessage := "";		]]></ST>
      </Implementation>
    </Method>
    <Method Name="setRunningSubstate" Id="{d7f6ee1f-b4e3-4b35-9d2a-d0008766d37e}" FolderPath="Service members\">
      <Declaration><![CDATA[METHOD PRIVATE setRunningSubstate
VAR_INPUT
	runningSubState : TASK_RUNNING_SUBSTATE;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[_RunningSubstate := runningSubState;]]></ST>
      </Implementation>
    </Method>
    <Method Name="setStoppedState" Id="{8d421360-d406-412c-baae-5d7b7f498ca6}" FolderPath="Service members\">
      <Declaration><![CDATA[METHOD PRIVATE FINAL setStoppedState
VAR_INPUT
	finalStopReason : TASK_STOP_REASON;
	finalStopReasonMessage : TOF_Core.ERROR_MESSAGE;
END_VAR
VAR CONSTANT
	NEW_STATE : TASK_STATE := TASK_STATE.STOPPED;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF _State <> NEW_STATE THEN
	LogManager.TryLogMessage(
		WideStrings.ConcatStrings255("Task '", Name, "' changed state from '", TO_WSTRING(_State), "' to '", TO_WSTRING(NEW_STATE), "'"), 
		LOG_CATEGORY.SUCCESS, 
		TO_WSTRING(ClassName), 
		"Task", 
		WideStrings.ConcatStrings255("Task.", TO_WSTRING(NEW_STATE))
	);

	_State := NEW_STATE;
	_StateChanged := TRUE;	
END_IF 

_FinalStopReason := finalStopReason;

IF finalStopReasonMessage <> "" THEN
	_FinalStopReasonMessage := finalStopReasonMessage;
ELSE
	IF _OriginalStopReasonMessage <> "" THEN
		_FinalStopReasonMessage := _OriginalStopReasonMessage;
	ELSE
		CASE _FinalStopReason OF
			TASK_STOP_REASON.WORK_DONE:
				_FinalStopReasonMessage := "Work done";
		
			TASK_STOP_REASON.INTERNAL_ABORT:
				_FinalStopReasonMessage := "Stopped by internal logic";
				
			TASK_STOP_REASON.EXTERNAL_CANCEL:
				_FinalStopReasonMessage := "Stopped by external logic";
		
			TASK_STOP_REASON.FAILURE:
				_FinalStopReasonMessage := "Exception occured";
		END_CASE	
	END_IF
END_IF]]></ST>
      </Implementation>
    </Method>
    <Method Name="Start" Id="{ce59c237-31f2-4c66-aa57-58d7b3210a0c}">
      <Declaration><![CDATA[METHOD FINAL Start : BOOL
VAR_OUTPUT
	refuseReason : TOF_Core.ERROR_MESSAGE;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF _State <> TASK_STATE.IDLE THEN
	refuseReason := TOF_Core.WideStrings.ConcatStrings255("Expected state is '", TO_WSTRING(TASK_STATE.IDLE), "' but current state is '", TO_WSTRING(_State), "'");
	RETURN;
END_IF

IF safeCallOnBeforeStart(refuseReason) THEN
	setRunningState();

	handleRunning();

	Start := TRUE;
END_IF]]></ST>
      </Implementation>
    </Method>
    <Property Name="State" Id="{dafc5e3d-e24c-471f-9fe0-3ccb7b88ba9e}">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
PROPERTY FINAL State : REFERENCE TO TaskState
]]></Declaration>
      <Get Name="Get" Id="{194ca53a-f3b4-4af6-941f-9e148bc75ff7}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[State REF= _StateObject;]]></ST>
        </Implementation>
      </Get>
    </Property>
  </POU>
</TcPlcObject>