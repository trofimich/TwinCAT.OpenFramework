<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1">
  <POU Name="Task" Id="{2881da81-273b-4545-b65d-3babc98f9385}" SpecialFunc="None">
    <Declaration><![CDATA[(*

.. <legal notes>

legal notes
=================
| SPDX-FileCopyrightText: © 2025 Oleksandr Tiutyk <trofimich@gmail.com>
| SPDX-License-Identifier: LGPL-3.0-only
| For details check: LGPL-3.0-only_

.. _LGPL-3.0-only: https://www.gnu.org/licenses/lgpl-3.0.en.html

.. </legal notes>

*)

{attribute 'no_explicit_call' := 'do not call this function block directly'} 
FUNCTION_BLOCK ABSTRACT Task EXTENDS TOF_Core.Object IMPLEMENTS ITask
VAR
	_Name : WSTRING;
	
	_State : TASK_STATE;	
	_StateChanged : BOOL;
	
	_RunningSubstate : TASK_RUNNING_SUBSTATE;

	_StopReason : TASK_STOP_REASON;
	_StopReasonMessage : TOF_Core.ERROR_MESSAGE;
	
	_LastExceptionMessage : TOF_Core.ERROR_MESSAGE;
	
	_StateObject : TaskState(THIS^);
	_RunningSubstateObject : TaskRunningSubstate(THIS^);
	_StopReasonObject : TaskStopReason(THIS^);
END_VAR]]></Declaration>
    <Implementation>
      <ST><![CDATA[]]></ST>
    </Implementation>
    <Folder Name="Members to override" Id="{db2f5785-7161-42e1-b407-390855af59c1}" />
    <Folder Name="Service members" Id="{edd1c5da-fac4-4215-94b9-14921518865c}" />
    <Method Name="Cancel" Id="{2a9e1292-e28a-4dd3-94e3-162c35bf20f1}">
      <Declaration><![CDATA[METHOD FINAL Cancel : BOOL
VAR_OUTPUT
	refuseReason : WSTRING(255);
END_VAR
VAR
	exceptionCode : __SYSTEM.ExceptionCode;
	
	exception : IException;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF _State <> TASK_STATE.RUNNING THEN
	refuseReason := TOF_Core.WideStringHelper.ConcatStrings255("Expected state is '", TO_WSTRING(TASK_STATE.RUNNING), "' but current state is '", TO_WSTRING(_State), "'");
	RETURN;
END_IF

setStoppedState();

_StopReason := TASK_STOP_REASON.EXTERNAL_LOGIC;
_StopReasonMessage := "Cancelled by external logic";

handleStop();

Cancel := TRUE;]]></ST>
      </Implementation>
    </Method>
    <Method Name="Execute" Id="{7d3bbbc2-d8e4-48ed-be78-b8da5dd67256}">
      <Declaration><![CDATA[METHOD FINAL Execute ]]></Declaration>
      <Implementation>
        <ST><![CDATA[_StateChanged := FALSE;

IF _State = TASK_STATE.RUNNING THEN
	handleRunning();
END_IF]]></ST>
      </Implementation>
    </Method>
    <Method Name="GetProcessingStateByStopReason" Id="{9962f8db-5572-44bf-849c-6a20a87e913d}" FolderPath="Service members\">
      <Declaration><![CDATA[METHOD PROTECTED GetProcessingStateByStopReason : TASK_PROCESSING_STATE
VAR_INPUT
	taskStopReason : TASK_STOP_REASON;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[CASE taskStopReason OF
	TASK_STOP_REASON.SUCCESS:
		GetProcessingStateByStopReason := TASK_PROCESSING_STATE.SUCCESS;
	
    TASK_STOP_REASON.EXTERNAL_LOGIC:
		GetProcessingStateByStopReason := TASK_PROCESSING_STATE.EXTERNAL_STOP;
	
    TASK_STOP_REASON.INTERNAL_LOGIC:
		GetProcessingStateByStopReason := TASK_PROCESSING_STATE.INTERNAL_STOP;
	
    TASK_STOP_REASON.FAILURE:
		GetProcessingStateByStopReason := TASK_PROCESSING_STATE.FAILURE;
	
	TASK_STOP_REASON.USER_DEFINED:
		GetProcessingStateByStopReason := TASK_PROCESSING_STATE.USER_DEFINED;
END_CASE ]]></ST>
      </Implementation>
    </Method>
    <Method Name="GetStopReasonByProcessingState" Id="{fda3bf18-557f-4243-8262-1f6978f3854e}" FolderPath="Service members\">
      <Declaration><![CDATA[METHOD PROTECTED GetStopReasonByProcessingState : TASK_STOP_REASON
VAR_INPUT
	taskpPocessingState : TASK_PROCESSING_STATE;
END_VAR
VAR
	invalidTaskRunningResult : TOF_Core.ArgumentOutOfRangeException;
	
	taskRunningResultName : STRING(16);
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[CASE taskpPocessingState OF
	TASK_PROCESSING_STATE.SUCCESS:
		GetStopReasonByProcessingState := TASK_STOP_REASON.SUCCESS;
	
    TASK_PROCESSING_STATE.EXTERNAL_STOP:
		GetStopReasonByProcessingState := TASK_STOP_REASON.EXTERNAL_LOGIC;
	
    TASK_PROCESSING_STATE.INTERNAL_STOP:
		GetStopReasonByProcessingState := TASK_STOP_REASON.INTERNAL_LOGIC;
	
    TASK_PROCESSING_STATE.FAILURE:
		GetStopReasonByProcessingState := TASK_STOP_REASON.FAILURE;
	
	TASK_PROCESSING_STATE.USER_DEFINED:
		GetStopReasonByProcessingState := TASK_STOP_REASON.USER_DEFINED;
	
	ELSE
		taskRunningResultName := TO_STRING(taskpPocessingState);
		invalidTaskRunningResult.Throw(NamespaceName, ClassName, __POUNAME(), __POSITION(), 'taskpPocessingState', taskRunningResultName);
END_CASE
 ]]></ST>
      </Implementation>
    </Method>
    <Method Name="handleRunning" Id="{ccb582d9-8390-4648-b568-59dfb083e837}" FolderPath="Service members\">
      <Declaration><![CDATA[METHOD PRIVATE handleRunning
VAR
	runningResult : TASK_PROCESSING_STATE;

	exceptionCode : __SYSTEM.ExceptionCode;
	
	exception : TOF_Core.IException;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[__TRY
	CASE _RunningSubstate OF
		TASK_RUNNING_SUBSTATE.RESOURCE_ACQUIRING:
			ResourceManager.AcquireResource();
			
			IF ResourceManager.ResourceAcquired THEN
				setRunningSubstate(TASK_RUNNING_SUBSTATE.PROCESSING);
			END_IF
		
		TASK_RUNNING_SUBSTATE.PROCESSING:
			OnRunning(processingState => runningResult, _StopReasonMessage);
	
			IF runningResult <> TASK_PROCESSING_STATE.IN_PROGRESS THEN
				_StopReason := GetStopReasonByProcessingState(runningResult);
				
				IF ResourceManager = 0 OR_ELSE NOT ResourceManager.ResourceAcquired THEN
					setStoppedState();				
				ELSE
					setRunningSubstate(TASK_RUNNING_SUBSTATE.RESOURCE_RELEASING);
				END_IF
			END_IF
		
		TASK_RUNNING_SUBSTATE.RESOURCE_RELEASING:
			ResourceManager.ReleaseResource();
			
			IF NOT ResourceManager.ResourceAcquired THEN
				setStoppedState();				
			END_IF				
	END_CASE	
__CATCH(exceptionCode)
	exception := ExceptionManager.GetLastException(exceptionCode);
	
	_LastExceptionMessage := exception.GetMessage();
	
	// save original stop reason if exists
	IF _StopReason = TASK_STOP_REASON.SUCCESS THEN
		_StopReason := TASK_STOP_REASON.FAILURE;
		_StopReasonMessage := _LastExceptionMessage;		
	END_IF
			
	IF ResourceManager = 0 OR_ELSE NOT ResourceManager.ResourceAcquired THEN
		setStoppedState();				
	ELSE
		setRunningSubstate(TASK_RUNNING_SUBSTATE.RESOURCE_RELEASING);
	END_IF
__ENDTRY]]></ST>
      </Implementation>
    </Method>
    <Method Name="handleStop" Id="{b198c32e-de99-475e-bf73-776ec44e7987}" FolderPath="Service members\">
      <Declaration><![CDATA[METHOD PRIVATE handleStop
VAR
	runningResult : TASK_PROCESSING_STATE;

	exceptionCode : __SYSTEM.ExceptionCode;

	exception : IException;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[__TRY
	CASE _RunningSubstate OF
		TASK_RUNNING_SUBSTATE.RESOURCE_ACQUIRING:
			setRunningSubstate(TASK_RUNNING_SUBSTATE.RESOURCE_RELEASING);
		
		TASK_RUNNING_SUBSTATE.PROCESSING:
			OnStop();

			IF ResourceManager = 0 OR_ELSE NOT ResourceManager.ResourceAcquired THEN
				setStoppedState();				
			ELSE
				setRunningSubstate(TASK_RUNNING_SUBSTATE.RESOURCE_RELEASING);
			END_IF			
	END_CASE	
__CATCH(exceptionCode)
	exception := ExceptionManager.GetLastException(exceptionCode);
	
	_LastExceptionMessage := exception.GetMessage();
	
	IF ResourceManager = 0 OR_ELSE NOT ResourceManager.ResourceAcquired THEN
		setStoppedState();				
	ELSE
		setRunningSubstate(TASK_RUNNING_SUBSTATE.RESOURCE_RELEASING);
	END_IF
__ENDTRY
]]></ST>
      </Implementation>
    </Method>
    <Property Name="Name" Id="{6e123c98-9b28-4ea6-9d42-c3fcc2d44b13}">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
PROPERTY FINAL Name : WSTRING]]></Declaration>
      <Get Name="Get" Id="{cc73f3bf-4ae8-4386-8341-9813f1a49820}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[Name := SEL(_Name = "", _Name, SUPER^.Name);]]></ST>
        </Implementation>
      </Get>
      <Set Name="Set" Id="{eeffa16a-a40c-49e7-a894-c1df81dfe2e7}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[_Name := Name;]]></ST>
        </Implementation>
      </Set>
    </Property>
    <Method Name="OnReset" Id="{bb27ab21-b4ba-4764-8874-8c81ccfebf0c}" FolderPath="Members to override\">
      <Declaration><![CDATA[METHOD PROTECTED OnReset ]]></Declaration>
      <Implementation>
        <ST><![CDATA[]]></ST>
      </Implementation>
    </Method>
    <Method Name="OnRunning" Id="{6dd0e9d8-af4c-4ffa-bd19-f8adf74f282d}" FolderPath="Members to override\">
      <Declaration><![CDATA[METHOD PROTECTED ABSTRACT OnRunning 
VAR_OUTPUT
	processingState : TASK_PROCESSING_STATE;
END_VAR
VAR_IN_OUT
	stopReasonMessage : TOF_Core.ERROR_MESSAGE;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[]]></ST>
      </Implementation>
    </Method>
    <Method Name="OnStop" Id="{50ea53b3-200a-461c-ab11-2f0c2b1dae40}" FolderPath="Members to override\">
      <Declaration><![CDATA[METHOD PROTECTED OnStop
]]></Declaration>
      <Implementation>
        <ST><![CDATA[]]></ST>
      </Implementation>
    </Method>
    <Method Name="Reset" Id="{0f2a4a27-8629-40c4-9efd-77f7df39221c}">
      <Declaration><![CDATA[METHOD FINAL Reset : BOOL
VAR_OUTPUT
	refuseReason : WSTRING(255);
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF _State = TASK_STATE.IDLE THEN
	Reset := TRUE;
	RETURN;
END_IF

IF _State = TASK_STATE.RUNNING THEN
	refuseReason := TOF_Core.WideStringHelper.ConcatStrings255("Expected state is '", TO_WSTRING(TASK_STATE.RUNNING), "' but current state is '", TO_WSTRING(_State), "'");
	RETURN;
END_IF
	
OnReset();
	
setIdleState();

Reset := TRUE;]]></ST>
      </Implementation>
    </Method>
    <Property Name="ResourceManager" Id="{3ffa0dab-be77-444f-aac1-3f851c0303ab}">
      <Declaration><![CDATA[PROPERTY ResourceManager : TOF_Core.IResourceManager]]></Declaration>
      <Get Name="Get" Id="{8259ec91-69bd-4d5f-a200-41ce2936c280}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Property Name="RunningSubstate" Id="{1074f9f0-899f-47f3-b104-32c726f902e3}">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
PROPERTY FINAL RunningSubstate : REFERENCE TO TaskRunningSubstate
]]></Declaration>
      <Get Name="Get" Id="{eaa3499c-033d-41bd-8266-6b82e372a9fe}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[RunningSubstate REF= _RunningSubstateObject;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Method Name="setIdleState" Id="{a1fa6764-5ade-425d-9893-0d955ee5f698}" FolderPath="Service members\">
      <Declaration><![CDATA[METHOD PRIVATE FINAL setIdleState
VAR CONSTANT
	NEW_STATE : TASK_STATE := TASK_STATE.IDLE;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF _State <> NEW_STATE THEN
	LogManager.TryLogMessage(
		WideStringHelper.ConcatStrings255("Task '", Name, "' changed state from '", TO_WSTRING(_State), "' to '", TO_WSTRING(NEW_STATE), "'"), 
		LOG_CATEGORY.SUCCESS, 
		TO_WSTRING(ClassName), 
		"Task", 
		WideStringHelper.ConcatStrings255("Task.", TO_WSTRING(NEW_STATE))
	);

	_State := NEW_STATE;
	_StateChanged := TRUE;	
END_IF 

_RunningSubstate := TASK_RUNNING_SUBSTATE.PROCESSING;

_StopReason := TASK_STOP_REASON.SUCCESS;
_StopReasonMessage := "";	]]></ST>
      </Implementation>
    </Method>
    <Method Name="setRunningState" Id="{129b7bf1-6245-4cb5-8474-1ec71cd6cfe9}" FolderPath="Service members\">
      <Declaration><![CDATA[METHOD PRIVATE FINAL setRunningState
VAR CONSTANT
	NEW_STATE : TASK_STATE := TASK_STATE.RUNNING;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF _State <> NEW_STATE THEN
	LogManager.TryLogMessage(
		WideStringHelper.ConcatStrings255("Task '", Name, "' changed state from '", TO_WSTRING(_State), "' to '", TO_WSTRING(NEW_STATE), "'"), 
		LOG_CATEGORY.SUCCESS, 
		TO_WSTRING(ClassName), 
		"Task", 
		WideStringHelper.ConcatStrings255("Task.", TO_WSTRING(NEW_STATE))
	);

	_State := NEW_STATE;
	_StateChanged := TRUE;	
END_IF 

IF ResourceManager <> 0 AND_THEN NOT ResourceManager.ResourceAcquired THEN
	_RunningSubstate := TASK_RUNNING_SUBSTATE.RESOURCE_ACQUIRING;
ELSE
	_RunningSubstate := TASK_RUNNING_SUBSTATE.PROCESSING;
END_IF

_StopReason := TASK_STOP_REASON.SUCCESS;
_StopReasonMessage := "";	]]></ST>
      </Implementation>
    </Method>
    <Method Name="setRunningSubstate" Id="{86c45bf8-c257-4bb6-9508-60b7fb98931b}" FolderPath="Service members\">
      <Declaration><![CDATA[METHOD PRIVATE setRunningSubstate
VAR_INPUT
	runningSubState : TASK_RUNNING_SUBSTATE;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[_RunningSubstate := runningSubState;]]></ST>
      </Implementation>
    </Method>
    <Method Name="setStoppedState" Id="{701711d6-d83b-4c51-bbb9-f891989ade15}" FolderPath="Service members\">
      <Declaration><![CDATA[METHOD PRIVATE FINAL setStoppedState
VAR CONSTANT
	NEW_STATE : TASK_STATE := TASK_STATE.STOPPED;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF _State <> NEW_STATE THEN
	LogManager.TryLogMessage(
		WideStringHelper.ConcatStrings255("Task '", Name, "' changed state from '", TO_WSTRING(_State), "' to '", TO_WSTRING(NEW_STATE), "'"), 
		LOG_CATEGORY.SUCCESS, 
		TO_WSTRING(ClassName), 
		"Task", 
		WideStringHelper.ConcatStrings255("Task.", TO_WSTRING(NEW_STATE))
	);

	_State := NEW_STATE;
	_StateChanged := TRUE;	
END_IF 

IF _StopReasonMessage = "" THEN
	CASE _StopReason OF
		TASK_STOP_REASON.SUCCESS:
			_StopReasonMessage := "Successfully completed";
			
		TASK_STOP_REASON.EXTERNAL_LOGIC:
			_StopReasonMessage := "Stopped by external logic";
	
		TASK_STOP_REASON.INTERNAL_LOGIC:
			_StopReasonMessage := "Stopped by internal logic";
	
		TASK_STOP_REASON.FAILURE:
			_StopReasonMessage := "Exception occured";
	
		TASK_STOP_REASON.USER_DEFINED:		
			_StopReasonMessage := "User defined";
	END_CASE	
END_IF]]></ST>
      </Implementation>
    </Method>
    <Method Name="Start" Id="{9882b806-c3e8-4a3f-aaf2-ed11ba23d42b}">
      <Declaration><![CDATA[METHOD FINAL Start : BOOL
VAR_OUTPUT
	refuseReason : WSTRING(255);
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF _State <> TASK_STATE.IDLE THEN
	refuseReason := TOF_Core.WideStringHelper.ConcatStrings255("Expected state is '", TO_WSTRING(TASK_STATE.IDLE), "' but current state is '", TO_WSTRING(_State), "'");
	RETURN;
END_IF

setRunningState();

handleRunning();

Start := TRUE;]]></ST>
      </Implementation>
    </Method>
    <Property Name="State" Id="{7f5b82bd-0883-4857-a0c7-770647e24256}">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
PROPERTY FINAL State : REFERENCE TO TaskState
]]></Declaration>
      <Get Name="Get" Id="{fa1e6aa2-a09d-4b31-b449-f6936821b743}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[State REF= _StateObject;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Property Name="StopReason" Id="{67946b75-ded8-468e-a4e3-0a11a6a9934d}">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
PROPERTY FINAL StopReason : REFERENCE TO TaskStopReason]]></Declaration>
      <Get Name="Get" Id="{9c409cdb-d955-4a97-8035-cec1dbac6e4c}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[StopReason REF= _StopReasonObject;]]></ST>
        </Implementation>
      </Get>
    </Property>
  </POU>
</TcPlcObject>