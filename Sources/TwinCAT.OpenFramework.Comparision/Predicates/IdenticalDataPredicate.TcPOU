<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1">
  <POU Name="IdenticalDataPredicate" Id="{69d89670-0944-46d2-b34c-f87cdf7e100c}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK IdenticalDataPredicate EXTENDS Core.Object IMPLEMENTS Core.IPredicate
VAR
	_ClassName : STRING := __POUNAME();
	
	_ReferenceValue : __System.AnyType;
END_VAR]]></Declaration>
    <Implementation>
      <ST><![CDATA[]]></ST>
    </Implementation>
    <Method Name="Check" Id="{cec5bd44-71ec-48bd-974d-4ba41cd8e3ee}">
      <Declaration><![CDATA[METHOD Check : BOOL
VAR_INPUT
	valueToCompare : __System.AnyType; 
END_VAR
VAR
	stringPointer : POINTER TO STRING;
	wideStringPointer : POINTER TO WSTRING;
	referenceDataSize : DINT;
	valueToComapreDataSize : DINT;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF _ReferenceValue.pValue = 0 OR_ELSE _ReferenceValue.diSize = 0 OR_ELSE valueToCompare.pValue = 0 OR_ELSE valueToCompare.diSize = 0 THEN
	Check := (_ReferenceValue.pValue = 0 OR_ELSE _ReferenceValue.diSize = 0) AND_THEN (valueToCompare.pValue = 0 OR_ELSE valueToCompare.diSize = 0);
ELSE
	IF _ReferenceValue.TypeClass = __SYSTEM.TYPE_CLASS.TYPE_STRING AND_THEN valueToCompare.TypeClass = __SYSTEM.TYPE_CLASS.TYPE_STRING THEN
		stringPointer := _ReferenceValue.pValue;
		referenceDataSize := TO_DINT(StringHelper.GetActualSize(stringPointer^));
		
		stringPointer := valueToCompare.pValue;		
		valueToComapreDataSize := TO_DINT(StringHelper.GetActualSize(stringPointer^));
	ELSIF _ReferenceValue.TypeClass = __SYSTEM.TYPE_CLASS.TYPE_WSTRING AND_THEN valueToCompare.TypeClass = __SYSTEM.TYPE_CLASS.TYPE_WSTRING THEN
		wideStringPointer := _ReferenceValue.pValue;
		referenceDataSize := TO_DINT(WideStringHelper.GetActualSize(wideStringPointer^));
		
		wideStringPointer := valueToCompare.pValue;		
		valueToComapreDataSize := TO_DINT(WideStringHelper.GetActualSize(wideStringPointer^));
	ELSE
		referenceDataSize := _ReferenceValue.diSize;
		valueToComapreDataSize := valueToCompare.diSize;		
	END_IF
 
	Check := referenceDataSize = valueToComapreDataSize AND_THEN Tc2_System.MEMCMP(_ReferenceValue.pValue, valueToCompare.pValue, TO_UDINT(referenceDataSize)) = 0;
END_IF]]></ST>
      </Implementation>
    </Method>
    <Property Name="ClassName" Id="{8e5ab7c3-2f39-4002-b1dc-2aeb600ea841}">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
PROPERTY ClassName : STRING]]></Declaration>
      <Get Name="Get" Id="{cf2504cf-80f7-4f82-801a-33325d77d0b0}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[ClassName := _ClassName;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Property Name="ReferenceValue" Id="{95fd5b50-81f9-4136-9b29-0bd733fb8a44}">
      <Declaration><![CDATA[PROPERTY ReferenceValue : __System.AnyType]]></Declaration>
      <Get Name="Get" Id="{14082bc3-b7d3-4869-b7c9-01eb20f846a3}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[ReferenceValue := _ReferenceValue;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Method Name="SetReferenceValue" Id="{d2db5743-54df-4a10-9415-dd40ad903d0e}">
      <Declaration><![CDATA[METHOD SetReferenceValue
VAR_INPUT
	referenceValue : ANY;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[_ReferenceValue := referenceValue;]]></ST>
      </Implementation>
    </Method>
    <Method Name="SetReferenceValueByAnyType" Id="{47a51104-523c-4416-8ae4-bda16604c8b3}">
      <Declaration><![CDATA[METHOD SetReferenceValueByAnyType
VAR_INPUT
	referenceValue : __SYSTEM.AnyType;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[_ReferenceValue := referenceValue;]]></ST>
      </Implementation>
    </Method>
    <Property Name="Size" Id="{807678b9-ec28-4f30-9f23-d752d98349af}">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
PROPERTY FINAL Size : ULINT]]></Declaration>
      <Get Name="Get" Id="{7c7a200b-5a01-4955-8f62-0234eb0a28fb}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[Size := XSIZEOF(THIS^);]]></ST>
        </Implementation>
      </Get>
    </Property>
  </POU>
</TcPlcObject>