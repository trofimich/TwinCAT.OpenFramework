<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1">
  <POU Name="ByteArrayComparer" Id="{2bb53bb7-7d07-4a7a-896e-016e853fd868}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK ByteArrayComparer EXTENDS Object IMPLEMENTS IComparer
VAR
	_ClassName : STRING := __POUNAME();
END_VAR]]></Declaration>
    <Implementation>
      <ST><![CDATA[]]></ST>
    </Implementation>
    <Property Name="ClassName" Id="{cf39d53b-ed13-4edf-ad55-696c13de665b}">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
PROPERTY ClassName : STRING]]></Declaration>
      <Get Name="Get" Id="{d30e6779-120e-49c2-abe1-17cbfac7965e}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[ClassName := _ClassName;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Method Name="Compare" Id="{ee7448b3-b623-4697-a6c8-19f6c43d07cb}">
      <Declaration><![CDATA[{attribute "object_name" := "Compare"}
METHOD Compare : COMPARISION_RESULT
VAR_INPUT
	left : __SYSTEM.AnyType;
	right : __SYSTEM.AnyType;
END_VAR
VAR
	leftPointer : POINTER TO BYTE; 
	rightPointer : POINTER TO BYTE; 
	
	i : LINT;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF left.pValue = 0 AND right.pValue = 0 THEN
	Compare := COMPARISION_RESULT.FIRST_EQUAL_TO_SECOND;
	RETURN;
END_IF

IF left.pValue = 0 THEN
	Compare := COMPARISION_RESULT.FIRST_LESS_THAN_SECOND;
	RETURN;	
END_IF

IF right.pValue = 0 THEN
	Compare := COMPARISION_RESULT.FIRST_GREATER_THAN_SECOND;
	RETURN;	
END_IF

FOR i := 0 TO TO_LINT(MIN(left.diSize, right.diSize)) - 1 DO
	leftPointer := left.pValue + TO_LWORD(i);
	rightPointer := right.pValue + TO_LWORD(i);
	
	IF leftPointer^ < rightPointer^ THEN
		Compare := COMPARISION_RESULT.FIRST_LESS_THAN_SECOND;
		RETURN;
	ELSIF leftPointer^ > rightPointer^ THEN
		Compare := COMPARISION_RESULT.FIRST_GREATER_THAN_SECOND;
		RETURN;
	END_IF	
END_FOR

IF left.diSize < right.diSize THEN
	Compare := COMPARISION_RESULT.FIRST_LESS_THAN_SECOND;
ELSIF left.diSize > right.diSize THEN
	Compare := COMPARISION_RESULT.FIRST_GREATER_THAN_SECOND;
ELSE
	Compare := COMPARISION_RESULT.FIRST_EQUAL_TO_SECOND;
END_IF]]></ST>
      </Implementation>
    </Method>
    <Property Name="Size" Id="{b1a1240f-8b51-48b9-84a3-3c39c9b5cd85}">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
PROPERTY Size : ULINT]]></Declaration>
      <Get Name="Get" Id="{7cf65b52-2f9d-4aa5-9cb0-fb0f5d03a50d}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[Size := XSIZEOF(THIS^);]]></ST>
        </Implementation>
      </Get>
    </Property>
  </POU>
</TcPlcObject>