<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1">
  <POU Name="Workflow" Id="{276b6eec-c9aa-49be-b713-e71500f934ba}" SpecialFunc="None">
    <Declaration><![CDATA[(*

.. <legal notes>

legal notes
=================
| SPDX-FileCopyrightText: © 2025 Oleksandr Tiutyk <trofimich@gmail.com>
| SPDX-License-Identifier: LGPL-3.0-only
| For details check: LGPL-3.0-only_

.. _LGPL-3.0-only: https://www.gnu.org/licenses/lgpl-3.0.en.html

.. </legal notes>

*)

{attribute 'no_explicit_call' := 'do not call this function block directly'} 
{attribute 'enable_dynamic_creation'}	
FUNCTION_BLOCK ABSTRACT Workflow 
VAR_GENERIC CONSTANT
    VariableCount : INT(0..10000) := 0;
END_VAR
EXTENDS Core.Object IMPLEMENTS IWorkflow
VAR
	_Initialized : BOOL;
	
	_Name : WSTRING;
	
	_Variables : Collections.Dictionary(0, 0);
	
	_RootActivity : IActivity;
	
	_DynamicChildren : Collections.UniqueSet(0);
END_VAR]]></Declaration>
    <Implementation>
      <ST><![CDATA[]]></ST>
    </Implementation>
    <Folder Name="Members to override" Id="{0a85699b-0877-4f46-87b6-cd53ff5cc628}" />
    <Property Name="Busy" Id="{bab15e3d-0210-4a1d-9bb4-41a96adad825}">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
{attribute 'OPC.UA.DA.Property' := '1'}
{attribute 'OPC.UA.DA.Access' := '1'}
PROPERTY Busy : BOOL
]]></Declaration>
      <Get Name="Get" Id="{25a01fa6-5f1d-49d9-abad-9f8714f9cd46}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[IF _RootActivity <> 0 THEN
	Busy := _RootActivity.Busy;
ELSE
	Busy := FALSE;
END_IF]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Property Name="CancellingAllowed" Id="{dfcebe9b-ab64-4804-8d3a-9c7b480916ca}">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
{attribute 'OPC.UA.DA.Property' := '1'}
{attribute 'OPC.UA.DA.Access' := '1'}
PROPERTY CancellingAllowed : BOOL]]></Declaration>
      <Get Name="Get" Id="{1950e828-b0f7-41b0-af94-b6507dbd368e}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[CancellingAllowed := WorkflowHelper.ValidateTransition(State, WORKFLOW_ELEMENT_STATE.ACTIVE_CANCELLING);]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Property Name="CancelReason" Id="{526411d6-4df3-4a3b-abe6-82d140566243}">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
PROPERTY CancelReason : REFERENCE TO WSTRING(255)
]]></Declaration>
      <Get Name="Get" Id="{2b2eea18-9847-400b-97a8-d83167a86a22}">
        <Declaration><![CDATA[VAR_INST
	emptyCancelReason : WSTRING(255) := "";
END_VAR]]></Declaration>
        <Implementation>
          <ST><![CDATA[IF _RootActivity <> 0 THEN
	CancelReason REF= _RootActivity.CancelReason;
ELSE
	CancelReason REF= emptyCancelReason;
END_IF]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Method Name="CreateVariables" Id="{77b0e16c-37a3-4b71-b6b3-d4ba31e98e52}" FolderPath="Members to override\">
      <Declaration><![CDATA[METHOD PROTECTED ABSTRACT CreateVariables : ARRAY [0..VariableCount - 1] OF VARIABLE_DESCRIPTOR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[]]></ST>
      </Implementation>
    </Method>
    <Method Name="DisposeDynamicChildren" Id="{6b69ccba-15f0-48cc-974c-9c24c24bc45d}">
      <Declaration><![CDATA[METHOD DisposeDynamicChildren]]></Declaration>
      <Implementation>
        <ST><![CDATA[_DynamicChildren.Clear();]]></ST>
      </Implementation>
    </Method>
    <Method Name="Execute" Id="{591aa81e-a79a-4b4b-bf41-97b54125a21a}">
      <Declaration><![CDATA[METHOD FINAL Execute]]></Declaration>
      <Implementation>
        <ST><![CDATA[OnRootActivityExecuted();

IF _RootActivity <> 0 THEN
	_RootActivity.Execute();
END_IF

OnRootActivityExecuting();]]></ST>
      </Implementation>
    </Method>
    <Property Name="FailErrorMessage" Id="{e7a8b006-53f5-4707-ae65-96e34a954950}">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
{attribute 'OPC.UA.DA.Property' := '1'}
{attribute 'OPC.UA.DA.Access' := '1'}
PROPERTY FailErrorMessage : REFERENCE TO ERROR_MESSAGE
]]></Declaration>
      <Get Name="Get" Id="{241dbeba-018c-4dcc-ae83-a41da8c0e1e7}">
        <Declaration><![CDATA[VAR_INST
	emptyErrorMessage : Core.ERROR_MESSAGE := ""; 
END_VAR]]></Declaration>
        <Implementation>
          <ST><![CDATA[IF _RootActivity <> 0 THEN
	FailErrorMessage REF= _RootActivity.FailErrorMessage;
ELSE
	FailErrorMessage REF= emptyErrorMessage;
END_IF]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Method Name="FB_exit" Id="{8a118ccc-cf33-4895-aaa4-ec240e9f2ff6}">
      <Declaration><![CDATA[METHOD FB_exit : BOOL
VAR_INPUT
	(* if TRUE, the exit method is called for exiting an instance that is copied afterwards (online change).*)
	bInCopyCode	: BOOL;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[DisposeDynamicChildren();]]></ST>
      </Implementation>
    </Method>
    <Property Name="HaltReason" Id="{d3195a13-7315-4fe3-bced-ed5c7b1dd8a3}">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
{attribute 'OPC.UA.DA.Property' := '1'}
{attribute 'OPC.UA.DA.Access' := '1'}
PROPERTY HaltReason : REFERENCE TO WSTRING(255)
]]></Declaration>
      <Get Name="Get" Id="{b4ccf352-2016-4ab8-933f-a656e330fbf2}">
        <Declaration><![CDATA[VAR_INST
	emptyHaltReason : WSTRING(255) := "";
END_VAR]]></Declaration>
        <Implementation>
          <ST><![CDATA[IF _RootActivity <> 0 THEN
	HaltReason REF= _RootActivity.HaltReason;
ELSE
	HaltReason REF= emptyHaltReason;
END_IF]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Property Name="Name" Id="{7260c51c-949e-4411-88e2-fdf1b44dbbf3}">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
{attribute 'OPC.UA.DA.Property' := '1'}
{attribute 'OPC.UA.DA.Access' := '1'}
PROPERTY FINAL Name : WSTRING]]></Declaration>
      <Get Name="Get" Id="{b01b805c-3bf4-4f88-b92c-776b7b945e61}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[Name := SEL(_Name = "", _Name, SUPER^.Name);]]></ST>
        </Implementation>
      </Get>
      <Set Name="Set" Id="{e01e9d2c-1c81-40aa-b7f1-278f07926b5e}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[_Name := Name;]]></ST>
        </Implementation>
      </Set>
    </Property>
    <Method Name="OnRootActivityExecuted" Id="{dea40369-d161-4fc1-bc15-7686100c5bae}" FolderPath="Members to override\">
      <Declaration><![CDATA[METHOD PROTECTED OnRootActivityExecuted]]></Declaration>
      <Implementation>
        <ST><![CDATA[]]></ST>
      </Implementation>
    </Method>
    <Method Name="OnRootActivityExecuting" Id="{98c31e4c-dd28-45ea-b6fa-6f7ffd46cc49}" FolderPath="Members to override\">
      <Declaration><![CDATA[METHOD PROTECTED OnRootActivityExecuting]]></Declaration>
      <Implementation>
        <ST><![CDATA[]]></ST>
      </Implementation>
    </Method>
    <Method Name="OnRootActivityRequestCancel" Id="{b3cbfe62-449f-465f-bf5c-b273ba14c9bb}" FolderPath="Members to override\">
      <Declaration><![CDATA[METHOD PROTECTED OnRootActivityRequestCancel
VAR_OUTPUT
	cancelRequest : BOOL;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[]]></ST>
      </Implementation>
    </Method>
    <Method Name="OnRootActivityRequestStart" Id="{ec309e5d-92ab-4d1e-bc30-2227f73237f8}" FolderPath="Members to override\">
      <Declaration><![CDATA[METHOD PROTECTED OnRootActivityRequestStart
VAR_OUTPUT
	cancelRequest : BOOL;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[]]></ST>
      </Implementation>
    </Method>
    <Method Name="OnRootActivityRequestSuspend" Id="{6021acef-ee70-43ba-9c45-d3783b487bf4}" FolderPath="Members to override\">
      <Declaration><![CDATA[METHOD PROTECTED OnRootActivityRequestSuspend
VAR_OUTPUT
	cancelRequest : BOOL;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[]]></ST>
      </Implementation>
    </Method>
    <Method Name="RegisterChild" Id="{d5ebe690-8ae4-4f6f-8bba-5616c293b52c}">
      <Declaration><![CDATA[METHOD FINAL RegisterChild
VAR_INPUT
	child : IWorkflowElement;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF child <> 0 AND_THEN child.IsDynamicInstance THEN	
	_DynamicChildren.SetGeneric(Core.GenericValueFactory.FromParts(GENERIC_TYPE_CLASS.OBJECT, child.Address, child.Size, GENERIC_MEMORY_MANAGEMENT_BEHAVIOR.STORE_ORIGINAL_REFERENCE_AND_OWNS_MEMORY));
	
	LogManager.TryLogMessage(WideStringHelper.ConcatStrings255("Dynamic child with address 16#", TO_WSTRING(Tc2_Utilities.LWORD_TO_HEXSTR(child.Address, 16)), " registred in workflow '", Name, "'"), LOG_CATEGORY.INFORMATION, "Workflow", "WorkflowRegisterChild");
END_IF]]></ST>
      </Implementation>
    </Method>
    <Method Name="RequestCancel" Id="{e9e3f3d2-fd31-4599-91c9-ed64362dfc36}">
      <Declaration><![CDATA[METHOD FINAL RequestCancel : BOOL
VAR_INPUT
	reason : WSTRING(255);
END_VAR
VAR
	cancelRequest : BOOL;

	rootActivityNotSpecifiedException : DataMemberNotSpecifiedException;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[LogManager.TryLogMessage(WideStringHelper.ConcatStrings255("Cancel requested for workflow '", Name, "'"), LOG_CATEGORY.INFORMATION, "Workflow", "WorkflowRequest", "WorkflowRequestCancel");

IF _RootActivity <> 0 THEN
	IF WorkflowHelper.ValidateTransition(State, WORKFLOW_ELEMENT_STATE.ACTIVE_CANCELLING) THEN
		OnRootActivityRequestCancel(cancelRequest => cancelRequest);
		
		IF NOT cancelRequest THEN
			_RootActivity.RequestCancel(reason);
		
			RequestCancel := TRUE;
		ELSE
			LogManager.TryLogMessage(WideStringHelper.ConcatStrings255("Request cancel for workflow '", Name, "' was cancelled"), LOG_CATEGORY.INFORMATION, "Workflow", "WorkflowRequest", "WorkflowRequestCancelCancelled");	
		END_IF
	END_IF
ELSE
	rootActivityNotSpecifiedException.Throw(NamespaceName, ClassName, __POUNAME(), __POSITION(), 'RootActivity');
END_IF]]></ST>
      </Implementation>
    </Method>
    <Method Name="RequestStart" Id="{8bafec37-0963-43f8-ba1c-3151d45bcb5a}">
      <Declaration><![CDATA[METHOD FINAL RequestStart : BOOL
VAR
	cancelRequest : BOOL;

	rootActivityNotSpecifiedException : DataMemberNotSpecifiedException;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[LogManager.TryLogMessage(WideStringHelper.ConcatStrings255("Start requested for workflow '", Name, "'"), LOG_CATEGORY.INFORMATION, "Workflow", "WorkflowRequest", "WorkflowRequestStart");

IF _RootActivity <> 0 THEN
	IF WorkflowHelper.ValidateTransition(State, WORKFLOW_ELEMENT_STATE.ACTIVE_RUNNING) THEN
		OnRootActivityRequestStart(cancelRequest => cancelRequest);
		
		IF NOT cancelRequest THEN		
			_RootActivity.RequestStart();
		
			RequestStart := TRUE;
		ELSE
			LogManager.TryLogMessage(WideStringHelper.ConcatStrings255("Start requested for workflow '", Name, "'"), LOG_CATEGORY.INFORMATION, "Workflow", "WorkflowRequest", "WorkflowRequestStartCancelled");
		END_IF
	END_IF
ELSE
	rootActivityNotSpecifiedException.Throw(NamespaceName, ClassName, __POUNAME(), __POSITION(), 'RootActivity');
END_IF]]></ST>
      </Implementation>
    </Method>
    <Method Name="RequestSuspend" Id="{d3ff2a73-061d-479e-8566-a8cc52ec948c}">
      <Declaration><![CDATA[METHOD FINAL RequestSuspend : BOOL
VAR
	cancelRequest : BOOL;
	
	rootActivityNotSpecifiedException : DataMemberNotSpecifiedException;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[LogManager.TryLogMessage(WideStringHelper.ConcatStrings255("Request suspend for workflow '", Name, "'"), LOG_CATEGORY.INFORMATION, "Workflow", "WorkflowRequest", "WorkflowRequestSuspend");

IF _RootActivity <> 0 THEN
	IF WorkflowHelper.ValidateTransition(State, WORKFLOW_ELEMENT_STATE.ACTIVE_SUSPENDING) THEN
		OnRootActivityRequestSuspend(cancelRequest => cancelRequest);
		
		IF NOT cancelRequest THEN
			_RootActivity.RequestSuspend();
		
			RequestSuspend := TRUE;
		ELSE
			LogManager.TryLogMessage(WideStringHelper.ConcatStrings255("Request suspend for workflow '", Name, "' was cancelled"), LOG_CATEGORY.INFORMATION, "Workflow", "WorkflowRequest", "WorkflowRequestSuspendCancelled");
		END_IF
	END_IF
ELSE
	rootActivityNotSpecifiedException.Throw(NamespaceName, ClassName, __POUNAME(), __POSITION(), 'RootActivity');
END_IF]]></ST>
      </Implementation>
    </Method>
    <Property Name="RootActivity" Id="{142ff0bb-9cbe-49d5-8b58-eabad1dc6713}">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
PROPERTY FINAL RootActivity : IActivity]]></Declaration>
      <Get Name="Get" Id="{bda62c52-93af-4541-b8a0-25e42cace2f2}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[RootActivity := _RootActivity;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Method Name="SetRootActivity" Id="{c98e2b1c-fc8c-4d20-ba30-5596dcd42dcd}">
      <Declaration><![CDATA[METHOD FINAL SetRootActivity
VAR_INPUT
	rootActivity : IActivity;
END_VAR
VAR
	workflowBusyException : Core.StandardException; 

	workflowVariablException : Core.StandardException;

	variables : ARRAY [0..VariableCount - 1] OF VARIABLE_DESCRIPTOR;
	
	i : DINT;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF _RootActivity = rootActivity THEN
	RETURN;
END_IF

IF Busy THEN
	workflowBusyException.Throw(NamespaceName, ClassName, __POUNAME(), __POSITION(), WideStringHelper.ConcatStrings255("Unable to change root activity for workflow '", Name, "': workflow is busy"));
END_IF

IF NOT _Initialized THEN
	variables := CreateVariables();
	
	FOR i := 0 TO VariableCount - 1 DO
		IF variables[i].Name = '' THEN
			workflowVariablException.Throw(NamespaceName, ClassName, __POUNAME(), __POSITION(), "Method CreateWorkflowVariables returned workflow variable with empty name");
		END_IF
		
		_Variables.SetGeneric(
			Core.GenericValueFactory.FromStringValue(variables[i].Name), 
			Core.GenericValueFactory.FromParts(variables[i].TypeClass, variables[i].Address, variables[i].Size, GENERIC_MEMORY_MANAGEMENT_BEHAVIOR.STORE_ORIGINAL_REFERENCE_AND_NOT_OWNS_MEMORY));
	END_FOR
	
	IF _Variables.Count <> VariableCount THEN
		workflowVariablException.Throw(NamespaceName, ClassName, __POUNAME(), __POSITION(), WideStringHelper.ConcatStrings255("Invalid number of unique workflow variables ", TO_WSTRING(_Variables.Count), " instead of ", TO_WSTRING(VariableCount), ". Check for duplicated names"));		
	END_IF
	
	_Initialized := TRUE;
END_IF

_RootActivity := rootActivity;]]></ST>
      </Implementation>
    </Method>
    <Property Name="StartingAllowed" Id="{95daf790-beb6-482f-b496-9fe693beae09}">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
{attribute 'OPC.UA.DA.Property' := '1'}
{attribute 'OPC.UA.DA.Access' := '1'}
PROPERTY StartingAllowed : BOOL]]></Declaration>
      <Get Name="Get" Id="{68298cce-8206-42d6-ae98-f824acf8e5c1}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[StartingAllowed := WorkflowHelper.ValidateTransition(State, WORKFLOW_ELEMENT_STATE.ACTIVE_RUNNING);]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Property Name="State" Id="{41e640de-75d7-4041-9e96-1cf5228bad0c}">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
{attribute 'OPC.UA.DA.Property' := '1'}
{attribute 'OPC.UA.DA.Access' := '1'}
PROPERTY State : WORKFLOW_ELEMENT_STATE]]></Declaration>
      <Get Name="Get" Id="{981298d5-8560-4acd-85b3-18b8e41e747f}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[IF _RootActivity <> 0 THEN
	State := _RootActivity.State;
ELSE
	State := WORKFLOW_ELEMENT_STATE.PASSIVE_FAILED;
END_IF]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Property Name="SuspendingAllowed" Id="{0820968e-0c3a-437c-b3a5-aa866f9eaee4}">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
{attribute 'OPC.UA.DA.Property' := '1'}
{attribute 'OPC.UA.DA.Access' := '1'}
PROPERTY SuspendingAllowed : BOOL]]></Declaration>
      <Get Name="Get" Id="{0986e853-cad0-400c-9019-3c43569bc0cb}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[SuspendingAllowed := WorkflowHelper.ValidateTransition(State, WORKFLOW_ELEMENT_STATE.ACTIVE_SUSPENDING);]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Method Name="TryGetVariable" Id="{5010e467-b98c-4a4e-ae7b-f87b6c5b379c}">
      <Declaration><![CDATA[METHOD TryGetVariable : BOOL
VAR_INPUT
	name : STRING;
END_VAR
VAR_OUTPUT
	variable : Core.GENERIC_VALUE;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[TryGetVariable := _Variables.TryGetValueByKey(name, value => variable);]]></ST>
      </Implementation>
    </Method>
  </POU>
</TcPlcObject>