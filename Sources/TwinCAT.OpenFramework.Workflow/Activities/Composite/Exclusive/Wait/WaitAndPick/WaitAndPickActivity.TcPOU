<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1">
  <POU Name="WaitAndPickActivity" Id="{98b3e546-b646-44e7-bb5b-488f34e41fc2}" SpecialFunc="None">
    <Declaration><![CDATA[(*

.. <legal notes>

legal notes
=================
| SPDX-FileCopyrightText: © 2025 Oleksandr Tiutyk <trofimich@gmail.com>
| SPDX-License-Identifier: LGPL-3.0-only
| For details check: LGPL-3.0-only_

.. _LGPL-3.0-only: https://www.gnu.org/licenses/lgpl-3.0.en.html

.. </legal notes>

*)

{attribute 'no_explicit_call' := 'do not call this function block directly'} 
{attribute 'enable_dynamic_creation'}
FUNCTION_BLOCK WaitAndPickActivity EXTENDS ExclusiveCompositeActivity IMPLEMENTS IWaitAndPickActivity
VAR
	{attribute 'OPC.UA.DA' := '0'}
	_ClassName : STRING := __POUNAME();

	{attribute 'OPC.UA.DA' := '0'}
	_BranchConditions : List(0);

	{attribute 'OPC.UA.DA' := '0'}
	_BranchActivities : List(0);
	
	{attribute 'OPC.UA.DA' := '0'}	
	_CurrentBranchIndex : DINT := -1;
END_VAR]]></Declaration>
    <Implementation>
      <ST><![CDATA[]]></ST>
    </Implementation>
    <Folder Name="Overriden" Id="{85831841-5ba2-46a5-9479-dc90add82035}" />
    <Folder Name="Service members" Id="{5438ee75-7621-4c52-a4d9-4dfea8dac7a8}" />
    <Method Name="AddBarnch" Id="{f3946fdc-ede0-4f71-bb51-f1411208bc91}">
      <Declaration><![CDATA[METHOD AddBarnch : DINT
VAR_INPUT
	condition : ICondition;
	activity : IActivity;
END_VAR
VAR
	conditionNullExcxeption : Core.ArgumentNotSpecifiedException;
	activityNullExcxeption : Core.ArgumentNotSpecifiedException;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF condition = 0  THEN
	activityNullExcxeption.Throw(NamespaceName, ClassName, __POUNAME(), __POSITION(), 'condition');
END_IF

IF activity = 0  THEN
	activityNullExcxeption.Throw(NamespaceName, ClassName, __POUNAME(), __POSITION(), 'activity');
END_IF

activity.ParentActivity := THIS^;

_BranchConditions.AppendGeneric(Core.GenericValueFactory.FromParts(GENERIC_TYPE_CLASS.TYPE_OBJECT, condition.Address, condition.Size, GENERIC_MEMORY_MANAGEMENT_BEHAVIOR.STORE_ORIGINAL_REFERENCE_AND_NOT_OWNS_MEMORY));

_BranchActivities.AppendGeneric(Core.GenericValueFactory.FromParts(GENERIC_TYPE_CLASS.TYPE_OBJECT, activity.Address, activity.Size, GENERIC_MEMORY_MANAGEMENT_BEHAVIOR.STORE_ORIGINAL_REFERENCE_AND_NOT_OWNS_MEMORY), index => AddBarnch);]]></ST>
      </Implementation>
    </Method>
    <Property Name="ClassName" Id="{0fd055e5-b559-4b38-9ea0-4ca6dfde96f4}">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
PROPERTY ClassName : STRING]]></Declaration>
      <Get Name="Get" Id="{03123722-c59b-4f8b-8e1d-57ef078fc153}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[ClassName := _ClassName;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Property Name="CurrentActivity" Id="{ff44d870-a64f-40ed-9ac5-63b06e2e61c2}">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
PROPERTY FINAL CurrentActivity : IActivity]]></Declaration>
      <Get Name="Get" Id="{8185d1d9-3e50-4bb8-a6f5-2467a7aadc60}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[IF _BranchActivities.Count > 0 AND_THEN _CurrentBranchIndex >= 0 AND_THEN _CurrentBranchIndex < _BranchActivities.Count THEN
	__QUERYINTERFACE(_BranchActivities.Get(_CurrentBranchIndex).AsObject(), CurrentActivity);
END_IF]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Property Name="CurrentBranchIndex" Id="{33275b8e-2247-47e4-9607-47cd580c7b26}">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
PROPERTY FINAL CurrentBranchIndex : DINT]]></Declaration>
      <Get Name="Get" Id="{b77479ef-312c-49a9-8f3c-c0dff4b2b548}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[CurrentBranchIndex := _CurrentBranchIndex;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Property Name="NamespaceName" Id="{71b51e02-0e7f-400d-98b6-f6fe9b6e421d}">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
PROPERTY NamespaceName : STRING]]></Declaration>
      <Get Name="Get" Id="{39b886bb-35c4-4342-9cee-6cf1f6e0a2e1}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[NamespaceName := CurrentNamespace.Name;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Method Name="OnCancelling" Id="{07e9f5dd-2a3d-4db3-89d5-f47d571203f6}" FolderPath="Overriden\">
      <Declaration><![CDATA[METHOD PROTECTED OnCancelling 
VAR_INPUT
	start : BOOL;
END_VAR
VAR_OUTPUT
	cancelled : BOOL;
END_VAR
VAR
	activity : IActivity;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[]]></ST>
      </Implementation>
    </Method>
    <Method Name="OnRunning" Id="{6bdc48e7-942d-42d5-a41d-15bff80339b4}">
      <Declaration><![CDATA[METHOD PROTECTED OnRunning : ACTIVITY_RUNNING_STATE
VAR_INPUT
	startRunning : BOOL;
	startResuming : BOOL;
END_VAR
VAR_OUTPUT
	haltReason : WSTRING(255);
END_VAR
VAR
	activity : IActivity;
	
	currentActivityNullException : Core.NullReferenceException;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF _BranchConditions.Count < 1 THEN
	OnRunning := ACTIVITY_RUNNING_STATE.WORK_COMPLETED;
	RETURN;
END_IF

IF startRunning THEN
	_CurrentBranchIndex := -1;
END_IF

IF _CurrentBranchIndex < 0 THEN
	_CurrentBranchIndex := tryPickBranch();
END_IF

IF _CurrentBranchIndex < 0 THEN
	RETURN;
END_IF

activity := CurrentActivity;

IF activity = 0 THEN
	currentActivityNullException.Throw(NamespaceName, ClassName, __POUNAME(), __POSITION(), 'CurrentActivity');
END_IF

IF startRunning OR_ELSE startResuming OR_ELSE NOT activity.Busy THEN
	activity.RequestStart();
ELSE
	activity.Execute();
END_IF

OnRunning := GetActivityRunningStateByNestedActivityState(activity.State);]]></ST>
      </Implementation>
    </Method>
    <Method Name="OnStopping" Id="{90584366-9ed4-4c76-823f-f6583e8cb7e4}">
      <Declaration><![CDATA[METHOD PROTECTED OnStopping : ACTIVITY_STOPPING_STATE
VAR_INPUT
	startCancelling : BOOL;
	startSuspending : BOOL;
END_VAR
VAR_OUTPUT
	haltReason : WSTRING(255);
END_VAR
VAR
	activity : IActivity;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF _BranchConditions.Count < 1 THEN
	OnStopping := ACTIVITY_STOPPING_STATE.WORK_COMPLETED;
	RETURN;
END_IF

activity := CurrentActivity;

IF activity = 0 THEN
	IF startCancelling OR_ELSE startSuspending THEN
		OnStopping := ACTIVITY_STOPPING_STATE.WORK_STOPPED;
	ELSE
		OnStopping := ACTIVITY_STOPPING_STATE.WORK_COMPLETED;		
	END_IF

	RETURN;
END_IF

IF NOT activity.Busy THEN
	OnStopping := GetActivityStoppingStateByNestedActivityState(activity.State);
	RETURN;
END_IF

IF startCancelling THEN
	activity.RequestCancel(_CancelReason);
ELSIF startSuspending THEN
	activity.RequestSuspend();
ELSE
	activity.Execute();
END_IF

OnStopping := GetActivityStoppingStateByNestedActivityState(activity.State);]]></ST>
      </Implementation>
    </Method>
    <Property Name="Size" Id="{f48717c6-b99e-47cb-94a9-c8d92c921628}">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
PROPERTY Size : ULINT]]></Declaration>
      <Get Name="Get" Id="{2bce559c-e271-4c1b-b7e1-6e1feaa74956}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[Size := XSIZEOF(THIS^);]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Method Name="tryPickBranch" Id="{8cf25bf3-94b9-4b19-951a-3c1cbb21bad6}" FolderPath="Service members\">
      <Declaration><![CDATA[METHOD PRIVATE tryPickBranch : DINT
VAR
	i : DINT;
	condition : ICondition;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[tryPickBranch := -1;

FOR i := 0 TO _BranchConditions.Count - 1 DO
	__QUERYINTERFACE(_BranchConditions.Get(i).AsObject(), condition);
	
	IF condition.Evaluate(THIS^) THEN
		tryPickBranch := i;
		RETURN;
	END_IF
END_FOR]]></ST>
      </Implementation>
    </Method>
  </POU>
</TcPlcObject>