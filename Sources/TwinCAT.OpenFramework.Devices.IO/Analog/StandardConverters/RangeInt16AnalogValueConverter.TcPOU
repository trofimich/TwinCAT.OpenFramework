<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1">
  <POU Name="RangeInt16AnalogValueConverter" Id="{4e7d0a51-21d9-4244-962b-6c39100b7639}" SpecialFunc="None">
    <Declaration><![CDATA[(*

.. <legal notes>

legal notes
=================
| SPDX-FileCopyrightText: © 2025 Oleksandr Tiutyk <trofimich@gmail.com>
| SPDX-License-Identifier: LGPL-3.0-only
| For details check: LGPL-3.0-only_

.. _LGPL-3.0-only: https://www.gnu.org/licenses/lgpl-3.0.en.html

.. </legal notes>

*)

FUNCTION_BLOCK RangeInt16AnalogValueConverter EXTENDS Core.Object IMPLEMENTS IAnalogValueConverter
VAR_OUTPUT
	MinDigitizedValue : INT;
	MaxDigitizedValue : INT;
	MinPhysicalValue : LREAL;
	MaxPhysicalValue : LREAL;
END_VAR
VAR
	_ClassName : STRING := __POUNAME();
END_VAR]]></Declaration>
    <Implementation>
      <ST><![CDATA[]]></ST>
    </Implementation>
    <Property Name="ClassName" Id="{a4a04786-fc36-4ebe-9008-693a4dc53fad}">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
PROPERTY ClassName : STRING]]></Declaration>
      <Get Name="Get" Id="{052bbd21-1abf-4a57-8093-00c715b3b951}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[ClassName := _ClassName;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Method Name="ConvertAnalogValueToDigitizedValue" Id="{fd3c6d8a-5399-42e6-96af-ecf346b17990}">
      <Declaration><![CDATA[METHOD ConvertAnalogValueToDigitizedValue : DIGITIZED_ANALOG_VALUE
VAR_INPUT
	analogValue	: LREAL;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF analogValue >= MaxPhysicalValue THEN
	ConvertAnalogValueToDigitizedValue._16Bit := MaxDigitizedValue;
ELSIF analogValue <= MinPhysicalValue THEN
	ConvertAnalogValueToDigitizedValue._16Bit := MinDigitizedValue;
ELSE
    ConvertAnalogValueToDigitizedValue._16Bit := TO_INT((analogValue - MinPhysicalValue) / (MaxPhysicalValue - MinPhysicalValue) * TO_LREAL(MaxDigitizedValue - MinDigitizedValue) + TO_LREAL(MinDigitizedValue));	
END_IF]]></ST>
      </Implementation>
    </Method>
    <Method Name="ConvertDigitizedValueToAnalogValue" Id="{2c9a18eb-14da-4c73-beac-cf36e8626f57}">
      <Declaration><![CDATA[METHOD ConvertDigitizedValueToAnalogValue : LREAL
VAR_INPUT
	digitizedValue : DIGITIZED_ANALOG_VALUE;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF digitizedValue._16Bit = MaxDigitizedValue THEN
	ConvertDigitizedValueToAnalogValue := MaxPhysicalValue;
ELSIF digitizedValue._16Bit = MinDigitizedValue THEN
	ConvertDigitizedValueToAnalogValue := MinPhysicalValue;
ELSE
	ConvertDigitizedValueToAnalogValue := TO_LREAL(digitizedValue._16Bit - MinDigitizedValue) / TO_LREAL(MaxDigitizedValue - MinDigitizedValue) * (MaxPhysicalValue - MinPhysicalValue) + MinPhysicalValue;
END_IF]]></ST>
      </Implementation>
    </Method>
    <Method Name="FB_init" Id="{c86d243b-f314-46f6-9a5e-2000dd3c2dff}">
      <Declaration><![CDATA[METHOD FB_init : BOOL
VAR_INPUT
	(* if TRUE, the retain variables are initialized (warm start / cold start)*)
	bInitRetains	: BOOL;
	(* if TRUE, the instance afterwards gets moved into the copy code (online change)*)
	bInCopyCode	: BOOL;
	minDigitizedValue : INT;
	maxDigitizedValue : INT;
	minPhysicalValue : LREAL;
	maxPhysicalValue : LREAL;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[THIS^.MinDigitizedValue := minDigitizedValue;
THIS^.MaxDigitizedValue := maxDigitizedValue;
THIS^.MinPhysicalValue := minPhysicalValue;
THIS^.MaxPhysicalValue := maxPhysicalValue;]]></ST>
      </Implementation>
    </Method>
    <Property Name="NamespaceName" Id="{8dce4c4f-9617-4c90-91d2-560ae98eba9e}">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
PROPERTY NamespaceName : STRING]]></Declaration>
      <Get Name="Get" Id="{16c09bd2-e9bc-4226-ac37-4256aae92566}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[NamespaceName := CurrentNamespace.Name;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Property Name="Size" Id="{3da3cdbd-6340-4e48-93f8-7d78a2ebce5a}">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
PROPERTY Size : ULINT]]></Declaration>
      <Get Name="Get" Id="{f1ea7dfe-b885-4e4d-8437-2e92cfb7a3b3}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[Size := XSIZEOF(THIS^);]]></ST>
        </Implementation>
      </Get>
    </Property>
  </POU>
</TcPlcObject>