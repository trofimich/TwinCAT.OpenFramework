<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1">
  <POU Name="InputOutputDevice" Id="{d3cfaf06-a014-47b7-8cf6-98a0cc083f94}" SpecialFunc="None">
    <Declaration><![CDATA[(*

.. <legal notes>

legal notes
=================
| SPDX-FileCopyrightText: © 2026 Oleksandr Tiutyk <trofimich@gmail.com>
| SPDX-License-Identifier: LGPL-3.0-only
| For details check: LGPL-3.0-only_

.. _LGPL-3.0-only: https://www.gnu.org/licenses/lgpl-3.0.en.html

.. </legal notes>

*)

FUNCTION_BLOCK ABSTRACT InputOutputDevice
VAR_GENERIC CONSTANT
    _SubDeviceCount : INT(0..1000) := 0;
END_VAR
EXTENDS Device<_SubDeviceCount> IMPLEMENTS IInputDevice, IOutputDevice]]></Declaration>
    <Implementation>
      <ST><![CDATA[]]></ST>
    </Implementation>
    <Folder Name="Methods to override" Id="{c65c0bbf-e1ef-43ca-a18f-3c425c65c3bc}" />
    <Method Name="OnInputProcessed" Id="{d8f04729-e573-4c49-b821-1b38da58b843}" FolderPath="Methods to override\">
      <Declaration><![CDATA[METHOD PROTECTED OnInputProcessed
VAR_INPUT
	exception : Core.IAggregateException;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[]]></ST>
      </Implementation>
    </Method>
    <Method Name="OnInputProcessing" Id="{12789fdb-7206-4b6d-9ba4-e340395c2892}" FolderPath="Methods to override\">
      <Declaration><![CDATA[METHOD PROTECTED OnInputProcessing]]></Declaration>
      <Implementation>
        <ST><![CDATA[]]></ST>
      </Implementation>
    </Method>
    <Method Name="OnOutputProcessed" Id="{7978f62d-7c3e-41fd-b6fb-5d0371a6a805}" FolderPath="Methods to override\">
      <Declaration><![CDATA[METHOD PROTECTED OnOutputProcessed
VAR_INPUT
	exception : Core.IAggregateException;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[]]></ST>
      </Implementation>
    </Method>
    <Method Name="OnOutputProcessing" Id="{4c787d5e-9ebd-4e1d-9d29-6abac1473421}" FolderPath="Methods to override\">
      <Declaration><![CDATA[METHOD PROTECTED OnOutputProcessing]]></Declaration>
      <Implementation>
        <ST><![CDATA[]]></ST>
      </Implementation>
    </Method>
    <Method Name="ProcessInput" Id="{253f2967-16b7-4926-8948-e00e6a6f87fb}">
      <Declaration><![CDATA[METHOD FINAL ProcessInput
VAR
	i : INT;
	
	inputSubDevice : IInputDevice;
	
	exceptionCode : __SYSTEM.ExceptionCode;
	exception : IException;
	
	aggregateException : Core.AggregateException;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[__TRY
	OnInputProcessing();
__CATCH(exceptionCode)
	exception := ExceptionManager.GetLastException(exceptionCode);
				
	aggregateException.AddInnerException(exception);
__ENDTRY

IF __ISVALIDREF(SubDevices) AND_THEN _SubDeviceCount > 0 THEN
	FOR i := 0 TO _SubDeviceCount - 1 DO
		IF NOT __QUERYINTERFACE(SubDevices[i], inputSubDevice) THEN
			CONTINUE;
		END_IF
		
		__TRY
			inputSubDevice.ProcessInput();
		__CATCH(exceptionCode)
			exception := ExceptionManager.GetLastException(exceptionCode);
						
			aggregateException.AddInnerException(exception);
		__ENDTRY 
	END_FOR	
END_IF

__TRY
	OnInputProcessed(aggregateException);
__CATCH(exceptionCode)
	exception := ExceptionManager.GetLastException(exceptionCode);
				
	aggregateException.AddInnerException(exception);
__ENDTRY

IF aggregateException.InnerExceptionCount > 0 THEN
	aggregateException.Throw(NamespaceName, ClassName, __POUNAME(), __POSITION());
END_IF]]></ST>
      </Implementation>
    </Method>
    <Method Name="ProcessOutput" Id="{41595c71-5414-4593-a797-cb95863046c3}">
      <Declaration><![CDATA[METHOD FINAL ProcessOutput
VAR
	i : INT;
	
	outputSubDevice : IOutputDevice;
	
	exceptionCode : __SYSTEM.ExceptionCode;
	exception : IException;
	
	aggregateException : Core.AggregateException;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[__TRY
	OnOutputProcessing();
__CATCH(exceptionCode)
	exception := ExceptionManager.GetLastException(exceptionCode);
				
	aggregateException.AddInnerException(exception);
__ENDTRY

IF __ISVALIDREF(SubDevices) AND_THEN _SubDeviceCount > 0 THEN
	FOR i := 0 TO _SubDeviceCount - 1 DO
		IF NOT __QUERYINTERFACE(SubDevices[i], outputSubDevice) THEN
			CONTINUE;
		END_IF
		
		__TRY
			outputSubDevice.ProcessOutput();
		__CATCH(exceptionCode)
			exception := ExceptionManager.GetLastException(exceptionCode);
						
			aggregateException.AddInnerException(exception);
		__ENDTRY 
	END_FOR	
END_IF

__TRY
	OnOutputProcessed(aggregateException);
__CATCH(exceptionCode)
	exception := ExceptionManager.GetLastException(exceptionCode);
				
	aggregateException.AddInnerException(exception);
__ENDTRY

IF aggregateException.InnerExceptionCount > 0 THEN
	aggregateException.Throw(NamespaceName, ClassName, __POUNAME(), __POSITION());
END_IF]]></ST>
      </Implementation>
    </Method>
  </POU>
</TcPlcObject>