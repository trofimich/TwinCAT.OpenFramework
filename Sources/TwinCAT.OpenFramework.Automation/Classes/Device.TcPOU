<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1">
  <POU Name="Device" Id="{7c21300f-1ad4-4217-9d49-d0f060249ecb}" SpecialFunc="None">
    <Declaration><![CDATA[(*

.. <legal notes>

legal notes
=================
| SPDX-FileCopyrightText: © 2025 Oleksandr Tiutyk <trofimich@gmail.com>
| SPDX-License-Identifier: LGPL-3.0-only
| For details check: LGPL-3.0-only_

.. _LGPL-3.0-only: https://www.gnu.org/licenses/lgpl-3.0.en.html

.. </legal notes>

*)

FUNCTION_BLOCK ABSTRACT Device
VAR_GENERIC CONSTANT
    _SubDeviceCount_ : INT(0..1000) := 0;
END_VAR
EXTENDS Core.Object IMPLEMENTS IDevice
VAR
	_Parent : ISimulatable;
	
	_PreviousEnabled : BOOL;
	_Enabled : BOOL := TRUE;
	
	_PreviousSimulationMode : BOOL;
	
	_SimulationModeOverride : SIMULATION_MODE_OVERRIDE;
END_VAR]]></Declaration>
    <Implementation>
      <ST><![CDATA[]]></ST>
    </Implementation>
    <Folder Name="Internal members to override" Id="{78a2b14c-f3bd-4833-8b88-abed9d422e8c}" />
    <Property Name="ChildSimulationModeCanBeChanged" Id="{0d575148-aa8e-43d2-bd83-68196b469957}" FolderPath="Internal members to override\">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
PROPERTY ChildSimulationModeCanBeChanged : BOOL]]></Declaration>
      <Get Name="Get" Id="{882fe28c-5914-4022-a2b4-ad9478164550}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[ChildSimulationModeCanBeChanged := TRUE;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Method Name="Disable" Id="{456e8034-80c7-4023-bbc1-b1fa7ea41430}">
      <Declaration><![CDATA[{attribute 'TcRpcEnable':='1'}
METHOD FINAL Disable]]></Declaration>
      <Implementation>
        <ST><![CDATA[_Enabled := FALSE;]]></ST>
      </Implementation>
    </Method>
    <Method Name="Enable" Id="{da30d30f-9904-4df0-aa77-06867eb85fe2}">
      <Declaration><![CDATA[{attribute 'TcRpcEnable':='1'}
METHOD FINAL Enable]]></Declaration>
      <Implementation>
        <ST><![CDATA[_Enabled := TRUE;]]></ST>
      </Implementation>
    </Method>
    <Property Name="Enabled" Id="{ac38c257-91b1-478a-9302-50becee70d2b}">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
{attribute 'OPC.UA.DA' := '1'}
{attribute 'OPC.UA.DA.Access' := '1'}
PROPERTY FINAL Enabled : BOOL]]></Declaration>
      <Get Name="Get" Id="{16b0a9c7-03ad-48e3-88ee-0d38de9d97f9}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[Enabled := _Enabled;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Method Name="FB_init" Id="{486d3e63-e78b-4191-9310-027ddbab0877}">
      <Declaration><![CDATA[METHOD FB_init : BOOL
VAR_INPUT
	(* if TRUE, the retain variables are initialized (warm start / cold start)*)
	bInitRetains	: BOOL;
	(* if TRUE, the instance afterwards gets moved into the copy code (online change)*)
	bInCopyCode	: BOOL;
	parent : ISimulatable;
END_VAR
VAR
	parentNullException : Core.ArgumentNotSpecifiedException;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF parent = 0 THEN
	parentNullException.Throw(NamespaceName, ClassName, __POUNAME(), __POSITION(), 'parent');
END_IF

_Parent := parent;]]></ST>
      </Implementation>
    </Method>
    <Method Name="HandleEnabledState" Id="{ff63c455-112e-4050-afee-5971d21c2aa4}">
      <Declaration><![CDATA[METHOD FINAL HandleEnabledState
VAR_IN_OUT CONSTANT
	executionContext : AUTOMATION_EXECUTION_CONTEXT;
	aggregateException : Core.AggregateException;
END_VAR
VAR
	i : INT;

	exceptionCode : __SYSTEM.ExceptionCode;
	exception : IException;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF _PreviousEnabled <> _Enabled THEN
	__TRY
		OnEnabledChanged(executionContext);
		
		_PreviousEnabled := _Enabled;
	__CATCH(exceptionCode)
		_PreviousEnabled := _Enabled;
		
		exception := ExceptionManager.GetLastException(exceptionCode);
					
		IF NOT exception.Fatal THEN
			aggregateException.AddInnerException(exception);
		ELSE
			ExceptionManager.ReThrowLastException();
		END_IF		
	__ENDTRY 
END_IF

IF __ISVALIDREF(SubDevices) AND_THEN _SubDeviceCount_ > 0 THEN
	FOR i := 0 TO _SubDeviceCount_ - 1 DO
		__TRY
			SubDevices[i].HandleEnabledState(executionContext, aggregateException);
		__CATCH(exceptionCode)
			exception := ExceptionManager.GetLastException(exceptionCode);
						
			IF NOT exception.Fatal THEN
				aggregateException.AddInnerException(exception);
			ELSE
				ExceptionManager.ReThrowLastException();
			END_IF		
		__ENDTRY 
	END_FOR	
END_IF]]></ST>
      </Implementation>
    </Method>
    <Method Name="HandleSimulationModeState" Id="{56cc590a-ae8d-418b-aa24-5cbc6f5e2b34}">
      <Declaration><![CDATA[METHOD FINAL HandleSimulationModeState
VAR_IN_OUT CONSTANT
	executionContext : AUTOMATION_EXECUTION_CONTEXT;
	aggregateException : Core.AggregateException;
END_VAR
VAR
	i : INT;

	exceptionCode : __SYSTEM.ExceptionCode;
	exception : IException;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF _PreviousSimulationMode <> SimulationMode THEN
	__TRY
		OnSimulationModeChanged(executionContext);
		
		_PreviousSimulationMode := SimulationMode;
	__CATCH(exceptionCode)
		_PreviousSimulationMode := SimulationMode;
		
		exception := ExceptionManager.GetLastException(exceptionCode);
					
		IF NOT exception.Fatal THEN
			aggregateException.AddInnerException(exception);
		ELSE
			ExceptionManager.ReThrowLastException();
		END_IF		
	__ENDTRY 
END_IF

IF __ISVALIDREF(SubDevices) AND_THEN _SubDeviceCount_ > 0 THEN
	FOR i := 0 TO _SubDeviceCount_ - 1 DO
		__TRY
			SubDevices[i].HandleSimulationModeState(executionContext, aggregateException);
		__CATCH(exceptionCode)
			exception := ExceptionManager.GetLastException(exceptionCode);
						
			IF NOT exception.Fatal THEN
				aggregateException.AddInnerException(exception);
			ELSE
				ExceptionManager.ReThrowLastException();
			END_IF					
		__ENDTRY
	END_FOR	
END_IF]]></ST>
      </Implementation>
    </Method>
    <Method Name="Initialize" Id="{b3b19006-a5ea-4fbb-ad1a-6167b8192dbd}">
      <Declaration><![CDATA[METHOD FINAL Initialize : BOOL
VAR_IN_OUT CONSTANT
	executionContext : AUTOMATION_EXECUTION_CONTEXT;
END_VAR
VAR
	i : INT;
		
	exceptionCode : __SYSTEM.ExceptionCode;
	exception : IException;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[Initialize := TRUE;

IF __ISVALIDREF(SubDevices) THEN
	FOR i := 0 TO _SubDeviceCount_ - 1 DO
		Initialize := Initialize AND SubDevices[i].Initialize(executionContext);
	END_FOR
END_IF

Initialize := Initialize AND OnInitialize(executionContext);]]></ST>
      </Implementation>
    </Method>
    <Method Name="OnEnabledChanged" Id="{9b34e142-9fc6-484f-999a-c1e79d516011}" FolderPath="Internal members to override\">
      <Declaration><![CDATA[METHOD PROTECTED OnEnabledChanged
VAR_IN_OUT CONSTANT
	executionContext : AUTOMATION_EXECUTION_CONTEXT;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[]]></ST>
      </Implementation>
    </Method>
    <Method Name="OnInitialize" Id="{896e6103-6f85-4d68-a85e-f1c9cefc8e44}" FolderPath="Internal members to override\">
      <Declaration><![CDATA[METHOD PROTECTED OnInitialize : BOOL
VAR_IN_OUT CONSTANT
	executionContext : AUTOMATION_EXECUTION_CONTEXT;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[OnInitialize := TRUE;]]></ST>
      </Implementation>
    </Method>
    <Method Name="OnSimulationModeChanged" Id="{c62f3cf3-693d-416f-b3e8-a590d6555831}" FolderPath="Internal members to override\">
      <Declaration><![CDATA[METHOD PROTECTED OnSimulationModeChanged
VAR_IN_OUT CONSTANT
	executionContext : AUTOMATION_EXECUTION_CONTEXT;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[]]></ST>
      </Implementation>
    </Method>
    <Property Name="Parent" Id="{02d768a3-95c4-4931-9d6a-360626987017}">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
PROPERTY FINAL Parent : ISimulatable]]></Declaration>
      <Get Name="Get" Id="{be1c49e3-7ebd-4a91-a8c8-d5a767248b50}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[Parent := _Parent;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Property Name="SimulationMode" Id="{d7e5aa07-12bb-4bf6-80e6-7fbb2b659753}">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
{attribute 'OPC.UA.DA' := '1'}
{attribute 'OPC.UA.DA.Access' := '1'}
PROPERTY FINAL SimulationMode : BOOL]]></Declaration>
      <Get Name="Get" Id="{dc0f4e5f-46f4-4797-a085-07525b3cf080}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[CASE _SimulationModeOverride OF
	SIMULATION_MODE_OVERRIDE.INHERITED:
		SimulationMode := _Parent.SimulationMode;
 	
	SIMULATION_MODE_OVERRIDE.ON:
		SimulationMode := TRUE;
	
	SIMULATION_MODE_OVERRIDE.OFF:
		SimulationMode := FALSE;
END_CASE]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Property Name="SimulationModeOverride" Id="{6e6cabcb-ebc2-4708-9ea7-397868fec8a7}">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
{attribute 'OPC.UA.DA' := '1'}
{attribute 'OPC.UA.DA.Access' := '1'}
PROPERTY FINAL SimulationModeOverride : SIMULATION_MODE_OVERRIDE]]></Declaration>
      <Get Name="Get" Id="{0e59a390-e0c3-414d-9d0f-a68ec8268d2a}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[SimulationModeOverride := _SimulationModeOverride;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Property Name="SubDeviceCount" Id="{2d761867-fcd1-4bc5-9cfb-2484409c8387}">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
{attribute 'OPC.UA.DA' := '1'}
{attribute 'OPC.UA.DA.Access' := '1'}
PROPERTY FINAL SubDeviceCount : INT]]></Declaration>
      <Get Name="Get" Id="{d4ece2cb-fe24-49ac-9255-b38b3e164b95}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[SubDeviceCount := _SubDeviceCount_;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Property Name="SubDevices" Id="{1e88dec3-27f0-4521-bc05-b72ec5701719}">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
PROPERTY ABSTRACT SubDevices : REFERENCE TO ARRAY [0.._SubDeviceCount_ - 1] OF IDevice]]></Declaration>
      <Get Name="Get" Id="{6a9df5c9-cd5a-4034-84fe-eb5b2eadb4ad}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Method Name="TryGetSubDeviceByIndex" Id="{c038517e-a5af-4cde-8680-a9be36714e91}">
      <Declaration><![CDATA[METHOD FINAL TryGetSubDeviceByIndex : IDevice
VAR_INPUT
	index : BYTE;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF index < 0 OR_ELSE index >= _SubDeviceCount_ OR_ELSE NOT __ISVALIDREF(SubDevices) THEN
	RETURN;
END_IF

TryGetSubDeviceByIndex := SubDevices[index];]]></ST>
      </Implementation>
    </Method>
    <Method Name="TrySetSimulationModeOverride" Id="{037872d8-8455-4270-b703-fa8d0169ad6e}">
      <Declaration><![CDATA[{attribute 'TcRpcEnable':='1'}
METHOD FINAL TrySetSimulationModeOverride : BOOL
VAR_INPUT
	value : SIMULATION_MODE_OVERRIDE;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF _SimulationModeOverride = value THEN
	TrySetSimulationModeOverride := TRUE;
	RETURN;
END_IF
  
IF ChildSimulationModeCanBeChanged THEN
	_SimulationModeOverride := value;
	
	TrySetSimulationModeOverride := TRUE;	
END_IF]]></ST>
      </Implementation>
    </Method>
  </POU>
</TcPlcObject>