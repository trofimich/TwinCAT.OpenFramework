<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1">
  <POU Name="OutputDevice" Id="{105b6dfa-d76f-4477-807a-cf1396dca32a}" SpecialFunc="None">
    <Declaration><![CDATA[(*

.. <legal notes>

legal notes
=================
| SPDX-FileCopyrightText: © 2026 Oleksandr Tiutyk <trofimich@gmail.com>
| SPDX-License-Identifier: LGPL-3.0-only
| For details check: LGPL-3.0-only_

.. _LGPL-3.0-only: https://www.gnu.org/licenses/lgpl-3.0.en.html

.. </legal notes>

*)

FUNCTION_BLOCK ABSTRACT OutputDevice
VAR_GENERIC CONSTANT
    _SubDeviceCount : INT(0..1000) := 0;
END_VAR
EXTENDS Device<_SubDeviceCount> IMPLEMENTS IOutputDevice]]></Declaration>
    <Implementation>
      <ST><![CDATA[]]></ST>
    </Implementation>
    <Folder Name="Methods to override" Id="{79900fc6-701a-41aa-8938-d0017f4e6cae}" />
    <Method Name="OnOutputProcessed" Id="{3d84b989-6e8b-4262-b050-07a61a496aab}" FolderPath="Methods to override\">
      <Declaration><![CDATA[METHOD PROTECTED OnOutputProcessed
VAR_INPUT
	exception : Core.IAggregateException;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[]]></ST>
      </Implementation>
    </Method>
    <Method Name="OnOutputProcessing" Id="{836154ee-dcb9-4012-94b0-addbb879e72d}" FolderPath="Methods to override\">
      <Declaration><![CDATA[METHOD PROTECTED OnOutputProcessing]]></Declaration>
      <Implementation>
        <ST><![CDATA[]]></ST>
      </Implementation>
    </Method>
    <Method Name="ProcessOutput" Id="{e860c3bd-fe5d-4af5-8b65-0b4dfced7747}">
      <Declaration><![CDATA[METHOD FINAL ProcessOutput
VAR
	i : INT;
	
	outputSubDevice : IOutputDevice;
	
	exceptionCode : __SYSTEM.ExceptionCode;
	exception : IException;
	
	aggregateException : Core.AggregateException;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[__TRY
	OnOutputProcessing();
__CATCH(exceptionCode)
	exception := ExceptionManager.GetLastException(exceptionCode);
				
	aggregateException.AddInnerException(exception);
__ENDTRY

IF __ISVALIDREF(SubDevices) AND_THEN _SubDeviceCount > 0 THEN
	FOR i := 0 TO _SubDeviceCount - 1 DO
		IF NOT __QUERYINTERFACE(SubDevices[i], outputSubDevice) THEN
			CONTINUE;
		END_IF
		
		__TRY
			outputSubDevice.ProcessOutput();
		__CATCH(exceptionCode)
			exception := ExceptionManager.GetLastException(exceptionCode);
						
			aggregateException.AddInnerException(exception);
		__ENDTRY 
	END_FOR	
END_IF

__TRY
	OnOutputProcessed(aggregateException);
__CATCH(exceptionCode)
	exception := ExceptionManager.GetLastException(exceptionCode);
				
	aggregateException.AddInnerException(exception);
__ENDTRY

IF aggregateException.InnerExceptionCount > 0 THEN
	aggregateException.Throw(NamespaceName, ClassName, __POUNAME(), __POSITION());
END_IF]]></ST>
      </Implementation>
    </Method>
  </POU>
</TcPlcObject>