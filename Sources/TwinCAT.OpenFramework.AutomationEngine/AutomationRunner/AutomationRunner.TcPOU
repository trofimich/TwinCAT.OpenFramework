<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1">
  <POU Name="AutomationRunner" Id="{142225ba-ead8-4c1c-a7bd-6f13f6bbc05e}" SpecialFunc="None">
    <Declaration><![CDATA[(*

.. <legal notes>

legal notes
=================
| SPDX-FileCopyrightText: © 2026 Oleksandr Tiutyk <trofimich@gmail.com>
| SPDX-License-Identifier: LGPL-3.0-only
| For details check: LGPL-3.0-only_

.. _LGPL-3.0-only: https://www.gnu.org/licenses/lgpl-3.0.en.html

.. </legal notes>

*)

{attribute 'no_explicit_call' := 'do not call this function block directly'} 
FUNCTION_BLOCK ABSTRACT AutomationRunner
VAR_GENERIC CONSTANT
    _ControllerCount : BYTE := 1;
END_VAR
EXTENDS Core.Object IMPLEMENTS IAutomationRunner
VAR_OUTPUT
	InitializationErrorMessage : Core.ERROR_MESSAGE;
END_VAR
VAR
	_State : AUTOMATION_RUNNER_STATE;
END_VAR
VAR_STAT
    _InstanceCounter : BYTE := 0;
END_VAR]]></Declaration>
    <Implementation>
      <ST><![CDATA[]]></ST>
    </Implementation>
    <Property Name="CodeExecutionPhase" Id="{c4f37de1-c494-4e98-bff8-e5a29ee70657}">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
PROPERTY FINAL CodeExecutionPhase : BOOL]]></Declaration>
      <Get Name="Get" Id="{cca78e62-5e2b-457e-91fc-ec64208d7a39}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[CodeExecutionPhase := GlobalState.CodeExecutionPhase;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Property Name="ControllerCount" Id="{b763fe3e-ac45-40f7-8e0a-80bac18bc801}">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
PROPERTY ControllerCount : BYTE]]></Declaration>
      <Get Name="Get" Id="{41fb60e1-30ec-4999-8fd3-d29a9cdfc0bd}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[ControllerCount := _ControllerCount;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Property Name="Controllers" Id="{d916e7c7-2f82-41ab-b518-da33d8b6a0ac}">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
PROPERTY ABSTRACT Controllers : REFERENCE TO ARRAY [0.._ControllerCount - 1] OF IAutomationController]]></Declaration>
      <Get Name="Get" Id="{bbac70c1-0c12-4a6e-b63c-959272410e00}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Property Name="CycleCounter" Id="{b44e2d41-8633-4c1c-ad38-18becdb27866}">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
PROPERTY FINAL CycleCounter : LWORD]]></Declaration>
      <Get Name="Get" Id="{3b2c949f-bedb-4db8-bff6-1b934597b8a9}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[CycleCounter := GlobalState.CycleCounter;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Method Name="Execute" Id="{cddfd6b6-d698-4df7-b955-3cc076d37dea}">
      <Declaration><![CDATA[METHOD FINAL Execute
VAR
	i : INT;
			
	initialized : BOOL;
	
	exceptionCode : __SYSTEM.ExceptionCode;
	exception : IException;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[GlobalState.CodeExecutionPhase := TRUE;

Core.SystemDateTimeManager.Execute();		

Core.LogManager.Execute();

IF Core.SystemDateTimeManager.SystemTimeValid THEN
	//_ExecutionContext.CycleLocalStartTime := Core.SystemDateTimeManager.SystemLocalDateTime;
	//_ExecutionContext.CycleUtcStartTime := Core.SystemDateTimeManager.SystemUtcDateTime;
	
	CASE _State OF
		AUTOMATION_RUNNER_STATE.INITIALIZING:
			IF NOT __ISVALIDREF(Controllers) THEN
				_State := AUTOMATION_RUNNER_STATE.INITIALIZATION_FAILED;		
				InitializationErrorMessage := "Controllers property is null";
				RETURN;		
			END_IF

			initialized := TRUE;
			
			FOR i := 0 TO _ControllerCount - 1 DO
				IF Controllers[i] = 0 THEN
					_State := AUTOMATION_RUNNER_STATE.INITIALIZATION_FAILED;
					InitializationErrorMessage := WideStrings.ConcatStrings255("Controller[", TO_WSTRING(i), "] is null");
					EXIT;					
				END_IF

				Controllers[i].Initialize();
				
				CASE Controllers[i].State OF
					AUTOMATION_CONTROLLER_STATE.FAULT:
						_State := AUTOMATION_RUNNER_STATE.INITIALIZATION_FAILED;
						InitializationErrorMessage := WideStrings.ConcatStrings255("Controller[", TO_WSTRING(i), "] failed");
						EXIT;
						
					AUTOMATION_CONTROLLER_STATE.INITIAL:
						initialized := FALSE;						
				END_CASE
			END_FOR		
			
			IF initialized THEN
				_State := AUTOMATION_RUNNER_STATE.RUNNING;
			END_IF
		
		AUTOMATION_RUNNER_STATE.RUNNING:
			FOR i := 0 TO _ControllerCount - 1 DO
				Controllers[i].Execute();			
			END_FOR		
	END_CASE

END_IF

Core.ExceptionManager.Cleanup();
Core.DynamicMemoryManager.CleanupMemory();

GlobalState.CycleCounter := GlobalState.CycleCounter + 1;
GlobalState.CodeExecutionPhase := FALSE;]]></ST>
      </Implementation>
    </Method>
    <Method Name="FB_init" Id="{d9dc6fd8-04ec-4586-b655-20027fb1c3eb}">
      <Declaration><![CDATA[METHOD FB_init : BOOL
VAR_INPUT
	(* if TRUE, the retain variables are initialized (warm start / cold start)*)
	bInitRetains	: BOOL;
	(* if TRUE, the instance afterwards gets moved into the copy code (online change)*)
	bInCopyCode	: BOOL;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[_InstanceCounter := _InstanceCounter + 1;]]></ST>
      </Implementation>
    </Method>
    <Property Name="State" Id="{f2dce25c-3e10-4ca3-baf9-9dc113064c6d}">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
PROPERTY FINAL State : AUTOMATION_RUNNER_STATE]]></Declaration>
      <Get Name="Get" Id="{62ee6d12-cc93-4e49-aed3-a8cc7d8a5a22}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[State := _State;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Property Name="StateName" Id="{58b8e5b1-72bd-47bf-97d3-37a11efb695b}">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
PROPERTY StateName : STRING]]></Declaration>
      <Get Name="Get" Id="{b78f5aab-7d64-4910-9ee4-7e1a78e3105f}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[StateName := TO_STRING(_State);]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Method Name="TryGetAutomationControllerByIndex" Id="{24622356-d6c8-4a30-a975-e7a7aa358d86}">
      <Declaration><![CDATA[METHOD FINAL TryGetAutomationControllerByIndex : IAutomationController
VAR_INPUT
	index : BYTE;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF index < 0 OR_ELSE index >= _ControllerCount OR_ELSE NOT __ISVALIDREF(Controllers) THEN
	RETURN;
END_IF

TryGetAutomationControllerByIndex := Controllers[index];]]></ST>
      </Implementation>
    </Method>
  </POU>
</TcPlcObject>