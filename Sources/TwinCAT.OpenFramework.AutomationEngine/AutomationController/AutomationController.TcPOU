<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1">
  <POU Name="AutomationController" Id="{715bcab9-6d21-43bf-ac33-fb2affdac1da}" SpecialFunc="None">
    <Declaration><![CDATA[(*

.. <legal notes>

legal notes
=================
| SPDX-FileCopyrightText: © 2026 Oleksandr Tiutyk <trofimich@gmail.com>
| SPDX-License-Identifier: LGPL-3.0-only
| For details check: LGPL-3.0-only_

.. _LGPL-3.0-only: https://www.gnu.org/licenses/lgpl-3.0.en.html

.. </legal notes>

*)

FUNCTION_BLOCK ABSTRACT AutomationController 
VAR_GENERIC CONSTANT
    _DeviceCount : INT(0..1000) := 0;
END_VAR
EXTENDS Core.Object IMPLEMENTS IAutomationController
VAR
	_ExceptionHandler : Core.IExceptionHandler;
	
	_Enabled : BOOL := TRUE;
			
	_SimulationMode : BOOL;

	_State : AUTOMATION_CONTROLLER_STATE;	
	_PreviousState : AUTOMATION_CONTROLLER_STATE;	

	_Diagnostics : AUTOMATION_CONTROLLER_DIAGNOSTICS;
END_VAR]]></Declaration>
    <Implementation>
      <ST><![CDATA[]]></ST>
    </Implementation>
    <Folder Name="Methods to override" Id="{18713aca-9c47-4769-936e-cd3be2ed069c}">
      <Folder Name="EnabledChange" Id="{7cc34a9f-6118-4793-9de6-359b78cb7be4}" />
      <Folder Name="Initialization" Id="{42d1bd21-062f-45b1-a165-3fc00e8443d8}" />
      <Folder Name="SimulationModeChange" Id="{e7dbeee9-12ad-4d8b-b88c-86c803e2d26d}" />
    </Folder>
    <Folder Name="Service memebers" Id="{5df4ad11-a7ac-4049-8c04-41c0497c7559}" />
    <Property Name="DeviceCount" Id="{612962ce-4a03-4dcb-a1ce-f9e94eefa0eb}">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
PROPERTY DeviceCount : INT]]></Declaration>
      <Get Name="Get" Id="{cc518b3a-e440-4848-8075-189e5ee4e093}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[DeviceCount := _DeviceCount;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Property Name="Devices" Id="{f1c94009-408f-4558-836f-c0ffe07e995d}">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
PROPERTY ABSTRACT Devices : REFERENCE TO ARRAY [0.._DeviceCount - 1] OF Automation.IDevice]]></Declaration>
      <Get Name="Get" Id="{3053aa14-2407-42d1-bc4d-bd38e5a9f72c}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Property Name="Diagnostics" Id="{89d80bd2-f2bc-45f4-b861-f9194e4bb6a5}">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
PROPERTY FINAL Diagnostics : REFERENCE TO AUTOMATION_CONTROLLER_DIAGNOSTICS]]></Declaration>
      <Get Name="Get" Id="{6974a2c5-c336-4c3a-a18a-82f762fc3656}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[Diagnostics REF= _Diagnostics;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Method Name="Disable" Id="{94567983-f3f5-4266-9cfb-3c5f4ea14259}">
      <Declaration><![CDATA[METHOD FINAL Disable
VAR
	i : INT;

	exceptionCode : __SYSTEM.ExceptionCode;
	exception : IException;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[__TRY
	OnEnabledChanging(FALSE);
	
	IF __ISVALIDREF(Devices) AND_THEN _DeviceCount > 0 THEN
		FOR i := 0 TO _DeviceCount - 1 DO
			Devices[i].Disable();
		END_FOR	
	END_IF
	
	_Enabled := FALSE;
	
	OnEnabledChanged();
__CATCH(exceptionCode)	
	_State := AUTOMATION_CONTROLLER_STATE.FAULT;

	exception := ExceptionManager.GetLastException(exceptionCode);	

	fillErrorLog('LastDisableError', Diagnostics.LastDisableError, exception);					
__ENDTRY]]></ST>
      </Implementation>
    </Method>
    <Method Name="Enable" Id="{df8c9490-4eb4-4f45-9a7b-8c348978a15b}">
      <Declaration><![CDATA[METHOD FINAL Enable
VAR
	i : INT;

	exceptionCode : __SYSTEM.ExceptionCode;
	exception : IException;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[__TRY
	OnEnabledChanging(TRUE);
	
	IF __ISVALIDREF(Devices) AND_THEN _DeviceCount > 0 THEN
		FOR i := 0 TO _DeviceCount - 1 DO
			Devices[i].Enable();
		END_FOR	
	END_IF
	
	_Enabled := TRUE;
	
	OnEnabledChanged();
__CATCH(exceptionCode)	
	_State := AUTOMATION_CONTROLLER_STATE.FAULT;

	exception := ExceptionManager.GetLastException(exceptionCode);	

	fillErrorLog('LastEnableError', Diagnostics.LastEnableError, exception);					
__ENDTRY]]></ST>
      </Implementation>
    </Method>
    <Property Name="Enabled" Id="{d5768b1c-6fec-4453-809d-b40c8bd349ff}">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
PROPERTY FINAL Enabled : BOOL]]></Declaration>
      <Get Name="Get" Id="{a02f7fac-5fa7-4362-9e5d-241f8804e466}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[Enabled := _Enabled;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Property Name="ExceptionHandler" Id="{60a29608-799c-49a7-a0f5-81ee4e55824c}">
      <Declaration><![CDATA[PROPERTY FINAL ExceptionHandler : Core.IExceptionHandler]]></Declaration>
      <Get Name="Get" Id="{fae050a0-52d3-4847-a89b-06de749e48ee}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[ExceptionHandler := _ExceptionHandler;]]></ST>
        </Implementation>
      </Get>
      <Set Name="Set" Id="{0ab07e6b-d588-44a7-8de9-0695649fe2c2}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[_ExceptionHandler := ExceptionHandler;]]></ST>
        </Implementation>
      </Set>
    </Property>
    <Method Name="Execute" Id="{edf960fe-6c94-4ace-a7be-524e71aafd2a}">
      <Declaration><![CDATA[METHOD FINAL Execute
VAR
	invalidStateException : Core.StandardException;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[_PreviousState := _State;

IF NOT Enabled THEN
	RETURN;
END_IF

IF _State = AUTOMATION_CONTROLLER_STATE.INITIAL THEN
	invalidStateException.Throw(NamespaceName, ClassName, __POUNAME(), __POSITION(), "Automation controller is not initialized");
END_IF

handleInputDevices();

handleWorkingStates();

handleOutputDevices();
]]></ST>
      </Implementation>
    </Method>
    <Method Name="fillErrorLog" Id="{13ecfb4e-9729-40c0-b5b7-a5c89543cb1f}" FolderPath="Service memebers\">
      <Declaration><![CDATA[METHOD PRIVATE fillErrorLog
VAR_INPUT
	errorPlace : STRING(39);
END_VAR
VAR_IN_OUT
	targetErrorLog : AUTOMATION_ERROR_LOG;
	sourceException : IException;  
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[_Diagnostics.LastErrorPalce := errorPlace;

targetErrorLog.ShortMessage := sourceException.GetMessage(FALSE, FALSE);
targetErrorLog.FullMessage := sourceException.GetMessage(TRUE, TRUE);
targetErrorLog.Source := sourceException.GetSourceDescription();
targetErrorLog.LocalTimestamp := sourceException.LocalTimestamp;
targetErrorLog.UtcTimestamp := sourceException.UtcTimestamp;

Core.LogManager.TryLogException(sourceException, "AutomationControllerError");]]></ST>
      </Implementation>
    </Method>
    <Method Name="handleInputDevices" Id="{cd23818a-f724-4bce-970a-f4c5c7f1d1fb}" FolderPath="Service memebers\">
      <Declaration><![CDATA[METHOD PRIVATE handleInputDevices
VAR
	i : DINT;

	exceptionCode : __SYSTEM.ExceptionCode;
	exception : IException;
	
	inputDevice : Automation.IInputDevice;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF __ISVALIDREF(Devices) AND_THEN _DeviceCount > 0 THEN
	FOR i := 0 TO _DeviceCount - 1 DO
		IF NOT __QUERYINTERFACE(Devices[i], inputDevice) OR_ELSE NOT inputDevice.Enabled THEN
			CONTINUE;
		END_IF
		
		__TRY
			inputDevice.ProcessInput();
		__CATCH(exceptionCode)
			exception := ExceptionManager.GetLastException(exceptionCode);
						
			fillErrorLog('LastRefreshInputDeviceError', Diagnostics.LastRefreshInputDeviceError, exception);
					
			IF _State <> AUTOMATION_CONTROLLER_STATE.FAULT THEN
				setState(AUTOMATION_CONTROLLER_STATE.FAULT);				
			END_IF					
		__ENDTRY
	END_FOR
END_IF]]></ST>
      </Implementation>
    </Method>
    <Method Name="handleOutputDevices" Id="{421f0782-6afa-4e82-84cb-a5cc3ea35f21}" FolderPath="Service memebers\">
      <Declaration><![CDATA[METHOD PRIVATE handleOutputDevices
VAR
	i : DINT;

	exceptionCode : __SYSTEM.ExceptionCode;
	exception : IException;
	
	outputDevice : Automation.IOutputDevice;	
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF __ISVALIDREF(Devices) AND_THEN _DeviceCount > 0 THEN
	FOR i := 0 TO _DeviceCount - 1 DO
		IF NOT __QUERYINTERFACE(Devices[i], outputDevice) OR_ELSE NOT outputDevice.Enabled THEN
			CONTINUE;
		END_IF
		
		__TRY
			outputDevice.ProcessOutput(); 
		__CATCH(exceptionCode)
			exception := ExceptionManager.GetLastException(exceptionCode);
						
			fillErrorLog('LastRefreshOutputDeviceError', Diagnostics.LastRefreshOutputDeviceError, exception);

			IF _State <> AUTOMATION_CONTROLLER_STATE.FAULT THEN
				setState(AUTOMATION_CONTROLLER_STATE.FAULT);				
			END_IF					
		__ENDTRY 
	END_FOR
END_IF]]></ST>
      </Implementation>
    </Method>
    <Method Name="handleWorkingStates" Id="{bb165360-ba12-474f-a548-167a2e5dd0a1}" FolderPath="Service memebers\">
      <Declaration><![CDATA[METHOD PRIVATE handleWorkingStates
VAR
	i : DINT;

	exceptionCode : __SYSTEM.ExceptionCode;
	exception : IException;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[__TRY
	CASE _State OF
		AUTOMATION_CONTROLLER_STATE.WORKING:
			OnWorking();
			
		AUTOMATION_CONTROLLER_STATE.FAULT:
			OnFault();
			
		AUTOMATION_CONTROLLER_STATE.RESETTING:			
			IF OnResetting() THEN
				setState(AUTOMATION_CONTROLLER_STATE.WORKING);
			END_IF			
	END_CASE
__CATCH(exceptionCode)	
	exception := ExceptionManager.GetLastException(exceptionCode);	

	CASE _State OF
		AUTOMATION_CONTROLLER_STATE.WORKING:
			fillErrorLog('LastRunningError', Diagnostics.LastRunningError, exception);			

		AUTOMATION_CONTROLLER_STATE.FAULT:
			fillErrorLog('LastFaultError', Diagnostics.LastFaultError, exception);
					
		AUTOMATION_CONTROLLER_STATE.RESETTING:		
			fillErrorLog('LastResettingError', Diagnostics.LastResettingError, exception);					
	END_CASE
	
	IF _ExceptionHandler = 0 OR_ELSE NOT _ExceptionHandler.HandleException(exception) THEN
		setState(AUTOMATION_CONTROLLER_STATE.FAULT);
	END_IF						
__ENDTRY]]></ST>
      </Implementation>
    </Method>
    <Method Name="Initialize" Id="{eb1221b0-f976-4a46-99f2-a6494cff3a27}">
      <Declaration><![CDATA[METHOD FINAL Initialize
VAR
	initialized : BOOL;
	
	i : DINT;

	exceptionCode : __SYSTEM.ExceptionCode;
	exception : IException;
	
	deviceNotSpecifiedException : DataMemberNotSpecifiedException;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF _State <> AUTOMATION_CONTROLLER_STATE.INITIAL THEN
	RETURN;
END_IF

__TRY
	IF _DeviceCount > 0 AND_THEN NOT __ISVALIDREF(Devices) THEN
		deviceNotSpecifiedException.Throw(Namespacename, ClassName, __POUNAME(), __POSITION(), 'Devices');
	END_IF
	
	initialized := OnPreDeviceInitializing();

	IF _DeviceCount > 0 THEN
		FOR i := 0 TO _DeviceCount - 1 DO
			IF Devices[i] = 0 THEN
				deviceNotSpecifiedException.Throw(Namespacename, ClassName, __POUNAME(), __POSITION(), Strings.ConcatStrings255('Devices[', TO_STRING(i), ']'));			
			END_IF
			
			initialized := initialized AND Devices[i].Initialize();
		END_FOR
	END_IF

	initialized := initialized AND OnPostDeviceInitializing();
	
	IF initialized THEN
		setState(AUTOMATION_CONTROLLER_STATE.WORKING);
	END_IF	
__CATCH(exceptionCode)
	setState(AUTOMATION_CONTROLLER_STATE.FAULT);
	
	exception := ExceptionManager.GetLastException(exceptionCode);
					
	fillErrorLog('InitializationError', Diagnostics.InitializationError, exception);					
__ENDTRY]]></ST>
      </Implementation>
    </Method>
    <Method Name="OnEnabledChanged" Id="{dd63764b-f122-4f50-a25d-0df5144930d7}" FolderPath="Methods to override\EnabledChange\">
      <Declaration><![CDATA[METHOD PROTECTED OnEnabledChanged]]></Declaration>
      <Implementation>
        <ST><![CDATA[]]></ST>
      </Implementation>
    </Method>
    <Method Name="OnEnabledChanging" Id="{d1fcb6d0-b53a-4b1a-b954-eb5cd2d33bc4}" FolderPath="Methods to override\EnabledChange\">
      <Declaration><![CDATA[METHOD PROTECTED OnEnabledChanging
VAR_INPUT
	newValue : BOOL;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[]]></ST>
      </Implementation>
    </Method>
    <Method Name="OnFault" Id="{47b787a0-74db-428e-a805-17453cf9f9ee}" FolderPath="Methods to override\">
      <Declaration><![CDATA[METHOD PROTECTED OnFault]]></Declaration>
      <Implementation>
        <ST><![CDATA[]]></ST>
      </Implementation>
    </Method>
    <Method Name="OnPostDeviceInitializing" Id="{b196f5d6-1614-4d95-80dc-69c224e94b14}" FolderPath="Methods to override\Initialization\">
      <Declaration><![CDATA[METHOD PROTECTED OnPostDeviceInitializing : BOOL]]></Declaration>
      <Implementation>
        <ST><![CDATA[OnPostDeviceInitializing := TRUE;]]></ST>
      </Implementation>
    </Method>
    <Method Name="OnPreDeviceInitializing" Id="{f30a4c4a-7966-446f-95b6-a1ce70efb536}" FolderPath="Methods to override\Initialization\">
      <Declaration><![CDATA[METHOD PROTECTED OnPreDeviceInitializing : BOOL]]></Declaration>
      <Implementation>
        <ST><![CDATA[OnPreDeviceInitializing := TRUE;]]></ST>
      </Implementation>
    </Method>
    <Method Name="OnResetting" Id="{26bb7324-2a54-4dd6-91d7-07c8f2cb1be0}" FolderPath="Methods to override\">
      <Declaration><![CDATA[METHOD PROTECTED OnResetting : BOOL]]></Declaration>
      <Implementation>
        <ST><![CDATA[OnResetting := TRUE;]]></ST>
      </Implementation>
    </Method>
    <Method Name="OnSimulationModeChanged" Id="{98e58809-7aad-4c0f-a45c-a1267872df0f}" FolderPath="Methods to override\SimulationModeChange\">
      <Declaration><![CDATA[METHOD PROTECTED OnSimulationModeChanged]]></Declaration>
      <Implementation>
        <ST><![CDATA[]]></ST>
      </Implementation>
    </Method>
    <Method Name="OnSimulationModeChanging" Id="{b12912cf-6c5d-475c-8063-64601bc950e5}" FolderPath="Methods to override\SimulationModeChange\">
      <Declaration><![CDATA[METHOD PROTECTED OnSimulationModeChanging
VAR_INPUT
	newValue : BOOL;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[]]></ST>
      </Implementation>
    </Method>
    <Method Name="OnWorking" Id="{f7f57440-8e03-4827-9c8c-4741292bf658}" FolderPath="Methods to override\">
      <Declaration><![CDATA[METHOD PROTECTED ABSTRACT OnWorking]]></Declaration>
      <Implementation>
        <ST><![CDATA[]]></ST>
      </Implementation>
    </Method>
    <Method Name="Reset" Id="{d15376e1-1a99-49f6-b2b0-b5cf3e7f7e51}">
      <Declaration><![CDATA[METHOD FINAL Reset : BOOL
VAR_OUTPUT
	refuseReason : WSTRING(255);
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF _State <> AUTOMATION_CONTROLLER_STATE.FAULT THEN
	refuseReason := WideStrings.ConcatStrings255("Expected controller state is 'FAULT', but actual controller state is '", TO_WSTRING(_State), "'");
	RETURN;
END_IF

setState(AUTOMATION_CONTROLLER_STATE.RESETTING);

Reset := TRUE;]]></ST>
      </Implementation>
    </Method>
    <Method Name="SetSimulationMode" Id="{3acd1d25-f788-4c6d-9785-9ccf62e4ddbf}">
      <Declaration><![CDATA[METHOD SetSimulationMode
VAR_INPUT
	value : BOOL;
END_VAR
VAR
	i : INT;

	exceptionCode : __SYSTEM.ExceptionCode;
	exception : IException;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[__TRY
	OnSimulationModeChanging(value);
	
	IF __ISVALIDREF(Devices) AND_THEN _DeviceCount > 0 THEN
		FOR i := 0 TO _DeviceCount - 1 DO
			Devices[i].SetSimulationModeOverride(Devices[i].SimulationModeOverride);
		END_FOR	
	END_IF
	
	_SimulationMode := value;
	
	OnSimulationModeChanged();
__CATCH(exceptionCode)	
	_State := AUTOMATION_CONTROLLER_STATE.FAULT;

	exception := ExceptionManager.GetLastException(exceptionCode);	

	fillErrorLog('LastSimulationModeChangeError', Diagnostics.LastSimulationModeChangeError, exception);					
__ENDTRY]]></ST>
      </Implementation>
    </Method>
    <Method Name="setState" Id="{9b9897ff-6da5-467a-bc24-ba7a46c549d1}" FolderPath="Service memebers\">
      <Declaration><![CDATA[METHOD PRIVATE setState
VAR_INPUT
	state : AUTOMATION_CONTROLLER_STATE;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[_PreviousState := _State;
_State := state;]]></ST>
      </Implementation>
    </Method>
    <Property Name="SimulationMode" Id="{d06903e4-ea01-42fc-a468-b432f4fc159b}">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
PROPERTY FINAL SimulationMode : BOOL]]></Declaration>
      <Get Name="Get" Id="{c0b7b53d-f5eb-4914-b43e-57840bfbc5da}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[SimulationMode := _SimulationMode;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Property Name="State" Id="{b57e32a3-ebf4-4492-89c3-02193601134f}">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
PROPERTY FINAL State : AUTOMATION_CONTROLLER_STATE]]></Declaration>
      <Get Name="Get" Id="{e675c2b5-5934-4e03-a0b0-61bb8b8e344b}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[State := _State;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Property Name="StateChanged" Id="{2d2d514b-9477-4fbb-b4e8-52562c635954}">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
PROPERTY FINAL StateChanged : BOOL]]></Declaration>
      <Get Name="Get" Id="{c4932c37-19e2-4343-9ad0-6d106776d675}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[StateChanged := _State <> _PreviousState;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Property Name="StateName" Id="{1f460bbf-201a-476c-9987-4ccb4784c5c5}">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
PROPERTY StateName : STRING]]></Declaration>
      <Get Name="Get" Id="{4fed4a37-5097-4a72-961f-12fff28b9f42}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[StateName := TO_STRING(_State);]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Method Name="TryGetDeviceByIndex" Id="{822b2535-548b-4af3-814e-271dc3e4c03f}">
      <Declaration><![CDATA[METHOD FINAL TryGetDeviceByIndex : Automation.IDevice
VAR_INPUT
	index : BYTE;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF index < 0 OR_ELSE index >= _DeviceCount OR_ELSE NOT __ISVALIDREF(Devices) THEN
	RETURN;
END_IF

TryGetDeviceByIndex := Devices[index];]]></ST>
      </Implementation>
    </Method>
  </POU>
</TcPlcObject>