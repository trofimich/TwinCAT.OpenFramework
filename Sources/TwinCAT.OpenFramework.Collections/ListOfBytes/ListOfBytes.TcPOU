<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1">
  <POU Name="ListOfBytes" Id="{44085af7-7241-03f1-3e6d-aeb87f0c58dd}" SpecialFunc="None">
    <Declaration><![CDATA[(*

.. <legal notes>

legal notes
=================
| SPDX-FileCopyrightText: © 2025 Oleksandr Tiutyk <trofimich@gmail.com>
| SPDX-License-Identifier: LGPL-3.0-only
| For details check: LGPL-3.0-only_

.. _LGPL-3.0-only: https://www.gnu.org/licenses/lgpl-3.0.en.html

.. </legal notes>

*)

{attribute 'no_explicit_call' := 'do not call this function block directly'} 
{attribute 'enable_dynamic_creation'}
// Collection of bytes of dynamic size
FUNCTION_BLOCK ListOfBytes EXTENDS Core.Object IMPLEMENTS IListOfBytes
VAR_OUTPUT
	DataAddress : POINTER TO BYTE;
END_VAR
VAR
	_ClassName : STRING := __POUNAME();
	
	_MemorySize : DINT;
	_BytesCount : DINT;
END_VAR]]></Declaration>
    <Implementation>
      <ST><![CDATA[]]></ST>
    </Implementation>
    <Method Name="AppendAny" Id="{fa939ac2-b614-4bc3-8ef3-f524865ffb5f}">
      <Declaration><![CDATA[METHOD AppendAny : IListOfBytes
VAR_INPUT
	data : ANY;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[AppendAny := AppendData(data.pValue, TO_UDINT(data.diSize));]]></ST>
      </Implementation>
    </Method>
    <Method Name="AppendByte" Id="{90a35576-aaee-0fbc-214a-d1c0f81c72fb}">
      <Declaration><![CDATA[METHOD AppendByte : IListOfBytes
VAR_INPUT
	value : BYTE;
END_VAR
VAR
	newMemory : POINTER TO BYTE;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[AppendByte := AppendData(ADR(value), 1);]]></ST>
      </Implementation>
    </Method>
    <Method Name="AppendData" Id="{d35c3ee4-7daa-49d5-be43-e476b3efb046}">
      <Declaration><![CDATA[METHOD AppendData : IListOfBytes
VAR_INPUT
	data : POINTER TO BYTE;
	dataSize : UDINT;
END_VAR
VAR
	newMemory : POINTER TO BYTE;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[AppendData := THIS^;

IF data = 0 OR_ELSE dataSize = 0 THEN
	RETURN;
END_IF

// if existing memory is not enough
IF _BytesCount + TO_DINT(dataSize) > _MemorySize THEN
	// calculate new memory size
	_MemorySize := _BytesCount + TO_DINT(dataSize);
	
	// allocate new memory
	newMemory := __NEW(BYTE, TO_UDINT(_MemorySize));
	
	// copy existing bytes to new memory if needed
	IF _BytesCount > 0 THEN
		Tc2_System.MEMCPY(newMemory, DataAddress, TO_UDINT(_BytesCount));		
	END_IF
	
	// release old memory
	IF DataAddress <> 0 THEN
		__DELETE(DataAddress);		
	END_IF
	
	// save new memory reference
	DataAddress := newMemory;
END_IF

// append new bytes
Tc2_System.MEMCPY(DataAddress + TO_DWORD(_BytesCount), data, dataSize);

// set new bytes count
_BytesCount := _BytesCount + TO_DINT(dataSize);]]></ST>
      </Implementation>
    </Method>
    <Method Name="AppendGeneric" Id="{ac72fb80-d130-4ae7-81e5-86ab3708db48}">
      <Declaration><![CDATA[METHOD AppendGeneric : IListOfBytes
VAR_INPUT
	data : Core.GENERIC_VALUE;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[AppendGeneric := AppendData(data.Address, TO_UDINT(data.Size));]]></ST>
      </Implementation>
    </Method>
    <Property Name="ClassName" Id="{3b9bf934-b83c-0948-370d-5e8e28db661a}">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
PROPERTY ClassName : STRING]]></Declaration>
      <Get Name="Get" Id="{a74d6b23-00f3-0fef-283c-b96c5cad2f8b}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[ClassName := _ClassName;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Method Name="Clear" Id="{58795b0c-c9a9-0b7d-1801-4867f25906b7}">
      <Declaration><![CDATA[METHOD Clear : IListOfBytes
VAR_INPUT
	releaseMemory : BOOL := FALSE;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[Clear := THIS^;

IF releaseMemory AND_THEN _MemorySize > 0 THEN
	__DELETE(DataAddress);
	_MemorySize := 0;
END_IF

_BytesCount := 0;]]></ST>
      </Implementation>
    </Method>
    <Property Name="Count" Id="{d1b0052d-4e1b-09cb-235e-e87df6b9b5a5}">
      <Declaration><![CDATA[{attribute 'OPC.UA.DA.Property' := '1'}
{attribute 'monitoring' := 'call'}
PROPERTY Count : DINT]]></Declaration>
      <Get Name="Get" Id="{457f3f94-ecd5-03a2-1012-5eb779d04c66}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[Count := _BytesCount;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Method Name="CreateEnumerator" Id="{9eb1bbc5-e145-4b1d-be02-fc14e3a7b7c4}">
      <Declaration><![CDATA[METHOD CreateEnumerator : IEnumerator
VAR
	enumerator : POINTER TO ListOfBytesEnumerator;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[enumerator := __NEW(ListOfBytesEnumerator(list := THIS^));

CreateEnumerator := enumerator^;]]></ST>
      </Implementation>
    </Method>
    <Method Name="FB_exit" Id="{58cee31c-ede5-0b41-17b5-f6dfb8c1dea7}">
      <Declaration><![CDATA[METHOD FB_exit : BOOL
VAR_INPUT
	bInCopyCode : BOOL; // if TRUE, the exit method is called for exiting an instance that is copied afterwards (online change).
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[Clear(TRUE);]]></ST>
      </Implementation>
    </Method>
    <Method Name="Find" Id="{f870e801-4961-0438-0486-eeff60dc3fea}">
      <Declaration><![CDATA[METHOD Find : BOOL
VAR_INPUT
	value : BYTE;
END_VAR
VAR_OUTPUT
	index : DINT;
END_VAR
VAR
	i : DINT;	
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF _BytesCount > 0 THEN
	FOR i := 0 TO _BytesCount - 1 DO
		IF DataAddress[i] = value THEN
			index := i;
			Find := TRUE;
			RETURN;
		END_IF 	
	END_FOR
END_IF]]></ST>
      </Implementation>
    </Method>
    <Method Name="Get" Id="{7a2d32dc-2bf1-07fe-3b69-aed2ad5b300d}">
      <Declaration><![CDATA[METHOD Get : BYTE
VAR_INPUT
	index : DINT;
END_VAR
VAR
	exception : StandardException;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF DataAddress = 0 THEN
	exception.Throw(NamespaceName, ClassName, __POUNAME(), __POSITION(), "Dynamic byte array is empty");
END_IF

IF index < 0 THEN
	exception.Throw(NamespaceName, ClassName, __POUNAME(), __POSITION(), "Index is less then zero");
END_IF

IF index >= _BytesCount THEN
	exception.Throw(NamespaceName, ClassName, __POUNAME(), __POSITION(), "Index is too large");
END_IF

Get := DataAddress[index];]]></ST>
      </Implementation>
    </Method>
    <Method Name="LoadFromAny" Id="{6ca12394-c5fc-0274-1ee4-d0bfc0727c06}">
      <Declaration><![CDATA[METHOD LoadFromAny : IListOfBytes
VAR_INPUT
	data : ANY;
	startIndex : UDINT := 0;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[LoadFromAny := LoadFromData(data.pValue, TO_UDINT(data.diSize), startIndex);]]></ST>
      </Implementation>
    </Method>
    <Method Name="LoadFromData" Id="{9fe65b5e-87b5-0537-23bd-6a15d825d74e}">
      <Declaration><![CDATA[METHOD LoadFromData : IListOfBytes
VAR_INPUT
	data : POINTER TO BYTE;
	dataSize : UDINT;
	startIndex : UDINT := 0;
END_VAR
VAR
	exception : StandardException;
	newMemory : POINTER TO BYTE;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF TO_DINT(startIndex) > _BytesCount THEN
	exception.Throw(NamespaceName, ClassName, __POUNAME(), __POSITION(), "Start index is too large");
END_IF

IF dataSize < 0 THEN
	exception.Throw(NamespaceName, ClassName, __POUNAME(), __POSITION(), "Source bytes count is less then zero");
END_IF

LoadFromData := THIS^;
 
IF data = 0 OR_ELSE dataSize = 0 THEN
	RETURN;
END_IF

IF TO_DINT(startIndex + dataSize) > _MemorySize THEN
	// calculate new memory size
	_MemorySize := TO_DINT(startIndex + dataSize);
	
	// allocate new memory
	newMemory := __NEW(BYTE, TO_UDINT(_MemorySize));
	
	// copy existing bytes to new memory if needed
	IF startIndex > 0 THEN
		Tc2_System.MEMCPY(newMemory, DataAddress, TO_UDINT(startIndex));		
	END_IF
	
	// append new bytes
	Tc2_System.MEMCPY(newMemory + TO_DWORD(startIndex), data, TO_UDINT(dataSize));
	
	// set new bytes count
	_BytesCount := _MemorySize;
	
	// release old memory
	IF DataAddress <> 0 THEN
		__DELETE(DataAddress);		
	END_IF
	
	// save new memory reference
	DataAddress := newMemory;
ELSE
	// append new bytes
	Tc2_System.MEMCPY(dataAddress + TO_DWORD(startIndex), data, TO_UDINT(dataSize));
	
	// set new bytes count
	_BytesCount := TO_DINT(startIndex + dataSize);	
END_IF]]></ST>
      </Implementation>
    </Method>
    <Method Name="LoadFromGeneric" Id="{803d0535-a6e1-4d89-878a-9e90b4d80911}">
      <Declaration><![CDATA[METHOD LoadFromGeneric : IListOfBytes
VAR_INPUT
	data : Core.GENERIC_VALUE;
	startIndex : UDINT := 0;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[LoadFromGeneric := LoadFromData(data.Address, TO_UDINT(data.Size), startIndex);]]></ST>
      </Implementation>
    </Method>
    <Property Name="NamespaceName" Id="{6affa192-b708-4407-b07b-3f99b221293a}">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
PROPERTY NamespaceName : STRING]]></Declaration>
      <Get Name="Get" Id="{08e774db-64ad-4b08-8c9f-7cf3351a9ee4}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[NamespaceName := CurrentNamespace.Name;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Method Name="Remove" Id="{bf82b30e-92dd-0d23-0038-cce05024df18}">
      <Declaration><![CDATA[METHOD Remove : IListOfBytes
VAR_INPUT
	value : BYTE;
END_VAR
VAR
	index : DINT;
	exception : StandardException;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF DataAddress = 0 THEN
	exception.Throw(NamespaceName, ClassName, __POUNAME(), __POSITION(), "Dynamic byte array is empty");
END_IF

IF count < 0 THEN
	exception.Throw(NamespaceName, ClassName, __POUNAME(), __POSITION(), "Count is less then zero");
END_IF

IF index + count > _BytesCount THEN
	exception.Throw(NamespaceName, ClassName, __POUNAME(), __POSITION(), "Index + count are outside the array bounds");
END_IF

Remove := THIS^;

IF Find(value, index => index) THEN
	RemoveAt(index, 1);
END_IF]]></ST>
      </Implementation>
    </Method>
    <Method Name="RemoveAt" Id="{13c7901e-f730-033f-2407-dd64a3fc2480}">
      <Declaration><![CDATA[METHOD RemoveAt : IListOfBytes
VAR_INPUT
	index : DINT;
	count : DINT := 1;
END_VAR
VAR
	exception : StandardException;
	newMemory : POINTER TO BYTE;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF DataAddress = 0 THEN
	exception.Throw(NamespaceName, ClassName, __POUNAME(), __POSITION(), "Dynamic byte array is empty");
END_IF

IF index < 0 THEN
	exception.Throw(NamespaceName, ClassName, __POUNAME(), __POSITION(), "Index is less then zero");
END_IF

IF count < 0 THEN
	exception.Throw(NamespaceName, ClassName, __POUNAME(), __POSITION(), "Count is less then zero");
END_IF

IF index + count > _BytesCount THEN
	exception.Throw(NamespaceName, ClassName, __POUNAME(), __POSITION(), "Index + count are outside the array bounds");
END_IF

RemoveAt := THIS^;

// calculate new memory size
_MemorySize := _BytesCount - count;

// allocate new memory
newMemory := __NEW(BYTE, TO_UDINT(_MemorySize)); 

// copy leading bytes if needed
IF index <> 0 THEN
	Tc2_System.MEMCPY(newMemory, DataAddress, TO_UDINT(index));
END_IF

// copy trailing bytes
Tc2_System.MEMCPY(newMemory + TO_DWORD(index), DataAddress + TO_DWORD(index + count), TO_UDINT(_BytesCount - index - count));

// release old buffer
__DELETE(DataAddress);

// save new buffer
DataAddress := newMemory;

// set bytes count
_BytesCount := _MemorySize;	]]></ST>
      </Implementation>
    </Method>
    <Method Name="Resize" Id="{8b327940-8027-042d-2b0a-ccea3f4d165c}">
      <Declaration><![CDATA[METHOD Resize : IListOfBytes
VAR_INPUT
	newBytesCount : DINT; 
END_VAR
VAR
	exception : StandardException;
	newMemory : POINTER TO BYTE;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF newBytesCount < 0 THEN
	exception.Throw(NamespaceName, ClassName, __POUNAME(), __POSITION(), "New bytes count is less then zero");
END_IF

Resize := THIS^;

// if existing memory is not enough
IF newBytesCount > _MemorySize THEN
	// calculate new memory size
	_MemorySize := newBytesCount;
	
	// allocate new memory
	newMemory := __NEW(BYTE, TO_UDINT(_MemorySize));
	
	// copy existing bytes to new memory if needed
	IF _BytesCount > 0 THEN
		Tc2_System.MEMCPY(newMemory, DataAddress, TO_UDINT(_BytesCount));		
	END_IF
	
	// release old memory
	IF DataAddress <> 0 THEN
		__DELETE(DataAddress);		
	END_IF
	
	// save new memory reference
	DataAddress := newMemory;
END_IF

// set new bytes count
_BytesCount := newBytesCount;	]]></ST>
      </Implementation>
    </Method>
    <Method Name="Set" Id="{7b62d34d-7ee9-07a4-2e13-b032abd0cd09}">
      <Declaration><![CDATA[METHOD Set : IListOfBytes
VAR_INPUT
	index : DINT;
	value : BYTE;
END_VAR
VAR
	exception : StandardException;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF DataAddress = 0 THEN
	exception.Throw(NamespaceName, ClassName, __POUNAME(), __POSITION(), "Dynamic byte array is empty");
END_IF

IF index < 0 THEN
	exception.Throw(NamespaceName, ClassName, __POUNAME(), __POSITION(), "Index is less then zero");
END_IF

IF index >= _BytesCount THEN
	exception.Throw(NamespaceName, ClassName, __POUNAME(), __POSITION(), "Index is too large");
END_IF

Set := THIS^;

DataAddress[index] := value;]]></ST>
      </Implementation>
    </Method>
    <Property Name="Size" Id="{eb116396-1c6b-495f-b284-5b142fa86f6c}">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
PROPERTY Size : ULINT]]></Declaration>
      <Get Name="Get" Id="{9d568fff-ebc6-4d27-9dd9-94ca65277b07}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[Size := XSIZEOF(THIS^);]]></ST>
        </Implementation>
      </Get>
    </Property>
  </POU>
</TcPlcObject>