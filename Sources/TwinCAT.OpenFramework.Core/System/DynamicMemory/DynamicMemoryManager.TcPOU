<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1">
  <POU Name="DynamicMemoryManager" Id="{dfd03ea7-11b7-42ce-b52e-f5a7df7a7ad3}" SpecialFunc="None">
    <Declaration><![CDATA[(*

.. <legal notes>

legal notes
=================
| SPDX-FileCopyrightText: © 2025 Oleksandr Tiutyk <trofimich@gmail.com>
| SPDX-License-Identifier: LGPL-3.0-only
| For details check: LGPL-3.0-only_

.. _LGPL-3.0-only: https://www.gnu.org/licenses/lgpl-3.0.en.html

.. </legal notes>

*)

PROGRAM DynamicMemoryManager
VAR
	_ClassName : STRING := __POUNAME();
	
	_AutoDisposedObjects : POINTER TO IObject;	
	_AutoDisposedPointerCount : DWORD;
	_AutoDisposedObjectCount : DWORD;

	_LastSystemTaskCycleCount : UDINT;

	_MemoryCleanupHelper : MemoryCleanupHelper;	
END_VAR
VAR CONSTANT
	ALLOCATE_POINTER_COUNT : BYTE := 10;
END_VAR]]></Declaration>
    <Implementation>
      <ST><![CDATA[]]></ST>
    </Implementation>
    <Folder Name="Service memebers" Id="{ead780a2-bf4c-47dc-a6d8-bac15c5bc6e0}" />
    <Property Name="ClassName" Id="{c33db740-6bfc-43f8-9d4d-602279175d35}">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
PROPERTY ClassName : STRING]]></Declaration>
      <Get Name="Get" Id="{fc745387-f67a-4d86-bdee-e470addbc3c6}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[ClassName := _ClassName;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Method Name="CleanupMemory" Id="{a080fe6a-f539-4ac0-9048-10f8d3587c3b}">
      <Declaration><![CDATA[METHOD CleanupMemory
VAR_INPUT
	releaseBuffer : BOOL := FALSE;
END_VAR
VAR
	i : ULINT;
	objectToDispose : POINTER TO Object;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF _AutoDisposedObjectCount > 0 THEN
	FOR i := 0 TO _AutoDisposedObjectCount - 1 DO
		DynamicMemoryManager.TryReleaseDynamicObject(_AutoDisposedObjects[i]);
	END_FOR
			
	_AutoDisposedObjectCount := 0;
END_IF

IF _AutoDisposedPointerCount > 0 AND_THEN releaseBuffer THEN
	__DELETE(_AutoDisposedObjects);
	
	_AutoDisposedPointerCount := 0;
END_IF]]></ST>
      </Implementation>
    </Method>
    <Method Name="cooperativeCleanupMemory" Id="{5754b7a5-6f2f-49c9-a633-b4906595d6ba}" FolderPath="Service memebers\">
      <Declaration><![CDATA[METHOD PRIVATE cooperativeCleanupMemory
VAR
	systemTaskInfo : Global.PlcTaskSystemInfo;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[systemTaskInfo := F_GetTaskInfo();

IF systemTaskInfo.CycleCount <> _LastSystemTaskCycleCount THEN
	_LastSystemTaskCycleCount := systemTaskInfo.CycleCount;
	
	CleanupMemory(FALSE);
END_IF]]></ST>
      </Implementation>
    </Method>
    <Method Name="IsDynamicData" Id="{4c5114c7-cc73-448a-9f8f-fa67bfde37e3}">
      <Declaration><![CDATA[METHOD IsDynamicData : BOOL
VAR_INPUT
	dataAddress : PVOID;
	dataSize : UDINT := 1;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IsDynamicData := Tc2_System.F_CheckMemoryArea(dataAddress, dataSize) = E_TcMemoryArea.Dynamic;]]></ST>
      </Implementation>
    </Method>
    <Method Name="RegisterAutoDisposedObject" Id="{74218506-44e1-4d86-8e89-41e66c133362}" FolderPath="Service memebers\">
      <Declaration><![CDATA[METHOD INTERNAL RegisterAutoDisposedObject
VAR_IN_OUT CONSTANT
	autoDisposedObject : AutoDisposedObject;
END_VAR
VAR
	newAutoDisposedObjects : POINTER TO IObject;	
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[cooperativeCleanupMemory();

IF NOT __ISVALIDREF(autoDisposedObject) OR_ELSE NOT IsDynamicData(autoDisposedObject.Address) THEN
	RETURN;
END_IF

IF _AutoDisposedObjectCount + 1 > _AutoDisposedPointerCount THEN
	_AutoDisposedPointerCount := _AutoDisposedPointerCount + ALLOCATE_POINTER_COUNT;
	
	newAutoDisposedObjects := __NEW(POINTER TO IObject, _AutoDisposedPointerCount);
	
	IF _AutoDisposedObjectCount > 0 THEN
		Tc2_System.MEMCPY(newAutoDisposedObjects, _AutoDisposedObjects, TO_UDINT(XSIZEOF(POINTER TO IObject) * _AutoDisposedObjectCount));
		
		__DELETE(_AutoDisposedObjects);
	END_IF
	
	_AutoDisposedObjects := newAutoDisposedObjects;
END_IF

_AutoDisposedObjects[_AutoDisposedObjectCount] := autoDisposedObject;
_AutoDisposedObjectCount := _AutoDisposedObjectCount + 1;	]]></ST>
      </Implementation>
    </Method>
    <Method Name="TryReleaseDynamicMemory" Id="{5fe5bc2a-b608-418e-9ff0-e9c1a91c92f5}">
      <Declaration><![CDATA[METHOD TryReleaseDynamicMemory : BOOL
VAR_IN_OUT
	address : POINTER TO BYTE;	
END_VAR
VAR_INPUT
	isObject : BOOL;
END_VAR
VAR
	objectPointer : POINTER TO Object;
	objectClassName : STRING;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF address = 0 OR_ELSE NOT IsDynamicData(address, 1) THEN
	RETURN;
END_IF

IF isObject THEN
	objectPointer := address;
	objectClassName := objectPointer^.ClassName;
	
	__DELETE(objectPointer);
	address := 0;
	
	LogManager.TryLogMessage(WideStringHelper.ConcatStrings255("Object of class '", TO_WSTRING(objectClassName), "' disposed at ", TO_WSTRING(SystemDateTimeManager.SystemLocalDateTime)), LOG_CATEGORY.SUCCESS, TO_WSTRING(ClassName), "ReleaseDynamicMemory", "ObjectDispose");
ELSE
	__DELETE(address);
END_IF

TryReleaseDynamicMemory := TRUE;]]></ST>
      </Implementation>
    </Method>
    <Method Name="TryReleaseDynamicObject" Id="{1a29f8e6-f929-40c9-bee2-ed6219c50a45}">
      <Declaration><![CDATA[METHOD TryReleaseDynamicObject : BOOL
VAR_IN_OUT
	instance : IObject;	
END_VAR
VAR
	objectPointer : POINTER TO Object;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF instance = 0 THEN
	RETURN;
END_IF
	
objectPointer := instance.Address;

IF NOT IsDynamicData(objectPointer, 1) THEN
	RETURN;
END_IF

instance := 0;	

TryReleaseDynamicObject := TryReleaseDynamicMemory(objectPointer, TRUE);]]></ST>
      </Implementation>
    </Method>
  </POU>
</TcPlcObject>