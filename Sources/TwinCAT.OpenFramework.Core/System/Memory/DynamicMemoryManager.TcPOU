<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1">
  <POU Name="DynamicMemoryManager" Id="{dfd03ea7-11b7-42ce-b52e-f5a7df7a7ad3}" SpecialFunc="None">
    <Declaration><![CDATA[(*

.. <legal notes>

legal notes
=================
| SPDX-FileCopyrightText: © 2025 Oleksandr Tiutyk <trofimich@gmail.com>
| SPDX-License-Identifier: LGPL-3.0-only
| For details check: LGPL-3.0-only_

.. _LGPL-3.0-only: https://www.gnu.org/licenses/lgpl-3.0.en.html

.. </legal notes>

*)

{attribute 'no_explicit_call' := 'Direct call is not allowed'} 
PROGRAM DynamicMemoryManager
VAR
	_ClassName : STRING := __POUNAME();
	
	_AutoDisposedObjects : POINTER TO IObject;	
	_AutoDisposedPointerCount : DWORD;
	_AutoDisposedObjectCount : DWORD;

	_LastSystemTaskCycleCount : UDINT;

	_MemoryCleanupHelper : MemoryCleanupHelper;	
END_VAR
VAR CONSTANT
	ALLOCATE_POINTER_COUNT : BYTE := 10;
END_VAR]]></Declaration>
    <Implementation>
      <ST><![CDATA[]]></ST>
    </Implementation>
    <Folder Name="Service memebers" Id="{ead780a2-bf4c-47dc-a6d8-bac15c5bc6e0}" />
    <Property Name="ClassName" Id="{c33db740-6bfc-43f8-9d4d-602279175d35}">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
PROPERTY ClassName : STRING]]></Declaration>
      <Get Name="Get" Id="{fc745387-f67a-4d86-bdee-e470addbc3c6}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[ClassName := _ClassName;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Method Name="CleanupMemory" Id="{a080fe6a-f539-4ac0-9048-10f8d3587c3b}">
      <Declaration><![CDATA[METHOD CleanupMemory
VAR_INPUT
	releaseBuffer : BOOL := FALSE;
END_VAR
VAR
	i : ULINT;
	objectToDispose : POINTER TO Object;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF _AutoDisposedObjectCount > 0 THEN
	FOR i := 0 TO _AutoDisposedObjectCount - 1 DO
		MemoryHelper.TryReleaseDynamicObject(_AutoDisposedObjects[i]);
	END_FOR
			
	_AutoDisposedObjectCount := 0;
END_IF

IF _AutoDisposedPointerCount > 0 AND_THEN releaseBuffer THEN
	__DELETE(_AutoDisposedObjects);
	
	_AutoDisposedPointerCount := 0;
END_IF]]></ST>
      </Implementation>
    </Method>
    <Method Name="cooperativeCleanupMemory" Id="{5754b7a5-6f2f-49c9-a633-b4906595d6ba}" FolderPath="Service memebers\">
      <Declaration><![CDATA[METHOD PRIVATE cooperativeCleanupMemory
VAR
	systemTaskInfo : Global.PlcTaskSystemInfo;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[systemTaskInfo := Tc2_System.F_GetTaskInfo();

IF systemTaskInfo.CycleCount <> _LastSystemTaskCycleCount THEN
	_LastSystemTaskCycleCount := systemTaskInfo.CycleCount;
	
	CleanupMemory(FALSE);
END_IF]]></ST>
      </Implementation>
    </Method>
    <Property Name="NamespaceName" Id="{958706a2-1f53-424d-8ad1-0af68e5c53ec}">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
PROPERTY NamespaceName : STRING]]></Declaration>
      <Get Name="Get" Id="{4f3b5fdf-4d9e-4c51-9c20-ce86f99117d4}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[NamespaceName := CurrentNamespace.Name;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Method Name="RegisterAutoDisposedObject" Id="{74218506-44e1-4d86-8e89-41e66c133362}" FolderPath="Service memebers\">
      <Declaration><![CDATA[METHOD INTERNAL RegisterAutoDisposedObject
VAR_INPUT
	autoDisposedObject : IObject;
END_VAR
VAR
	newAutoDisposedObjects : POINTER TO IObject;
	i : LINT;	
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[cooperativeCleanupMemory();

// skip registration id object did not created in the dynamic memory area
IF autoDisposedObject = 0 OR_ELSE NOT MemoryHelper.IsDynamicData(autoDisposedObject.Address) THEN
	RETURN;
END_IF

// prevents multiple registrations of the same object
IF _AutoDisposedObjectCount > 0 THEN
	FOR i := 0 TO TO_LINT(_AutoDisposedObjectCount) - 1 DO
		IF _AutoDisposedObjects[i] = autoDisposedObject THEN
			RETURN;
		END_IF
	END_FOR	
END_IF

IF _AutoDisposedObjectCount + 1 > _AutoDisposedPointerCount THEN
	_AutoDisposedPointerCount := _AutoDisposedPointerCount + ALLOCATE_POINTER_COUNT;
	
	newAutoDisposedObjects := __NEW(POINTER TO IObject, _AutoDisposedPointerCount);
	
	IF _AutoDisposedObjectCount > 0 THEN
		Tc2_System.MEMCPY(newAutoDisposedObjects, _AutoDisposedObjects, TO_UDINT(XSIZEOF(POINTER TO IObject) * _AutoDisposedObjectCount));
		
		__DELETE(_AutoDisposedObjects);
	END_IF
	
	_AutoDisposedObjects := newAutoDisposedObjects;
END_IF

_AutoDisposedObjects[_AutoDisposedObjectCount] := autoDisposedObject;
_AutoDisposedObjectCount := _AutoDisposedObjectCount + 1;	]]></ST>
      </Implementation>
    </Method>
  </POU>
</TcPlcObject>