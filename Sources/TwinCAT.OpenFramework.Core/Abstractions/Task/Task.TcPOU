<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1">
  <POU Name="Task" Id="{3fcb82bf-fcc2-4c60-ab58-32776172ce5b}" SpecialFunc="None">
    <Declaration><![CDATA[(*

.. <legal notes>

legal notes
=================
| SPDX-FileCopyrightText: © 2025 Oleksandr Tiutyk <trofimich@gmail.com>
| SPDX-License-Identifier: LGPL-3.0-only
| For details check: LGPL-3.0-only_

.. _LGPL-3.0-only: https://www.gnu.org/licenses/lgpl-3.0.en.html

.. </legal notes>

*)

{attribute 'no_explicit_call' := 'do not call this function block directly'} 
FUNCTION_BLOCK ABSTRACT Task EXTENDS Object IMPLEMENTS ITask
VAR
	_Name : WSTRING;
	
	_StateChanged : BOOL;

	_Busy : BOOL;
	
	_Done : BOOL;

	_Cancelled : BOOL;
	
	_Aborted : BOOL;
	
	_Error : BOOL;	
	_ErrorMessage : ERROR_MESSAGE;	
END_VAR]]></Declaration>
    <Implementation>
      <ST><![CDATA[]]></ST>
    </Implementation>
    <Folder Name="Members to override" Id="{76112447-5878-4246-92fb-0910ffa3ef2d}" />
    <Folder Name="Service members" Id="{6b1a5589-8d7c-4542-877b-5fcd2d9b2fbd}" />
    <Property Name="Aborted" Id="{205fba08-26d2-4312-a96e-e17dee6c923f}">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
{attribute 'OPC.UA.DA.Property' := '1'}
{attribute 'OPC.UA.DA.Access' := '1'}
PROPERTY FINAL Aborted : BOOL
]]></Declaration>
      <Get Name="Get" Id="{9f8851fa-4d24-41c2-b7cf-fd9ecc9bdb84}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[Aborted := _Aborted;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Property Name="Busy" Id="{e3f55a84-6dc8-42a8-870b-cbc03d5ce896}">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
{attribute 'OPC.UA.DA.Property' := '1'}
{attribute 'OPC.UA.DA.Access' := '1'}
PROPERTY FINAL Busy : BOOL
]]></Declaration>
      <Get Name="Get" Id="{9b0f9833-3b49-48b8-9041-0090b996669f}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[Busy := _Busy;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Method Name="Cancel" Id="{85ebde85-30ac-4488-ad53-ac1ecdb74776}">
      <Declaration><![CDATA[METHOD FINAL Cancel
VAR
	exceptionCode : __SYSTEM.ExceptionCode;
	
	exception : IException;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF Busy THEN
	RETURN;
END_IF

__TRY
	OnCancel();
	
	setCancelledState();
__CATCH(exceptionCode)
	handleFault(exceptionCode);
__ENDTRY]]></ST>
      </Implementation>
    </Method>
    <Property Name="Cancelled" Id="{72ad9134-a921-4460-953e-dd085c603cb7}">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
{attribute 'OPC.UA.DA.Property' := '1'}
{attribute 'OPC.UA.DA.Access' := '1'}
PROPERTY FINAL Cancelled : BOOL
]]></Declaration>
      <Get Name="Get" Id="{6008e232-f65d-448f-8b7c-75b411e44c3a}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[Cancelled := _Cancelled;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Property Name="Done" Id="{10c3e2b7-99f3-4fa9-bdac-62a94dfa4509}">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
{attribute 'OPC.UA.DA.Property' := '1'}
{attribute 'OPC.UA.DA.Access' := '1'}
PROPERTY FINAL Done : BOOL
]]></Declaration>
      <Get Name="Get" Id="{bbb5f7ba-4447-4958-8ec9-7e3859b0437b}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[Done := _Done OR_ELSE _Aborted OR_ELSE _Error;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Property Name="Error" Id="{3d54590f-9d24-46b4-9724-32260ce4207a}">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
{attribute 'OPC.UA.DA.Property' := '1'}
{attribute 'OPC.UA.DA.Access' := '1'}
PROPERTY FINAL Error : BOOL
]]></Declaration>
      <Get Name="Get" Id="{998b8321-1744-4a0a-b9cf-78a20c7dc6e5}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[Error := _Error;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Property Name="ErrorMessage" Id="{e92b95ec-4a2f-46a9-85eb-1f48df75a257}">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
{attribute 'OPC.UA.DA.Property' := '1'}
{attribute 'OPC.UA.DA.Access' := '1'}
PROPERTY FINAL ErrorMessage : REFERENCE TO ERROR_MESSAGE
]]></Declaration>
      <Get Name="Get" Id="{82e42fe0-1017-4cc0-a630-23020b73322e}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[ErrorMessage REF= _ErrorMessage;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Method Name="Execute" Id="{356dfaf1-5836-4634-88c3-9adf266fe2b8}">
      <Declaration><![CDATA[METHOD FINAL Execute 
VAR
	runningState : TASK_RUNNING_STATE;
	
	exceptionCode : __SYSTEM.ExceptionCode;
	
	exception : IException;	
	
	errorMessage : ERROR_MESSAGE;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[_StateChanged := FALSE;

__TRY
	IF Busy THEN
		OnRunning(FALSE, state => runningState);
		
		CASE runningState OF
			TASK_RUNNING_STATE.DONE:
				setDoneState();
			
			TASK_RUNNING_STATE.ABORTED:
				setAbortedState();
		END_CASE
	END_IF
__CATCH(exceptionCode)
	handleFault(exceptionCode);
__ENDTRY]]></ST>
      </Implementation>
    </Method>
    <Method Name="handleFault" Id="{87f22517-b446-496a-a773-92d534ab21f6}" FolderPath="Service members\">
      <Declaration><![CDATA[METHOD PRIVATE handleFault
VAR_INPUT
	exceptionCode : __SYSTEM.ExceptionCode;
END_VAR
VAR
	exception : IException;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[exception := ExceptionManager.GetLastException(exceptionCode);

__TRY
	OnFault();
__CATCH
__ENDTRY
	
IF NOT _Error THEN
	LogManager.TryLogMessage(WideStringHelper.ConcatStrings255("Task '", Name, "' failed at ", TO_WSTRING(SystemDateTimeManager.SystemLocalDateTime)), LOG_CATEGORY.SUCCESS, TO_WSTRING(ClassName), "Task", "TaskError");
	
	_StateChanged := TRUE;
END_IF

_Busy := FALSE;

_Done := FALSE;

_Cancelled := FALSE;

_Aborted := FALSE;

_Error := TRUE;		
_ErrorMessage := exception.GetMessage();

ExceptionManager.Throw(exception);]]></ST>
      </Implementation>
    </Method>
    <Property Name="Name" Id="{82c59c7f-12d8-4fab-8d33-ec1ebb7c64fc}">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
{attribute 'OPC.UA.DA.Property' := '1'}
{attribute 'OPC.UA.DA.Access' := '1'}
PROPERTY FINAL Name : WSTRING]]></Declaration>
      <Get Name="Get" Id="{61eb0fbe-e691-4c63-b864-dbfd0811101b}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[Name := SEL(_Name = "", _Name, SUPER^.Name);]]></ST>
        </Implementation>
      </Get>
      <Set Name="Set" Id="{8648b17d-3897-46ca-b930-e499fe043a7b}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[_Name := Name;]]></ST>
        </Implementation>
      </Set>
    </Property>
    <Method Name="OnCancel" Id="{e62e6e28-d2f5-4a49-a4f3-1ac1c3a6c40e}" FolderPath="Members to override\">
      <Declaration><![CDATA[METHOD PROTECTED ABSTRACT OnCancel
]]></Declaration>
      <Implementation>
        <ST><![CDATA[]]></ST>
      </Implementation>
    </Method>
    <Method Name="OnFault" Id="{c12cd4b4-6b5e-40a4-9547-4538d1fad656}" FolderPath="Members to override\">
      <Declaration><![CDATA[METHOD PROTECTED OnFault ]]></Declaration>
      <Implementation>
        <ST><![CDATA[]]></ST>
      </Implementation>
    </Method>
    <Method Name="OnRunning" Id="{a15599f5-e9ad-4bc1-b827-e975fc966d86}" FolderPath="Members to override\">
      <Declaration><![CDATA[METHOD PROTECTED ABSTRACT OnRunning 
VAR_INPUT
	startRequest : BOOL;
END_VAR
VAR_OUTPUT
	state : TASK_RUNNING_STATE;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[]]></ST>
      </Implementation>
    </Method>
    <Method Name="setAbortedState" Id="{fac00809-31d6-4f90-8e02-b442214ce266}" FolderPath="Service members\">
      <Declaration><![CDATA[METHOD PRIVATE FINAL setAbortedState]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF NOT _Aborted THEN
	LogManager.TryLogMessage(WideStringHelper.ConcatStrings255("Task '", Name, "' aborted at ", TO_WSTRING(SystemDateTimeManager.SystemLocalDateTime)), LOG_CATEGORY.SUCCESS, TO_WSTRING(ClassName), "Task", "TaskAborted");

	_StateChanged := TRUE;
END_IF

_Busy := FALSE;

_Done := FALSE;

_Cancelled := FALSE;

_Aborted := TRUE;

_Error := FALSE;	
_ErrorMessage := "";	]]></ST>
      </Implementation>
    </Method>
    <Method Name="setBusyState" Id="{8ba032d1-b467-4f29-94d8-e5d440995321}" FolderPath="Service members\">
      <Declaration><![CDATA[METHOD PRIVATE FINAL setBusyState
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF NOT _Busy THEN
	LogManager.TryLogMessage(WideStringHelper.ConcatStrings255("Task '", Name, "' started at ", TO_WSTRING(SystemDateTimeManager.SystemLocalDateTime)), LOG_CATEGORY.SUCCESS, TO_WSTRING(ClassName), "Task", "TaskBusy");
	
	_StateChanged := TRUE;
END_IF

_Busy := TRUE;

_Done := FALSE;

_Cancelled := FALSE;

_Aborted := FALSE;

_Error := FALSE;	
_ErrorMessage := "";	]]></ST>
      </Implementation>
    </Method>
    <Method Name="setCancelledState" Id="{9eb02e57-605e-46fb-afde-513fb736d90a}" FolderPath="Service members\">
      <Declaration><![CDATA[METHOD PRIVATE FINAL setCancelledState]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF NOT _Aborted THEN
	LogManager.TryLogMessage(WideStringHelper.ConcatStrings255("Task '", Name, "' cancelled at ", TO_WSTRING(SystemDateTimeManager.SystemLocalDateTime)), LOG_CATEGORY.SUCCESS, TO_WSTRING(ClassName), "Task", "TaskCancelled");

	_StateChanged := TRUE;
END_IF

_Busy := FALSE;

_Done := FALSE;

_Cancelled := TRUE;

_Aborted := FALSE;

_Error := FALSE;	
_ErrorMessage := "";	]]></ST>
      </Implementation>
    </Method>
    <Method Name="setDoneState" Id="{5a91bd28-905f-4ff0-ac15-4afd240589a6}" FolderPath="Service members\">
      <Declaration><![CDATA[METHOD PRIVATE FINAL setDoneState]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF NOT _Done THEN
	LogManager.TryLogMessage(WideStringHelper.ConcatStrings255("Task '", Name, "' done at ", TO_WSTRING(SystemDateTimeManager.SystemLocalDateTime)), LOG_CATEGORY.SUCCESS, TO_WSTRING(ClassName), "Task", "TaskDone");
	
	_StateChanged := TRUE;
END_IF

_Busy := FALSE;

_Done := TRUE;

_Cancelled := FALSE;

_Aborted := FALSE;

_Error := FALSE;	
_ErrorMessage := "";	]]></ST>
      </Implementation>
    </Method>
    <Method Name="Start" Id="{5e4f958e-3441-449c-a3ee-2bfa2bc2b0b1}">
      <Declaration><![CDATA[METHOD FINAL Start : BOOL
VAR
	runningState : TASK_RUNNING_STATE;

	exceptionCode : __SYSTEM.ExceptionCode;
	
	exception : IException;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF Busy THEN
	RETURN;
END_IF

__TRY
	setBusyState();
	
	OnRunning(TRUE, state => runningState);

	CASE runningState OF
		TASK_RUNNING_STATE.DONE:
			setDoneState();
		
		TASK_RUNNING_STATE.ABORTED:
			setAbortedState();	
	END_CASE
	
	Start := TRUE;
__CATCH(exceptionCode)
	handleFault(exceptionCode);
__ENDTRY]]></ST>
      </Implementation>
    </Method>
    <Property Name="StateChanged" Id="{5df375fb-8678-4172-86a8-056cc8dfa2cd}">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
{attribute 'OPC.UA.DA.Property' := '1'}
{attribute 'OPC.UA.DA.Access' := '1'}
PROPERTY FINAL StateChanged : BOOL]]></Declaration>
      <Get Name="Get" Id="{68fadd26-a99e-461e-8aa1-2b482d0dd654}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[StateChanged := _StateChanged;]]></ST>
        </Implementation>
      </Get>
    </Property>
  </POU>
</TcPlcObject>