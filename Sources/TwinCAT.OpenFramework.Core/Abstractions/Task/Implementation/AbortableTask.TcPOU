<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1">
  <POU Name="AbortableTask" Id="{3fcb82bf-fcc2-4c60-ab58-32776172ce5b}" SpecialFunc="None">
    <Declaration><![CDATA[{attribute 'no_explicit_call' := 'do not call this function block directly'} 
FUNCTION_BLOCK ABSTRACT AbortableTask EXTENDS Object IMPLEMENTS IAbortableTask
VAR
	_StateChanged : BOOL;

	_Running : BOOL;

	_Done : BOOL;
	
	_Aborting : BOOL;	
	_Aborted : BOOL;
	_AbortReason : ERROR_MESSAGE;
	
	_Error : BOOL;	
	_ErrorMessage : ERROR_MESSAGE;	
END_VAR]]></Declaration>
    <Implementation>
      <ST><![CDATA[]]></ST>
    </Implementation>
    <Folder Name="Members to override" Id="{76112447-5878-4246-92fb-0910ffa3ef2d}" />
    <Folder Name="Service members" Id="{6b1a5589-8d7c-4542-877b-5fcd2d9b2fbd}" />
    <Property Name="Aborted" Id="{205fba08-26d2-4312-a96e-e17dee6c923f}">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
{attribute 'OPC.UA.DA.Property' := '1'}
{attribute 'OPC.UA.DA.Access' := '1'}
PROPERTY FINAL Aborted : BOOL
]]></Declaration>
      <Get Name="Get" Id="{9f8851fa-4d24-41c2-b7cf-fd9ecc9bdb84}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[Aborted := _Aborted;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Property Name="Aborting" Id="{c0530bff-30c3-4c28-990a-6e7a951c38e9}">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
{attribute 'OPC.UA.DA.Property' := '1'}
{attribute 'OPC.UA.DA.Access' := '1'}
PROPERTY FINAL Aborting : BOOL
]]></Declaration>
      <Get Name="Get" Id="{c76d8074-3a6c-4ceb-839f-e729688d48e1}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[Aborting := _Aborting;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Property Name="AbortReason" Id="{62b5d978-3b39-4864-a514-bb07423cf0e2}">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
{attribute 'OPC.UA.DA.Property' := '1'}
{attribute 'OPC.UA.DA.Access' := '1'}
PROPERTY FINAL AbortReason : REFERENCE TO ERROR_MESSAGE]]></Declaration>
      <Get Name="Get" Id="{e89b4f7e-5119-407b-92c1-18a61068d8bb}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[AbortReason REF= _AbortReason;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Property Name="Busy" Id="{e3f55a84-6dc8-42a8-870b-cbc03d5ce896}">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
{attribute 'OPC.UA.DA.Property' := '1'}
{attribute 'OPC.UA.DA.Access' := '1'}
PROPERTY FINAL Busy : BOOL
]]></Declaration>
      <Get Name="Get" Id="{9b0f9833-3b49-48b8-9041-0090b996669f}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[Busy := _Running OR_ELSE _Aborting;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Property Name="Done" Id="{b95d743b-c72e-4bcb-a404-c7e3e4a6a37e}">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
{attribute 'OPC.UA.DA.Property' := '1'}
{attribute 'OPC.UA.DA.Access' := '1'}
PROPERTY FINAL Done : BOOL
]]></Declaration>
      <Get Name="Get" Id="{bfaa887d-c314-4518-a171-41d0308d56ff}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[Done := _Done;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Property Name="Error" Id="{3d54590f-9d24-46b4-9724-32260ce4207a}">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
{attribute 'OPC.UA.DA.Property' := '1'}
{attribute 'OPC.UA.DA.Access' := '1'}
PROPERTY FINAL Error : BOOL
]]></Declaration>
      <Get Name="Get" Id="{998b8321-1744-4a0a-b9cf-78a20c7dc6e5}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[Error := _Error;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Property Name="ErrorMessage" Id="{e92b95ec-4a2f-46a9-85eb-1f48df75a257}">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
{attribute 'OPC.UA.DA.Property' := '1'}
{attribute 'OPC.UA.DA.Access' := '1'}
PROPERTY FINAL ErrorMessage : REFERENCE TO ERROR_MESSAGE
]]></Declaration>
      <Get Name="Get" Id="{82e42fe0-1017-4cc0-a630-23020b73322e}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[ErrorMessage REF= _ErrorMessage;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Method Name="Execute" Id="{356dfaf1-5836-4634-88c3-9adf266fe2b8}">
      <Declaration><![CDATA[METHOD FINAL Execute 
VAR
	done : BOOL;
	
	exceptionCode : __SYSTEM.ExceptionCode;
	
	exception : IException;	
	
	errorMessage : ERROR_MESSAGE;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[_StateChanged := FALSE;

__TRY
	IF Busy THEN
		IF _Running THEN
			OnRunning(FALSE, done => done);
			
			IF done THEN
				setDoneState();
			END_IF		
		ELSE
			OnAborting(FALSE, done => done);
			
			IF done THEN
				setAbortedState();
			END_IF		
		END_IF
	END_IF
__CATCH(exceptionCode)
	handleException(exceptionCode);
__ENDTRY]]></ST>
      </Implementation>
    </Method>
    <Method Name="handleException" Id="{87f22517-b446-496a-a773-92d534ab21f6}" FolderPath="Service members\">
      <Declaration><![CDATA[METHOD PRIVATE handleException
VAR_INPUT
	exceptionCode : __SYSTEM.ExceptionCode;
END_VAR
VAR
	exception : IException;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[exception := ExceptionManager.GetLastException(exceptionCode);

__TRY
	OnError();
__CATCH
__ENDTRY

_ErrorMessage := exception.GetMessage();
	
IF NOT _Error THEN
	LogManager.TryLogMessage(WideStringHelper.ConcatStrings255("Task '", Name, "' failed at ", TO_WSTRING(SystemDateTimeManager.SystemLocalDateTime)), LOG_CATEGORY.SUCCESS, TO_WSTRING(ClassName), "Task", "TaskFailed");
	
	_StateChanged := TRUE;
END_IF

_Running := FALSE;

_Done := FALSE;

_Aborting := FALSE;	
_Aborted := FALSE;
_AbortReason := "";

_Error := TRUE;		

ExceptionManager.Throw(exception);]]></ST>
      </Implementation>
    </Method>
    <Property Name="Name" Id="{6fc84b72-bae7-48c4-9b6d-21dd2d8602d7}">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
{attribute 'OPC.UA.DA.Property' := '1'}
{attribute 'OPC.UA.DA.Access' := '1'}
PROPERTY ABSTRACT Name : WSTRING
]]></Declaration>
      <Get Name="Get" Id="{159969ce-3ca4-45c0-a82e-3687a3ad2fff}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Method Name="OnAborting" Id="{bd8eb179-c550-42d5-a679-2b68c1503c0e}" FolderPath="Members to override\">
      <Declaration><![CDATA[METHOD PROTECTED ABSTRACT OnAborting 
VAR_INPUT
	start : BOOL;
END_VAR
VAR_OUTPUT
	done : BOOL;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[]]></ST>
      </Implementation>
    </Method>
    <Method Name="OnError" Id="{c12cd4b4-6b5e-40a4-9547-4538d1fad656}" FolderPath="Members to override\">
      <Declaration><![CDATA[METHOD PROTECTED OnError ]]></Declaration>
      <Implementation>
        <ST><![CDATA[]]></ST>
      </Implementation>
    </Method>
    <Method Name="OnRunning" Id="{a15599f5-e9ad-4bc1-b827-e975fc966d86}" FolderPath="Members to override\">
      <Declaration><![CDATA[METHOD PROTECTED ABSTRACT OnRunning 
VAR_INPUT
	start : BOOL;
END_VAR
VAR_OUTPUT
	done : BOOL;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[]]></ST>
      </Implementation>
    </Method>
    <Property Name="Running" Id="{dda6cb05-0e58-4715-8fa9-9ed4428ca958}">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
{attribute 'OPC.UA.DA.Property' := '1'}
{attribute 'OPC.UA.DA.Access' := '1'}
PROPERTY FINAL Running : BOOL
]]></Declaration>
      <Get Name="Get" Id="{ac4bced3-fa18-48d8-a5d4-4beba0226ec1}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[Running := _Running;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Method Name="setAbortedState" Id="{fac00809-31d6-4f90-8e02-b442214ce266}" FolderPath="Service members\">
      <Declaration><![CDATA[METHOD PRIVATE FINAL setAbortedState]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF NOT _Aborted THEN
	LogManager.TryLogMessage(WideStringHelper.ConcatStrings255("Task '", Name, "' aborted at ", TO_WSTRING(SystemDateTimeManager.SystemLocalDateTime)), LOG_CATEGORY.SUCCESS, TO_WSTRING(ClassName), "Task", "TaskAborted");

	_StateChanged := TRUE;
END_IF

_Running := FALSE;

_Done := FALSE;

_Aborting := FALSE;	
_Aborted := TRUE;

_Error := FALSE;	
_ErrorMessage := "";	]]></ST>
      </Implementation>
    </Method>
    <Method Name="setAbortingState" Id="{dc660dfe-c65d-4d91-bb97-ccc531a7b715}" FolderPath="Service members\">
      <Declaration><![CDATA[METHOD PRIVATE FINAL setAbortingState
VAR_IN_OUT CONSTANT 
	reason : ERROR_MESSAGE;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF NOT _Aborting THEN
	LogManager.TryLogMessage(WideStringHelper.ConcatStrings255("Task '", Name, "' abort started at ", TO_WSTRING(SystemDateTimeManager.SystemLocalDateTime)), LOG_CATEGORY.SUCCESS, TO_WSTRING(ClassName), "Task", "TaskAbortStarted");

	_StateChanged := TRUE;
END_IF

_Running := FALSE;

_Done := FALSE;

_Aborting := TRUE;	
_Aborted := FALSE;
_AbortReason := reason;

_Error := FALSE;	
_ErrorMessage := "";	]]></ST>
      </Implementation>
    </Method>
    <Method Name="setDoneState" Id="{5a91bd28-905f-4ff0-ac15-4afd240589a6}" FolderPath="Service members\">
      <Declaration><![CDATA[METHOD PRIVATE FINAL setDoneState]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF NOT _Done THEN
	LogManager.TryLogMessage(WideStringHelper.ConcatStrings255("Task '", Name, "' done at ", TO_WSTRING(SystemDateTimeManager.SystemLocalDateTime)), LOG_CATEGORY.SUCCESS, TO_WSTRING(ClassName), "Task", "TaskDone");
	
	_StateChanged := TRUE;
END_IF

_Running := FALSE;

_Done := TRUE;

_Aborting := FALSE;	
_Aborted := FALSE;
_AbortReason := "";

_Error := FALSE;	
_ErrorMessage := "";	]]></ST>
      </Implementation>
    </Method>
    <Method Name="setRunningState" Id="{8ba032d1-b467-4f29-94d8-e5d440995321}" FolderPath="Service members\">
      <Declaration><![CDATA[METHOD PRIVATE FINAL setRunningState
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF NOT _Running THEN
	LogManager.TryLogMessage(WideStringHelper.ConcatStrings255("Task '", Name, "' started at ", TO_WSTRING(SystemDateTimeManager.SystemLocalDateTime)), LOG_CATEGORY.SUCCESS, TO_WSTRING(ClassName), "Task", "TaskStarted");
	
	_StateChanged := TRUE;
END_IF

_Running := TRUE;

_Done := FALSE;

_Aborting := FALSE;	
_Aborted := FALSE;
_AbortReason := "";

_Error := FALSE;	
_ErrorMessage := "";	]]></ST>
      </Implementation>
    </Method>
    <Method Name="Start" Id="{5e4f958e-3441-449c-a3ee-2bfa2bc2b0b1}">
      <Declaration><![CDATA[METHOD FINAL Start
VAR
	done : BOOL;

	exceptionCode : __SYSTEM.ExceptionCode;
	
	exception : IException;

	errorMessage : ERROR_MESSAGE;	
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[__TRY
	setRunningState();
	
	OnRunning(TRUE, done => done);

	IF done THEN
		setDoneState();
	END_IF
__CATCH(exceptionCode)
	handleException(exceptionCode);
__ENDTRY]]></ST>
      </Implementation>
    </Method>
    <Method Name="StartAbort" Id="{f4d5eddb-0774-4410-8518-af02c1510410}">
      <Declaration><![CDATA[METHOD FINAL StartAbort
VAR_INPUT 
	reason : ERROR_MESSAGE := "";
END_VAR
VAR
	done : BOOL;

	exceptionCode : __SYSTEM.ExceptionCode;
	
	exception : IException;

	errorMessage : ERROR_MESSAGE;	
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF NOT Running THEN
	RETURN;
END_IF

__TRY
	setAbortingState(reason);
	
	OnAborting(TRUE, done => done);

	IF done THEN
		setAbortedState();
	END_IF
__CATCH(exceptionCode)
	handleException(exceptionCode);
__ENDTRY]]></ST>
      </Implementation>
    </Method>
    <Property Name="StateChanged" Id="{5df375fb-8678-4172-86a8-056cc8dfa2cd}">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
{attribute 'OPC.UA.DA.Property' := '1'}
{attribute 'OPC.UA.DA.Access' := '1'}
PROPERTY FINAL StateChanged : BOOL]]></Declaration>
      <Get Name="Get" Id="{68fadd26-a99e-461e-8aa1-2b482d0dd654}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[StateChanged := _StateChanged;]]></ST>
        </Implementation>
      </Get>
    </Property>
  </POU>
</TcPlcObject>