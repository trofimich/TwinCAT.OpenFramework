<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1">
  <POU Name="LogManager" Id="{5168e535-223e-4560-9821-bca21a9b8561}" SpecialFunc="None">
    <Declaration><![CDATA[(*

.. <legal notes>

legal notes
=================
| SPDX-FileCopyrightText: © 2026 Oleksandr Tiutyk <trofimich@gmail.com>
| SPDX-License-Identifier: LGPL-3.0-only
| For details check: LGPL-3.0-only_

.. _LGPL-3.0-only: https://www.gnu.org/licenses/lgpl-3.0.en.html

.. </legal notes>

*)

PROGRAM LogManager
VAR_INPUT
	AddNumberToMessage : BOOL := TRUE;
END_VAR
VAR
	_DefaultLogger : AdsLogger;
	_Logger : ILogger;	
	_Filter : ILogFilter;  
END_VAR]]></Declaration>
    <Implementation>
      <ST><![CDATA[]]></ST>
    </Implementation>
    <Method Name="Execute" Id="{13c3c36e-9fa4-45b8-9a63-156fe14d049b}">
      <Declaration><![CDATA[METHOD Execute
VAR
	exceptionCode : __SYSTEM.ExceptionCode;
	exception : IException;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[__TRY
	Logger.Execute();
__CATCH(exceptionCode)
	exception := ExceptionManager.GetLastException(exceptionCode);
	
	_DefaultLogger.TryLogException(exception);
__ENDTRY]]></ST>
      </Implementation>
    </Method>
    <Property Name="Filter" Id="{b6f1f4aa-92b0-409e-ad33-af6c50416e83}">
      <Declaration><![CDATA[PROPERTY Filter : ILogFilter]]></Declaration>
      <Get Name="Get" Id="{31e6a597-f4cb-45a7-bcf1-c6f162ef07f2}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[Filter := _Filter;]]></ST>
        </Implementation>
      </Get>
      <Set Name="Set" Id="{3a6b37ff-c92d-4d4c-a4cf-30b8093e5043}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[_Filter := Filter;]]></ST>
        </Implementation>
      </Set>
    </Property>
    <Property Name="Logger" Id="{8f399577-3e03-44ae-a287-168cdae9d0ec}">
      <Declaration><![CDATA[PROPERTY Logger : ILogger]]></Declaration>
      <Get Name="Get" Id="{c9c1cefb-1091-483c-b6a3-ca70a961903f}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[IF _Logger = 0 THEN
	Logger := _DefaultLogger;
ELSE
	Logger := _Logger;
END_IF]]></ST>
        </Implementation>
      </Get>
      <Set Name="Set" Id="{82c82d05-b526-431e-b338-c69fda2bdd1a}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[_Logger := Logger;]]></ST>
        </Implementation>
      </Set>
    </Property>
    <Property Name="NamespaceName" Id="{84be6e8d-e2b5-4bd6-ae4f-31889ab0432e}">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
PROPERTY NamespaceName : STRING]]></Declaration>
      <Get Name="Get" Id="{4891555c-102c-47e3-8f69-9a215e109b78}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[NamespaceName := CurrentNamespace.Name;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Method Name="TryLogException" Id="{bd7d75b2-118d-460f-87a2-4f124e9232ba}">
      <Declaration><![CDATA[METHOD TryLogException : BOOL
VAR_INPUT
	exception : IException;
	topic1 : WSTRING := "";	
	topic2 : WSTRING := "";	
	topic3 : WSTRING := "";	
	topic4 : WSTRING := "";	
END_VAR
VAR
	category : LOG_CATEGORY;
	topics : ARRAY [0..4] OF WSTRING; 
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF exception = 0 THEN
	RETURN;
END_IF

category := LOG_CATEGORY.ERROR; 

topics[0] := TO_WSTRING(exception.ClassName);
topics[1] := topic1;
topics[2] := topic2;
topics[3] := topic3;
topics[4] := topic4;

IF Filter <> 0 AND_THEN NOT Filter.ShouldLog(category, topics) THEN
	RETURN;
END_IF

TryLogException := Logger.TryLogException(exception);]]></ST>
      </Implementation>
    </Method>
    <Method Name="TryLogMessage" Id="{1b41c149-f4d6-4a68-875a-675fa3954ac9}">
      <Declaration><![CDATA[METHOD TryLogMessage : BOOL
VAR_INPUT
	message : ERROR_MESSAGE;
	category : LOG_CATEGORY;
	topic1 : WSTRING := "";	
	topic2 : WSTRING := "";	
	topic3 : WSTRING := "";	
	topic4 : WSTRING := "";	
	topic5 : WSTRING := "";	
END_VAR
VAR
	topics : ARRAY [0..4] OF WSTRING; 
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[topics[0] := topic1;
topics[1] := topic2;
topics[2] := topic3;
topics[3] := topic4;
topics[4] := topic5;

IF Filter <> 0 AND_THEN NOT Filter.ShouldLog(category, topics) THEN
	RETURN;
END_IF

TryLogMessage := Logger.TryLogMessage(category, message);]]></ST>
      </Implementation>
    </Method>
  </POU>
</TcPlcObject>