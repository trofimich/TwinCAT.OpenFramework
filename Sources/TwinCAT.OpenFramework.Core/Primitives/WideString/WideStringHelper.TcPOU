<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1">
  <POU Name="WideStringHelper" Id="{a08ca29b-58ac-448f-9daf-cbacb656ba72}" SpecialFunc="None">
    <Declaration><![CDATA[(*

.. <legal notes>

legal notes
=================
| SPDX-FileCopyrightText: © 2026 Oleksandr Tiutyk <trofimich@gmail.com>
| SPDX-License-Identifier: LGPL-3.0-only
| For details check: LGPL-3.0-only_

.. _LGPL-3.0-only: https://www.gnu.org/licenses/lgpl-3.0.en.html

.. </legal notes>

*)

{attribute 'no_explicit_call' := 'Direct call is not allowed'} 
PROGRAM WideStringHelper
VAR_INPUT CONSTANT
	CharSize : BYTE := 2;
	NewLine : WSTRING(1) := "$R"; 
	Empty : WSTRING(0) := "";
	ByteOrderMask : WORD := 16#FEFF;
END_VAR
VAR
	_ClassName : STRING := __POUNAME();
END_VAR]]></Declaration>
    <Implementation>
      <ST><![CDATA[]]></ST>
    </Implementation>
    <Folder Name="Append" Id="{0bb8b256-f491-4a2d-9a6b-2432088dbd33}" />
    <Folder Name="Replace" Id="{a756485d-6296-418d-b791-6f297941943f}" />
    <Folder Name="Service members" Id="{9fab643e-bbff-4a9b-947d-8a7d558c89e1}" />
    <Folder Name="Split" Id="{3fe01d04-7439-4837-881a-92e26af7a9f4}" />
    <Folder Name="Trim" Id="{222b85cc-d2c6-46ad-a621-67a695bdca8e}" />
    <Method Name="AnyToString255" Id="{5eddfbb1-a4c4-4bf3-95de-4ec7342266cd}">
      <Declaration><![CDATA[METHOD AnyToString255 : WSTRING(255)
VAR_INPUT
	value : ANY;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[AnyToString255 := InternalAnyToString255(value);]]></ST>
      </Implementation>
    </Method>
    <Method Name="AppendToTarget" Id="{ed2085a1-77d7-4a38-8551-436ba13822e3}" FolderPath="Append\">
      <Declaration><![CDATA[METHOD AppendToTarget
VAR_INPUT
	targetString : ANY_STRING;
END_VAR
VAR_IN_OUT CONSTANT
	sourceString : WSTRING;
END_VAR
VAR
	stringTypeNotSupportedException : NotSupportedException;
	targetStringMemoryOverflowExcepton : TargetVariableMemoryOwerflowException;	
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF targetString.TypeClass <> __SYSTEM.TYPE_CLASS.TYPE_WSTRING THEN
	stringTypeNotSupportedException.Throw(NamespaceName, ClassName, __POUNAME(), __POSITION(), Tc2_Utilities.WCONCAT("Not supported type ", TO_WSTRING(targetString.TypeClass)));
END_IF

IF NOT InternalTryAppendToTarget(targetString, ADR(sourceString), 0, 0, 0, 0, 0, 0, 0, 0, 0) THEN
	targetStringMemoryOverflowExcepton.Throw(NamespaceName, ClassName, __POUNAME(), __POSITION(), 'targetString', TO_ULINT(targetString.diSize)); 
END_IF]]></ST>
      </Implementation>
    </Method>
    <Method Name="AppendToTarget255" Id="{ccf916ac-f329-40a8-9de1-90c04294dea5}" FolderPath="Append\">
      <Declaration><![CDATA[METHOD AppendToTarget255
VAR_INPUT
	targetString : ANY_STRING;
	sourceString1 : WSTRING(255);
	sourceString2 : WSTRING(255) := "";
	sourceString3 : WSTRING(255) := "";
	sourceString4 : WSTRING(255) := "";
	sourceString5 : WSTRING(255) := "";
	sourceString6 : WSTRING(255) := "";
	sourceString7 : WSTRING(255) := "";
	sourceString8 : WSTRING(255) := "";
	sourceString9 : WSTRING(255) := "";
	sourceString10 : WSTRING(255) := "";
END_VAR
VAR
	stringTypeNotSupportedException : NotSupportedException;
	targetStringMemoryOverflowExcepton : TargetVariableMemoryOwerflowException;	
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF targetString.TypeClass <> __SYSTEM.TYPE_CLASS.TYPE_WSTRING THEN
	stringTypeNotSupportedException.Throw(NamespaceName, ClassName, __POUNAME(), __POSITION(), Tc2_Utilities.WCONCAT("Not supported type ", TO_WSTRING(targetString.TypeClass)));
END_IF

IF NOT InternalTryAppendToTarget(targetString, ADR(sourceString1), ADR(sourceString2), ADR(sourceString3), ADR(sourceString4), ADR(sourceString5), ADR(sourceString6), ADR(sourceString7), ADR(sourceString8), ADR(sourceString9), ADR(sourceString10)) THEN
	targetStringMemoryOverflowExcepton.Throw(NamespaceName, ClassName, __POUNAME(), __POSITION(), 'targetString', TO_ULINT(targetString.diSize)); 
END_IF]]></ST>
      </Implementation>
    </Method>
    <Property Name="ClassName" Id="{1a6c82de-d589-49d7-9c77-f4f624ad8685}">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
PROPERTY ClassName : STRING]]></Declaration>
      <Get Name="Get" Id="{9690cba0-2d05-4c7a-ad00-db2bf2465f2d}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[ClassName := _ClassName;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Method Name="ConcatStrings255" Id="{8dba526b-4d55-4f06-81f2-ed0ad34ce538}">
      <Declaration><![CDATA[METHOD ConcatStrings255 : WSTRING(255)
VAR_INPUT
	string1 : WSTRING(255);
	string2 : WSTRING(255);
	string3 : WSTRING(255) := "";
	string4 : WSTRING(255) := "";
	string5 : WSTRING(255) := "";
	string6 : WSTRING(255) := "";
	string7 : WSTRING(255) := "";
	string8 : WSTRING(255) := "";
	string9 : WSTRING(255) := "";
	string10 : WSTRING(255) := "";
	string11 : WSTRING(255) := "";
	string12 : WSTRING(255) := "";
	string13 : WSTRING(255) := "";
	string14 : WSTRING(255) := "";
	string15 : WSTRING(255) := "";
	string16 : WSTRING(255) := "";
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[ConcatStrings255 := Tc2_Standard.WCONCAT(string1, string2);

IF Tc2_Standard.WLEN(string3) = 0 THEN
	RETURN;
ELSE
	ConcatStrings255 := Tc2_Standard.WCONCAT(ConcatStrings255, string3);
END_IF

IF Tc2_Standard.WLEN(string4) = 0 THEN
	RETURN;
ELSE
	ConcatStrings255 := Tc2_Standard.WCONCAT(ConcatStrings255, string4);
END_IF

IF Tc2_Standard.WLEN(string5) = 0 THEN
	RETURN;
ELSE
	ConcatStrings255 := Tc2_Standard.WCONCAT(ConcatStrings255, string5);
END_IF

IF Tc2_Standard.WLEN(string6) = 0 THEN
	RETURN;
ELSE
	ConcatStrings255 := Tc2_Standard.WCONCAT(ConcatStrings255, string6);
END_IF

IF Tc2_Standard.WLEN(string7) = 0 THEN
	RETURN;
ELSE
	ConcatStrings255 := Tc2_Standard.WCONCAT(ConcatStrings255, string7);
END_IF

IF Tc2_Standard.WLEN(string8) = 0 THEN
	RETURN;
ELSE
	ConcatStrings255 := Tc2_Standard.WCONCAT(ConcatStrings255, string8);
END_IF

IF Tc2_Standard.WLEN(string9) = 0 THEN
	RETURN;
ELSE
	ConcatStrings255 := Tc2_Standard.WCONCAT(ConcatStrings255, string9);
END_IF

IF Tc2_Standard.WLEN(string10) = 0 THEN
	RETURN;
ELSE
	ConcatStrings255 := Tc2_Standard.WCONCAT(ConcatStrings255, string10);
END_IF

IF Tc2_Standard.WLEN(string11) = 0 THEN
	RETURN;
ELSE
	ConcatStrings255 := Tc2_Standard.WCONCAT(ConcatStrings255, string11);
END_IF

IF Tc2_Standard.WLEN(string12) = 0 THEN
	RETURN;
ELSE
	ConcatStrings255 := Tc2_Standard.WCONCAT(ConcatStrings255, string12);
END_IF

IF Tc2_Standard.WLEN(string13) = 0 THEN
	RETURN;
ELSE
	ConcatStrings255 := Tc2_Standard.WCONCAT(ConcatStrings255, string13);
END_IF

IF Tc2_Standard.WLEN(string14) = 0 THEN
	RETURN;
ELSE
	ConcatStrings255 := Tc2_Standard.WCONCAT(ConcatStrings255, string14);
END_IF

IF Tc2_Standard.WLEN(string15) = 0 THEN
	RETURN;
ELSE
	ConcatStrings255 := Tc2_Standard.WCONCAT(ConcatStrings255, string15);
END_IF

IF Tc2_Standard.WLEN(string16) = 0 THEN
	RETURN;
ELSE
	ConcatStrings255 := Tc2_Standard.WCONCAT(ConcatStrings255, string16);
END_IF]]></ST>
      </Implementation>
    </Method>
    <Method Name="CreateSplitter" Id="{58f73e01-1198-4842-bb84-b0fc8575acd0}" FolderPath="Split\">
      <Declaration><![CDATA[METHOD CreateSplitter : WideStringSplittingEnumerator
VAR_INPUT
	sourceString : ANY_STRING;
	separator : WSTRING(255);
	maxCount : UDINT := 0;
	trimEntries : BOOL := FALSE;
	removeEmptyEntries : BOOL := FALSE;
END_VAR
VAR
	stringTypeNotSupportedException : NotSupportedException;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF sourceString.TypeClass <> __SYSTEM.TYPE_CLASS.TYPE_WSTRING THEN
	stringTypeNotSupportedException.Throw(NamespaceName, ClassName, __POUNAME(), __POSITION(), Tc2_Utilities.WCONCAT("Not supported type ", TO_WSTRING(sourceString.TypeClass)));
END_IF

CreateSplitter.Initialize(sourceString.pValue, separator, maxCount, trimEntries, removeEmptyEntries);]]></ST>
      </Implementation>
    </Method>
    <Method Name="Find" Id="{fbf73ef7-e095-455c-b374-56142e92beda}">
      <Declaration><![CDATA[METHOD Find : BOOL
VAR_IN_OUT CONSTANT
	stringToSearchIn : WSTRING;
	stringToFind : WSTRING;
END_VAR
VAR_OUTPUT
	position : UDINT;
END_VAR
VAR
	searchInStringLength : UDINT;
	stringToFindLength : UDINT;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[Find := InternalFind(ADR(stringToSearchIn), InternalGetLength(ADR(stringToSearchIn)), ADR(stringToFind), InternalGetLength(ADR(stringToFindLength)), position => position);]]></ST>
      </Implementation>
    </Method>
    <Method Name="GetActualSize" Id="{f88efc62-cf38-4f16-a4e6-1677f1ebb799}">
      <Declaration><![CDATA[METHOD GetActualSize : UDINT
VAR_IN_OUT CONSTANT
	value : WSTRING;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[GetActualSize := (InternalGetLength(ADR(value)) * CharSize) + CharSize;]]></ST>
      </Implementation>
    </Method>
    <Method Name="GetLength" Id="{92cb56d9-db9c-4638-bd53-1d463d8daa4a}">
      <Declaration><![CDATA[METHOD GetLength : UDINT
VAR_IN_OUT CONSTANT
	value : WSTRING;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[GetLength := InternalGetLength(ADR(value));]]></ST>
      </Implementation>
    </Method>
    <Method Name="InternalAnyToString255" Id="{b35c56b7-7d8f-4737-8cea-d0ecbebe9e7f}" FolderPath="Service members\">
      <Declaration><![CDATA[METHOD INTERNAL InternalAnyToString255 : WSTRING(255)
VAR_INPUT
	value : __SYSTEM.AnyType;
END_VAR
VAR
	anyBool : POINTER TO BOOL;

	anyByte : POINTER TO BYTE;
	anyWord : POINTER TO WORD;
	anyDWord : POINTER TO LWORD;
	anyLWord : POINTER TO LWORD;
	
	anySInt : POINTER TO SINT;
	anyInt : POINTER TO INT;
	anyDInt : POINTER TO DINT;
	anyLInt : POINTER TO LINT;
	
	anyUSInt : POINTER TO USINT;
	anyUInt : POINTER TO UINT;
	anyUDInt : POINTER TO UDINT;
	anyULInt : POINTER TO ULINT;
	
	anyReal : POINTER TO REAL;
	anyLReal : POINTER TO LREAL;

	anyString : POINTER TO STRING(255);
	anyWString : POINTER TO WSTRING(255);
	
	anyTime : POINTER TO TIME;
	anyLTime : POINTER TO LTIME;
	anyDate : POINTER TO DATE;
	anyDateAndTime : POINTER TO DATE_AND_TIME;
	anyTimeOfDay : POINTER TO TOD;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[CASE value.TypeClass OF
	__SYSTEM.TYPE_CLASS.TYPE_BOOL:
		anyBool := value.pValue;
		InternalAnyToString255 := TO_WSTRING(anyBool^);		
	
	__SYSTEM.TYPE_CLASS.TYPE_BIT:
		anyByte := value.pValue;
		IF anyByte^ > 0 THEN
			InternalAnyToString255 := "1";		
		ELSE
			InternalAnyToString255 := "0";		
		END_IF
	
	__SYSTEM.TYPE_CLASS.TYPE_BYTE:
		anyByte := value.pValue;
		InternalAnyToString255 := TO_WSTRING(anyByte^);		
	__SYSTEM.TYPE_CLASS.TYPE_WORD:
		anyWord := value.pValue;
		InternalAnyToString255 := TO_WSTRING(anyWord^);
	__SYSTEM.TYPE_CLASS.TYPE_DWORD:
		anyDWord := value.pValue;
		InternalAnyToString255 := TO_WSTRING(anyDWord^);
	__SYSTEM.TYPE_CLASS.TYPE_LWORD:
		anyLWord := value.pValue;
		InternalAnyToString255 := TO_WSTRING(anyLWord^);
		
	__SYSTEM.TYPE_CLASS.TYPE_SINT:
		anySInt := value.pValue;
		InternalAnyToString255 := TO_WSTRING(anySInt^);
	__SYSTEM.TYPE_CLASS.TYPE_INT:
		anyInt := value.pValue;
		InternalAnyToString255 := TO_WSTRING(anyInt^);		
	__SYSTEM.TYPE_CLASS.TYPE_DINT:
		anyDInt := value.pValue;
		InternalAnyToString255 := TO_WSTRING(anyDInt^);
	__SYSTEM.TYPE_CLASS.TYPE_LINT:
		anyLInt := value.pValue;
		InternalAnyToString255 := TO_WSTRING(anyLInt^);
		
	__SYSTEM.TYPE_CLASS.TYPE_USINT:
		anyUSInt := value.pValue;
		InternalAnyToString255 := TO_WSTRING(anyUSInt^);
	__SYSTEM.TYPE_CLASS.TYPE_UINT:
		anyUInt := value.pValue;
		InternalAnyToString255 := TO_WSTRING(anyUInt^);		
	__SYSTEM.TYPE_CLASS.TYPE_UDINT:
		anyUDInt := value.pValue;
		InternalAnyToString255 := TO_WSTRING(anyUDInt^);
	__SYSTEM.TYPE_CLASS.TYPE_ULINT:
		anyULInt := value.pValue;
		InternalAnyToString255 := TO_WSTRING(anyULInt^);

	__SYSTEM.TYPE_CLASS.TYPE_REAL:
		anyReal := value.pValue;
		InternalAnyToString255 := TO_WSTRING(anyReal^);
	__SYSTEM.TYPE_CLASS.TYPE_LREAL:
		anyLReal := value.pValue;
		InternalAnyToString255 := TO_WSTRING(anyLReal^);
	
	__SYSTEM.TYPE_CLASS.TYPE_STRING:
		anyString := value.pValue;
		InternalAnyToString255 := TO_WSTRING(anyString^);
	__SYSTEM.TYPE_CLASS.TYPE_WSTRING:
		anyWString := value.pValue;
		InternalAnyToString255 := anyWString^;
	
	__SYSTEM.TYPE_CLASS.TYPE_TIME:
		anyTime := value.pValue;
		InternalAnyToString255 := TO_WSTRING(anyTime^);
	__SYSTEM.TYPE_CLASS.TYPE_LTIME:
		anyLTime := value.pValue;
		InternalAnyToString255 := TO_WSTRING(anyLTime^);
	__SYSTEM.TYPE_CLASS.TYPE_DATE:
		anyDate := value.pValue;
		InternalAnyToString255 := TO_WSTRING(anyDate^);
	__SYSTEM.TYPE_CLASS.TYPE_DATEANDTIME:
		anyDateAndTime := value.pValue;
		InternalAnyToString255 := TO_WSTRING(anyDateAndTime^);  
	__SYSTEM.TYPE_CLASS.TYPE_TIMEOFDAY:
		anyTimeOfDay := value.pValue;
		InternalAnyToString255 := TO_WSTRING(anyTimeOfDay^);
	
	__SYSTEM.TYPE_CLASS.TYPE_POINTER, __SYSTEM.TYPE_CLASS.TYPE_REFERENCE:
		InternalAnyToString255 := TO_WSTRING(Tc2_Utilities.PVOID_TO_HEXSTR(anyDate, 8, FALSE));		
	
	(*__SYSTEM.TYPE_CLASS.TYPE_SUBRANGE:
	__SYSTEM.TYPE_CLASS.TYPE_ENUM:
	__SYSTEM.TYPE_CLASS.TYPE_ARRAY:
	__SYSTEM.TYPE_CLASS.TYPE_PARAMS:
	__SYSTEM.TYPE_CLASS.TYPE_USERDEF:
	__SYSTEM.TYPE_CLASS.TYPE_NONE:
	__SYSTEM.TYPE_CLASS.TYPE_ANY:
	__SYSTEM.TYPE_CLASS.TYPE_ANYBIT:
	__SYSTEM.TYPE_CLASS.TYPE_ANYDATE:
	__SYSTEM.TYPE_CLASS.TYPE_ANYINT:
	__SYSTEM.TYPE_CLASS.TYPE_ANYNUM:
	__SYSTEM.TYPE_CLASS.TYPE_ANYREAL:
	__SYSTEM.TYPE_CLASS.TYPE_LAZY:
	__SYSTEM.TYPE_CLASS.TYPE_BITCONST:*)
	ELSE
		IF value.pValue <> 0 THEN
			InternalAnyToString255 := TO_WSTRING(value.pValue^); 
		ELSE
			InternalAnyToString255 := "NULL";
		END_IF
END_CASE	]]></ST>
      </Implementation>
    </Method>
    <Method Name="InternalFind" Id="{06a5edf2-4fa6-4c6f-b8c2-32a4e932af0a}" FolderPath="Service members\">
      <Declaration><![CDATA[METHOD INTERNAL InternalFind : BOOL
VAR_INPUT
	stringToSearchIn : POINTER TO WSTRING;
	stringToSearchInLength : UDINT;
	stringToFind : POINTER TO WSTRING;
	stringToFindLength : UDINT;
END_VAR
VAR_OUTPUT
	position : UDINT;
END_VAR
VAR	
	i : UDINT;
	memCoparisionResult : DINT;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF stringToSearchInLength < stringToFindLength THEN
	RETURN;
END_IF

FOR i := 0 TO stringToSearchInLength - stringToFindLength DO
	IF Tc2_System.MEMCMP(stringToSearchIn + (i * CharSize), stringToFind, stringToFindLength * CharSize) = 0 THEN
		position := i + 1;
		InternalFind := TRUE;
		RETURN;
	END_IF
END_FOR]]></ST>
      </Implementation>
    </Method>
    <Method Name="InternalGetLength" Id="{28a2b8aa-3a94-4df4-930f-bfbe80fc9784}" FolderPath="Service members\">
      <Declaration><![CDATA[METHOD INTERNAL InternalGetLength : UDINT
VAR_INPUT
	value : POINTER TO WSTRING;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[InternalGetLength := SEL(value = 0, Tc2_Utilities.WLEN2(value), 0);]]></ST>
      </Implementation>
    </Method>
    <Method Name="InternalReplaceInSource" Id="{ba1ad49c-b07c-4f21-b23e-4e0400c679ac}" FolderPath="Service members\">
      <Declaration><![CDATA[METHOD INTERNAL InternalReplaceInSource : BOOL
VAR_INPUT
	sourceString : __SYSTEM.AnyType;
	oldSubstring : POINTER TO WSTRING;
	newSubstring : POINTER TO WSTRING;
	maxReplacements : UINT;
	skipReplacements : UINT;
END_VAR
VAR_OUTPUT
	replacementCount : UINT;
END_VAR
VAR
	sourceStringMemorySize : UDINT;
	sourceStringLength : UDINT;

	handledString : POINTER TO WSTRING;
	handledStringLength : UDINT;
		
	oldSubstringLength : UDINT;
	newSubstringLength : UDINT;
	checkMemorySize : BOOL;	
	
	substringPosition : UDINT;

	skipCount : UINT;
	
	memFunctionResult : UDINT;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[sourceStringMemorySize := TO_UDINT(sourceString.diSize);
sourceStringLength := InternalGetLength(sourceString.pValue) * CharSize;

handledString := sourceString.pValue;
handledStringLength := sourceStringLength;

oldSubstringLength := InternalGetLength(oldSubstring) * CharSize;
newSubstringLength := InternalGetLength(newSubstring) * CharSize;
checkMemorySize := oldSubstringLength < newSubstringLength;

WHILE handledStringLength >= oldSubstringLength DO
	IF NOT InternalFind(handledString, handledStringLength / CharSize, oldSubstring, oldSubstringLength / CharSize, position => substringPosition) THEN
		EXIT;
	END_IF
	
	IF skipReplacements > 0 AND_THEN skipCount < skipReplacements THEN
		skipCount := skipCount + 1;

		handledString := handledString + oldSubstringLength + (substringPosition * CharSize) - CharSize;	
		handledStringLength := handledStringLength - oldSubstringLength - (substringPosition * CharSize) + CharSize;
		
		CONTINUE;		
	END_IF
	
	handledString := handledString + (substringPosition * CharSize) - CharSize;
	handledStringLength := handledStringLength - (substringPosition * CharSize) + CharSize;

	sourceStringLength := sourceStringLength + newSubstringLength - oldSubstringLength;
	
	IF checkMemorySize AND_THEN sourceStringLength + CharSize > sourceStringMemorySize THEN
		RETURN;
	END_IF
	
	IF oldSubstringLength <> newSubstringLength THEN
		memFunctionResult := Tc2_System.MEMMOVE(handledString + newSubstringLength, handledString + oldSubstringLength, handledStringLength - oldSubstringLength + CharSize);
		
		IF memFunctionResult < 1 THEN
			RETURN;			
		END_IF
		
		handledStringLength := handledStringLength + newSubstringLength - oldSubstringLength;
	END_IF
	
	memFunctionResult := Tc2_System.MEMCPY(handledString, newSubstring, newSubstringLength);

	IF memFunctionResult < 1 THEN
		RETURN;			
	END_IF
	
	replacementCount := replacementCount + 1;
	
	IF maxReplacements > 0 AND_THEN replacementCount>= maxReplacements THEN
		EXIT;
	END_IF

	handledString := handledString + newSubstringLength;	
	handledStringLength := handledStringLength - newSubstringLength;
END_WHILE

InternalReplaceInSource := TRUE;]]></ST>
      </Implementation>
    </Method>
    <Method Name="InternalTryAppendToTarget" Id="{0fda8c2e-c85d-41ae-b5cc-9b9c596ab7fb}" FolderPath="Service members\">
      <Declaration><![CDATA[METHOD INTERNAL InternalTryAppendToTarget : BOOL
VAR_INPUT
	targetString : __SYSTEM.AnyType;
	sourceString1 : POINTER TO WSTRING;
	sourceString2 : POINTER TO WSTRING;
	sourceString3 : POINTER TO WSTRING;
	sourceString4 : POINTER TO WSTRING;
	sourceString5 : POINTER TO WSTRING;
	sourceString6 : POINTER TO WSTRING;
	sourceString7 : POINTER TO WSTRING;
	sourceString8 : POINTER TO WSTRING;
	sourceString9 : POINTER TO WSTRING;
	sourceString10 : POINTER TO WSTRING;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF targetString.TypeClass <> __SYSTEM.TYPE_CLASS.TYPE_WSTRING THEN
	RETURN;
END_IF

InternalTryAppendToTarget := Tc2_Utilities.WCONCAT2(targetString.pValue, sourceString1, targetString.pValue, TO_UDINT(targetString.diSize));

IF NOT InternalTryAppendToTarget OR_ELSE Tc2_Utilities.WLEN2(sourceString2) = 0 THEN
	RETURN;
ELSE
	InternalTryAppendToTarget := Tc2_Utilities.WCONCAT2(targetString.pValue, sourceString2, targetString.pValue, TO_UDINT(targetString.diSize));
END_IF

IF NOT InternalTryAppendToTarget OR_ELSE Tc2_Utilities.WLEN2(sourceString3) = 0 THEN
	RETURN;
ELSE
	InternalTryAppendToTarget := Tc2_Utilities.WCONCAT2(targetString.pValue, sourceString3, targetString.pValue, TO_UDINT(targetString.diSize));
END_IF

IF NOT InternalTryAppendToTarget OR_ELSE Tc2_Utilities.WLEN2(sourceString4) = 0 THEN
	RETURN;
ELSE
	InternalTryAppendToTarget := Tc2_Utilities.WCONCAT2(targetString.pValue, sourceString4, targetString.pValue, TO_UDINT(targetString.diSize));
END_IF

IF NOT InternalTryAppendToTarget OR_ELSE Tc2_Utilities.WLEN2(sourceString5) = 0 THEN
	RETURN;
ELSE
	InternalTryAppendToTarget := Tc2_Utilities.WCONCAT2(targetString.pValue, sourceString5, targetString.pValue, TO_UDINT(targetString.diSize));
END_IF

IF NOT InternalTryAppendToTarget OR_ELSE Tc2_Utilities.WLEN2(sourceString6) = 0 THEN
	RETURN;
ELSE
	InternalTryAppendToTarget := Tc2_Utilities.WCONCAT2(targetString.pValue, sourceString6, targetString.pValue, TO_UDINT(targetString.diSize));
END_IF

IF NOT InternalTryAppendToTarget OR_ELSE Tc2_Utilities.WLEN2(sourceString7) = 0 THEN
	RETURN;
ELSE
	InternalTryAppendToTarget := Tc2_Utilities.WCONCAT2(targetString.pValue, sourceString7, targetString.pValue, TO_UDINT(targetString.diSize));
END_IF

IF NOT InternalTryAppendToTarget OR_ELSE Tc2_Utilities.WLEN2(sourceString8) = 0 THEN
	RETURN;
ELSE
	InternalTryAppendToTarget := Tc2_Utilities.WCONCAT2(targetString.pValue, sourceString8, targetString.pValue, TO_UDINT(targetString.diSize));
END_IF

IF NOT InternalTryAppendToTarget OR_ELSE Tc2_Utilities.WLEN2(sourceString9) = 0 THEN
	RETURN;
ELSE
	InternalTryAppendToTarget := Tc2_Utilities.WCONCAT2(targetString.pValue, sourceString9, targetString.pValue, TO_UDINT(targetString.diSize));
END_IF

IF NOT InternalTryAppendToTarget OR_ELSE Tc2_Utilities.WLEN2(sourceString10) = 0 THEN
	RETURN;
ELSE
	InternalTryAppendToTarget := Tc2_Utilities.WCONCAT2(targetString.pValue, sourceString10, targetString.pValue, TO_UDINT(targetString.diSize));
END_IF]]></ST>
      </Implementation>
    </Method>
    <Method Name="IsNullOrWhiteSpace" Id="{f6d91594-f8c2-4b36-b45d-e7cc6eae55c4}">
      <Declaration><![CDATA[METHOD IsNullOrWhiteSpace : BOOL
VAR_INPUT
	stringToCheck : REFERENCE TO WSTRING;
END_VAR
VAR
	i : DINT;
	
	stringData : POINTER TO WORD;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[IsNullOrWhiteSpace := TRUE;

IF NOT __ISVALIDREF(stringToCheck) THEN
	RETURN;
END_IF

stringData := ADR(stringToCheck);

WHILE stringData[i] <> 0 DO
	CASE stringData[i] OF
		16#0020, 16#00A0, 16#1680, 16#2000, 16#2001, 16#2002, 16#2003, 16#2004, 16#2005, 16#2006, 16#2007, 16#2008, 16#2009, 
		16#200A, 16#202F, 16#205F, 16#3000, 16#2028, 16#2029, 16#0009, 16#000A, 16#000B, 16#000C, 16#000D, 16#0085:
			i := i + 1;
			
		ELSE
			IsNullOrWhiteSpace := FALSE;
			RETURN;
	END_CASE 
END_WHILE]]></ST>
      </Implementation>
    </Method>
    <Property Name="NamespaceName" Id="{06787fa4-fd31-454c-be97-5c26e13d97fa}">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
PROPERTY NamespaceName : STRING]]></Declaration>
      <Get Name="Get" Id="{404a412b-96d2-4c44-ae43-0c154ea517e4}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[NamespaceName := CurrentNamespace.Name;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Method Name="ReplaceInSource" Id="{c35f862a-887f-4b4c-96da-6739cfbc7256}" FolderPath="Replace\">
      <Declaration><![CDATA[METHOD ReplaceInSource
VAR_INPUT
	sourceString : ANY_STRING;
END_VAR
VAR_IN_OUT CONSTANT
	oldSubstring : WSTRING;
	newSubstring : WSTRING;
END_VAR
VAR_INPUT
	maxReplacements : UINT := 0;
	skipReplacements : UINT := 0;
END_VAR
VAR_OUTPUT
	replacementCount : UINT;
END_VAR
VAR	
	stringTypeNotSupportedException : NotSupportedException;
	sourceStringMemoryOverflowExcepton : TargetVariableMemoryOwerflowException;
	
	replaceResult : BOOL; 
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF sourceString.TypeClass <> __SYSTEM.TYPE_CLASS.TYPE_WSTRING THEN
	stringTypeNotSupportedException.Throw(NamespaceName, ClassName, __POUNAME(), __POSITION(), Tc2_Utilities.WCONCAT("Not supported type ", TO_WSTRING(sourceString.TypeClass)));
END_IF

IF NOT InternalReplaceInSource(sourceString, ADR(oldSubstring), ADR(newSubstring), maxReplacements, skipReplacements, replacementCount => replacementCount) THEN
	sourceStringMemoryOverflowExcepton.Throw(NamespaceName, ClassName, __POUNAME(), __POSITION(), 'sourceString', TO_UDINT(sourceString.diSize));
END_IF]]></ST>
      </Implementation>
    </Method>
    <Method Name="ReplaceInSource255" Id="{e0f107e1-9b6f-4d77-b127-da5f2bb84170}" FolderPath="Replace\">
      <Declaration><![CDATA[METHOD ReplaceInSource255
VAR_INPUT
	sourceString : ANY_STRING;
	oldSubstring : WSTRING(255);
	newSubstring : WSTRING(255);
	maxReplacements : UINT := 0;
	skipReplacements : UINT := 0;
END_VAR
VAR_OUTPUT
	replacementCount : UINT;
END_VAR
VAR	
	stringTypeNotSupportedException : NotSupportedException;
	sourceStringMemoryOverflowExcepton : TargetVariableMemoryOwerflowException;
	
	replaceResult : BOOL; 
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF sourceString.TypeClass <> __SYSTEM.TYPE_CLASS.TYPE_WSTRING THEN
	stringTypeNotSupportedException.Throw(NamespaceName, ClassName, __POUNAME(), __POSITION(), Tc2_Utilities.WCONCAT("Not supported type ", TO_WSTRING(sourceString.TypeClass)));
END_IF

IF NOT InternalReplaceInSource(sourceString, ADR(oldSubstring), ADR(newSubstring), maxReplacements, skipReplacements, replacementCount => replacementCount) THEN
	sourceStringMemoryOverflowExcepton.Throw(NamespaceName, ClassName, __POUNAME(), __POSITION(), 'sourceString', TO_UDINT(sourceString.diSize));
END_IF]]></ST>
      </Implementation>
    </Method>
    <Method Name="StringPointerToAnyType" Id="{e7b7eb9d-23fd-4387-8408-e33d707c6233}">
      <Declaration><![CDATA[METHOD StringPointerToAnyType : __System.AnyType
VAR_INPUT
	value : POINTER TO WSTRING;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[StringPointerToAnyType.TypeClass := __SYSTEM.TYPE_CLASS.TYPE_WSTRING;
StringPointerToAnyType.pValue := value;
StringPointerToAnyType.diSize := TO_DINT(GetActualSize(value^));]]></ST>
      </Implementation>
    </Method>
    <Method Name="StringReferenceToAnyType" Id="{8467a0c9-fba0-4e3d-9b58-fb51783bbdec}">
      <Declaration><![CDATA[METHOD StringReferenceToAnyType : __System.AnyType
VAR_INPUT
	value : REFERENCE TO WSTRING;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[StringReferenceToAnyType.TypeClass := __SYSTEM.TYPE_CLASS.TYPE_WSTRING;
StringReferenceToAnyType.pValue := ADR(value);
StringReferenceToAnyType.diSize := TO_DINT(GetActualSize(value));]]></ST>
      </Implementation>
    </Method>
    <Method Name="TrimChar" Id="{e2ff62b2-4701-4d44-a6fc-9b69cb60ec38}" FolderPath="Trim\">
      <Declaration><![CDATA[METHOD TrimChar
VAR_IN_OUT
	stringToTrim : WSTRING;
END_VAR
VAR_INPUT
	trimMode : TRIM_MODE := TRIM_MODE.LEFT_AND_RIGHT;
	trimChar : WSTRING(1) := " ";
END_VAR
VAR
	trimCharArray : ARRAY [0..0] OF WSTRING(1);	
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[trimCharArray[0] := trimChar;

TrimChars(stringToTrim, trimMode, trimCharArray);]]></ST>
      </Implementation>
    </Method>
    <Method Name="TrimChars" Id="{cb597a03-3a6d-40b2-92cb-bb844efea4ef}" FolderPath="Trim\">
      <Declaration><![CDATA[METHOD TrimChars
VAR_IN_OUT
	stringToTrim : WSTRING;
END_VAR
VAR_INPUT
	trimMode : TRIM_MODE := TRIM_MODE.LEFT_AND_RIGHT;
END_VAR
VAR_IN_OUT CONSTANT
	trimChars : ARRAY [*] OF WSTRING(1);	
END_VAR
VAR
	stringLength : UDINT;
	stringData : POINTER TO WORD;
	trimCharData : POINTER TO WORD;
	
	originalCharIndex : UDINT;
	trimCharIndex : UDINT;
	trimCharCount : UDINT;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[stringData := ADR(stringToTrim);
stringLength := InternalGetLength(stringData);

IF stringLength < 1 THEN
	RETURN;
END_IF

IF trimMode = TRIM_MODE.LEFT_AND_RIGHT OR_ELSE trimMode = TRIM_MODE.LEFT THEN
	trimCharCount := 0;
	
	FOR originalCharIndex := 0 TO stringLength - 1 BY 1 DO
		FOR trimCharIndex := TO_UDINT(LOWER_BOUND(trimChars, 1)) TO TO_UDINT(UPPER_BOUND(trimChars, 1)) DO
			trimCharData := ADR(trimChars[trimCharIndex]);
			
			IF stringData[originalCharIndex] = trimCharData^ THEN
				trimCharCount := trimCharCount + 1;
				EXIT;
			END_IF
		END_FOR
		
		IF trimCharCount <> originalCharIndex + 1 THEN
			EXIT;
		END_IF
	END_FOR

	IF trimCharCount > 0 THEN
		stringLength := stringLength - trimCharCount;
		
		Tc2_System.MEMCPY(stringData, (stringData + (trimCharCount * CharSize)), (stringLength * CharSize) + CharSize);		
	END_IF
END_IF

IF stringLength < 1 THEN
	RETURN;
END_IF

IF trimMode = TRIM_MODE.LEFT_AND_RIGHT OR_ELSE trimMode = TRIM_MODE.RIGHT THEN
	trimCharCount := 0;
	
	{warning disable C0266}
	FOR originalCharIndex := stringLength - 1 TO 0 BY -1 DO
	{warning restore C0266}
		FOR trimCharIndex := TO_UDINT(LOWER_BOUND(trimChars, 1)) TO TO_UDINT(UPPER_BOUND(trimChars, 1)) DO
			trimCharData := ADR(trimChars[trimCharIndex]);
			
			IF stringData[originalCharIndex] = trimCharData^ THEN
				trimCharCount := trimCharCount + 1;
				EXIT;
			END_IF
		END_FOR
		
		IF trimCharCount <> stringLength - originalCharIndex THEN
			EXIT;
		END_IF
	END_FOR

	IF trimCharCount > 0 THEN
		stringLength := stringLength - trimCharCount;
		
		Tc2_System.MEMSET(stringData + (stringLength * CharSize), 0, CharSize);		
	END_IF	
END_IF]]></ST>
      </Implementation>
    </Method>
    <Method Name="TryAppendToTarget" Id="{4de57109-f456-48e2-92b9-7ccc4d2d48fb}" FolderPath="Append\">
      <Declaration><![CDATA[METHOD TryAppendToTarget : BOOL
VAR_INPUT
	targetString : ANY_STRING;
END_VAR
VAR_IN_OUT CONSTANT
	sourceString : WSTRING;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[TryAppendToTarget := InternalTryAppendToTarget(targetString, ADR(sourceString), 0, 0, 0, 0, 0, 0, 0, 0, 0);]]></ST>
      </Implementation>
    </Method>
    <Method Name="TryAppendToTarget255" Id="{85ee2c53-41c4-4f11-9049-314aaf814c0e}" FolderPath="Append\">
      <Declaration><![CDATA[METHOD TryAppendToTarget255 : BOOL
VAR_INPUT
	targetString : ANY_STRING;
	sourceString1 : WSTRING(255);
	sourceString2 : WSTRING(255) := "";
	sourceString3 : WSTRING(255) := "";
	sourceString4 : WSTRING(255) := "";
	sourceString5 : WSTRING(255) := "";
	sourceString6 : WSTRING(255) := "";
	sourceString7 : WSTRING(255) := "";
	sourceString8 : WSTRING(255) := "";
	sourceString9 : WSTRING(255) := "";
	sourceString10 : WSTRING(255) := "";
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[TryAppendToTarget255 := InternalTryAppendToTarget(targetString, ADR(sourceString1), ADR(sourceString2), ADR(sourceString3), ADR(sourceString4), ADR(sourceString5), ADR(sourceString6), ADR(sourceString7), ADR(sourceString8), ADR(sourceString9), ADR(sourceString10));]]></ST>
      </Implementation>
    </Method>
    <Method Name="TryCreateSplitter" Id="{527506d6-1420-4056-93a4-b1b0eacd1e2d}" FolderPath="Split\">
      <Declaration><![CDATA[METHOD TryCreateSplitter : BOOL
VAR_INPUT
	sourceString : ANY_STRING;
	separator : WSTRING(255);
	maxCount : UDINT := 0;
	trimEntries : BOOL := FALSE;
	removeEmptyEntries : BOOL := FALSE;
END_VAR
VAR_OUTPUT
	splitter : WideStringSplittingEnumerator;
END_VAR
VAR
	stringTypeNotSupportedException : NotSupportedException;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF sourceString.TypeClass <> __SYSTEM.TYPE_CLASS.TYPE_WSTRING THEN
	RETURN;
END_IF

splitter.Initialize(sourceString.pValue, separator, maxCount, trimEntries, removeEmptyEntries);

TryCreateSplitter := TRUE;]]></ST>
      </Implementation>
    </Method>
    <Method Name="TryReplaceInSource" Id="{4174b626-3bd8-4d6b-815f-679d9353671f}" FolderPath="Replace\">
      <Declaration><![CDATA[METHOD TryReplaceInSource : BOOL
VAR_INPUT
	sourceString : ANY_STRING;
END_VAR
VAR_IN_OUT CONSTANT
	oldSubstring : WSTRING;
	newSubstring : WSTRING;
END_VAR
VAR_INPUT
	maxReplacements : UINT := 0;
	skipReplacements : UINT := 0;
END_VAR
VAR_OUTPUT
	replacementCount : UINT;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF sourceString.TypeClass <> __SYSTEM.TYPE_CLASS.TYPE_WSTRING THEN
	RETURN;
END_IF

TryReplaceInSource := InternalReplaceInSource(sourceString, ADR(oldSubstring), ADR(newSubstring), maxReplacements, skipReplacements, replacementCount => replacementCount);]]></ST>
      </Implementation>
    </Method>
    <Method Name="TryReplaceInSource255" Id="{79d89556-eff6-43ae-9899-d26b8dfe9a56}" FolderPath="Replace\">
      <Declaration><![CDATA[METHOD TryReplaceInSource255 : BOOL
VAR_INPUT
	sourceString : ANY_STRING;
	oldSubstring : WSTRING(255);
	newSubstring : WSTRING(255);
	maxReplacements : UINT := 0;
	skipReplacements : UINT := 0;
END_VAR
VAR_OUTPUT
	replacementCount : UINT;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF sourceString.TypeClass <> __SYSTEM.TYPE_CLASS.TYPE_WSTRING THEN
	RETURN;
END_IF

TryReplaceInSource255 := InternalReplaceInSource(sourceString, ADR(oldSubstring), ADR(newSubstring), maxReplacements, skipReplacements, replacementCount => replacementCount);]]></ST>
      </Implementation>
    </Method>
  </POU>
</TcPlcObject>