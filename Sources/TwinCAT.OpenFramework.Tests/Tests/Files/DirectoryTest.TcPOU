<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4026.13">
  <POU Name="DirectoryTest" Id="{9e209233-70f2-4364-8c25-0eebe43c08be}" SpecialFunc="None">
    <Declaration><![CDATA[(*

.. <legal notes>

legal notes
=================
| SPDX-FileCopyrightText: © 2025 Oleksandr Tiutyk <trofimich@gmail.com>
| SPDX-License-Identifier: LGPL-3.0-only
| For details check: LGPL-3.0-only_

.. _LGPL-3.0-only: https://www.gnu.org/licenses/lgpl-3.0.en.html

.. </legal notes>

*)

FUNCTION_BLOCK DirectoryTest EXTENDS TcUnit.FB_TestSuite
VAR
	_ClassName : STRING := __POUNAME();
	
	_CheckDirectoryExists : TOF_FileSystem.CheckDirectoryExists;	
	_CreateDirectory : TOF_FileSystem.CreateDirectory;

	_CheckInvalidDirectoryExists : TOF_FileSystem.CheckDirectoryExists;	
END_VAR]]></Declaration>
    <Implementation>
      <ST><![CDATA[PathValidation();
CheckThenCreateDirectory();
CheckInvalidDirectory();]]></ST>
    </Implementation>
    <Method Name="CheckInvalidDirectory" Id="{5b90f0a8-63c9-4de6-954b-71a101a6f473}">
      <Declaration><![CDATA[METHOD CheckInvalidDirectory
VAR CONSTANT
	PATH : STRING(255) := 'W:\abcde12345';
END_VAR
VAR_INST
	initialized : BOOL;
END_VAR
VAR
	exceptionCode : __SYSTEM.ExceptionCode;

	caughtException : TOF_Core.IException;
	
	errorMessage : TOF_Core.ERROR_MESSAGE;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[TEST(__POUNAME());

IF NOT initialized THEN
	initialized := TRUE;

	_CheckInvalidDirectoryExists.DirectoryPath := PATH;

	_CheckInvalidDirectoryExists.Start();
ELSE
	_CheckInvalidDirectoryExists.Execute();
END_IF

IF NOT _CheckInvalidDirectoryExists.Busy THEN
	IF _CheckInvalidDirectoryExists.DirectoryExists THEN
		AssertTrue(FALSE, StringHelper.ConcatStrings255('Found not existing directory "', PATH, '"'));			
	END_IF
	
	TEST_FINISHED();
END_IF]]></ST>
      </Implementation>
    </Method>
    <Method Name="CheckThenCreateDirectory" Id="{5bc73480-957c-4dd7-ad6d-100d5d15a788}">
      <Declaration><![CDATA[METHOD CheckThenCreateDirectory
VAR CONSTANT
	PATH : STRING(255) := 'C:\TwinCatFiles';
END_VAR
VAR_INST
	initialized : BOOL;
	directoryChecking : BOOL;
END_VAR
VAR
	exceptionCode : __SYSTEM.ExceptionCode;

	caughtException : TOF_Core.IException;
	
	errorMessage : TOF_Core.ERROR_MESSAGE;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[TEST(__POUNAME());

__TRY
	IF NOT initialized THEN
		initialized := TRUE;
		
		directoryChecking := TRUE;
	
		_CheckDirectoryExists.DirectoryPath := PATH;
		
		_CreateDirectory.DirectoryPath := PATH;
	
		_CheckDirectoryExists.Start();
	ELSE
		IF directoryChecking THEN
			_CheckDirectoryExists.Execute();
		ELSE
			_CreateDirectory.Execute();		
		END_IF
	END_IF
	
	IF directoryChecking THEN
		IF NOT _CheckDirectoryExists.Busy THEN
			IF _CheckDirectoryExists.Done THEN
				IF _CheckDirectoryExists.DirectoryExists THEN
					TEST_FINISHED();
				ELSE
					directoryChecking := FALSE;
					_CreateDirectory.Start();
				END_IF
			ELSE
				AssertTrue(FALSE, 'CheckDirectoryExists wrong result');
				TEST_FINISHED();
			END_IF			
		END_IF
	ELSE
		IF NOT _CreateDirectory.Busy THEN
			IF NOT _CheckDirectoryExists.Done THEN;
				AssertTrue(FALSE, 'CreateDirectory error');
			END_IF
			
			TEST_FINISHED();
		END_IF
	END_IF
__CATCH(exceptionCode)
	caughtException := TOF_Core.ExceptionManager.GetLastException(exceptionCode);
	
	errorMessage := caughtException.GetMessage();
	
	AssertTrue(FALSE, WSTRING_TO_STRING(errorMessage));
	
	TEST_FINISHED();	
__ENDTRY]]></ST>
      </Implementation>
    </Method>
    <Property Name="ClassName" Id="{b781c019-1c1b-484b-acec-96a5874d1a6b}">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
PROPERTY ClassName : STRING]]></Declaration>
      <Get Name="Get" Id="{d936d22f-afe8-4c1e-9761-f36cc99eb530}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[ClassName := _ClassName;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Property Name="NamespaceName" Id="{84d1f26a-3563-43b2-b392-59a3c3ab34fd}">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
PROPERTY NamespaceName : STRING]]></Declaration>
      <Get Name="Get" Id="{88651a57-c797-417c-8993-132cac33a90a}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[NamespaceName := CurrentNamespace.Name;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Method Name="PathValidation" Id="{984d2d35-d096-4947-a09f-1a7b87f42b95}">
      <Declaration><![CDATA[METHOD PathValidation
VAR CONSTANT
	VALID_PATH : STRING(255) := 'C:\Windows';
	INALID_PATH1 : STRING(255) := 'C:\123|456';
	INALID_PATH2 : STRING(255) := 'C:\Windows//Boot';
END_VAR
VAR
	validationResult : BOOL;
	errorMessage : WSTRING(255);
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[TEST(__POUNAME());

validationResult := TOF_FileSystem.PathHelper.IsValidDirectoryPath(VALID_PATH, errorMessage => errorMessage);

AssertEquals_BOOL(TRUE, validationResult, 'Valid directory path validated as invalid');

validationResult := TOF_FileSystem.PathHelper.IsValidDirectoryPath(INALID_PATH1, errorMessage => errorMessage);

AssertEquals_BOOL(FALSE, validationResult, 'Inalid directory path 1 validated as valid');

validationResult := TOF_FileSystem.PathHelper.IsValidDirectoryPath(INALID_PATH2, errorMessage => errorMessage);

AssertEquals_BOOL(FALSE, validationResult, 'Inalid directory path 2 validated as valid');
	
TEST_FINISHED();	]]></ST>
      </Implementation>
    </Method>
  </POU>
</TcPlcObject>