<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4026.13">
  <POU Name="FileContentManagerTest" Id="{88d7041b-0299-48dd-b459-48a4f85c2b50}" SpecialFunc="None">
    <Declaration><![CDATA[(*

.. <legal notes>

legal notes
=================
| SPDX-FileCopyrightText: © 2026 Oleksandr Tiutyk <trofimich@gmail.com>
| SPDX-License-Identifier: LGPL-3.0-only
| For details check: LGPL-3.0-only_

.. _LGPL-3.0-only: https://www.gnu.org/licenses/lgpl-3.0.en.html

.. </legal notes>

*)

FUNCTION_BLOCK FileContentManagerTest EXTENDS TcUnit.FB_TestSuite
VAR
	_ClassName : STRING := __POUNAME();

	_FilePath : STRING(255) := 'C:\TestFile.txt';
	
	_FileContentManager : TOF_FileSystem.FileContentManager;
	
	_WriteStringToFile1 : TOF_FileSystem.WriteString255ToFile;
	_WriteStringToFile2 : TOF_FileSystem.WriteString255ToFile;
	_SetPositionInFile : TOF_FileSystem.SetPositionInFile;
	_ReadStringFromFile : TOF_FileSystem.ReadString255FromFile;
END_VAR]]></Declaration>
    <Implementation>
      <ST><![CDATA[CreateTextFile();]]></ST>
    </Implementation>
    <Property Name="ClassName" Id="{15b21f35-d24f-4ab1-a702-91ec38a46309}">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
PROPERTY ClassName : STRING]]></Declaration>
      <Get Name="Get" Id="{91a3149f-5498-41eb-9a10-635d05b0e259}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[ClassName := _ClassName;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Method Name="CreateTextFile" Id="{3ee86f47-dc17-44e0-bf7e-f1687979763f}">
      <Declaration><![CDATA[METHOD CreateTextFile
VAR CONSTANT
	TEST_STRING1 : STRING := 'Test string...';
	TEST_STRING2 : STRING := 'Another test string...';
END_VAR
VAR_INST
	_Initialized : BOOL;
END_VAR
VAR
	exceptionCode : __SYSTEM.ExceptionCode;

	caughtException : TOF_Core.IException;
	
	errorMessage : TOF_Core.ERROR_MESSAGE;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[TEST(__POUNAME());

IF NOT _Initialized THEN
	_FileContentManager.FilePath := _FilePath;
	_FileContentManager.ModeFlags := Tc2_System.Global_Variables.FOPEN_MODEWRITE OR Tc2_System.Global_Variables.FOPEN_MODEPLUS OR Tc2_System.Global_Variables.FOPEN_MODETEXT;
	_FileContentManager.PathType := Tc2_System.E_OpenPath.PATH_GENERIC;
	
	_WriteStringToFile1.StringToWrite := TEST_STRING1;
	_WriteStringToFile2.StringToWrite := TEST_STRING2;
	
	_FileContentManager.Enqueue(_WriteStringToFile1);
	_FileContentManager.Enqueue(_WriteStringToFile2);
	_FileContentManager.Enqueue(_SetPositionInFile);
	_FileContentManager.Enqueue(_ReadStringFromFile);
END_IF

__TRY
	IF NOT _Initialized THEN
		_Initialized := TRUE;
	
		_FileContentManager.Start();
	ELSE
		_FileContentManager.Execute();
	END_IF	
	
	IF _FileContentManager.PendingTaskCount = 0 THEN
		AssertEquals_STRING(CONCAT(TEST_STRING1, TEST_STRING2), _ReadStringFromFile.ReadString, 'The written to file string differs from the read string');			
		
		TEST_FINISHED();
	END_IF
__CATCH(exceptionCode)
	caughtException := TOF_Core.ExceptionManager.GetLastException(exceptionCode);
	
	errorMessage := caughtException.GetMessage();
	
	AssertTrue(FALSE, WSTRING_TO_STRING(errorMessage));
	
	TEST_FINISHED();	
__ENDTRY]]></ST>
      </Implementation>
    </Method>
    <Property Name="NamespaceName" Id="{8fdb97a0-d902-494b-81c7-71229c516980}">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
PROPERTY NamespaceName : STRING]]></Declaration>
      <Get Name="Get" Id="{dd92adc3-305b-4a29-bbd8-71436c04b1b1}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[NamespaceName := CurrentNamespace.Name;]]></ST>
        </Implementation>
      </Get>
    </Property>
  </POU>
</TcPlcObject>