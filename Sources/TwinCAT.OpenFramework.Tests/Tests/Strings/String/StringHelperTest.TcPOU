<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4026.13">
  <POU Name="StringHelperTest" Id="{c2b9cac4-73fd-4d3d-8df7-b5beb6b26c4c}" SpecialFunc="None">
    <Declaration><![CDATA[(*

.. <legal notes>

legal notes
=================
| SPDX-FileCopyrightText: © 2026 Oleksandr Tiutyk <trofimich@gmail.com>
| SPDX-License-Identifier: LGPL-3.0-only
| For details check: LGPL-3.0-only_

.. _LGPL-3.0-only: https://www.gnu.org/licenses/lgpl-3.0.en.html

.. </legal notes>

*)

FUNCTION_BLOCK StringHelperTest EXTENDS TcUnit.FB_TestSuite
VAR
	_ClassName : STRING := __POUNAME();
END_VAR]]></Declaration>
    <Implementation>
      <ST><![CDATA[AppendToTarget();
ReplaceInSource();
Split();
Trim();]]></ST>
    </Implementation>
    <Method Name="AppendToTarget" Id="{dfc63422-169c-4318-97d7-f4972908ec69}">
      <Declaration><![CDATA[METHOD AppendToTarget
VAR
	testString : STRING;

	expectedString : STRING := 'Original string + first appended string + second appended string';
	
	stringToAppend_1 : STRING := 'first appended string';
	stringToAppend_2 : STRING := 'second appended string';
	plusString : STRING := ' + ';
	
	result : BOOL;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[TEST(__POUNAME());

testString := 'Original string';
StringHelper.AppendToTarget255(testString, ' + ', stringToAppend_1, ' + ', stringToAppend_2);
AssertEquals_STRING(expectedString, testString, 'Invalid StringHelper.AppendToTarget255 method result');

testString := 'Original string';
result := StringHelper.TryAppendToTarget255(testString, ' + ', stringToAppend_1, ' + ', stringToAppend_2);
AssertEquals_STRING(expectedString, testString, 'Invalid StringHelper.TryAppendToTarget255 method result');
AssertEquals_BOOL(result, TRUE, 'Invalid StringHelper.TryAppendToTarget255 method returned result');

result := StringHelper.TryAppendToTarget255(testString, ' + ', stringToAppend_1);
AssertEquals_BOOL(result, FALSE, 'Invalid StringHelper.TryAppendToTarget255 method returned result');

testString := 'Original string';
StringHelper.AppendToTarget(testString, plusString);
StringHelper.AppendToTarget(testString, stringToAppend_1);
StringHelper.AppendToTarget(testString, plusString);
StringHelper.AppendToTarget(testString, stringToAppend_2);
AssertEquals_STRING(expectedString, testString, 'Invalid StringHelper.AppendToTarget method result');

testString := 'Original string';
result := StringHelper.TryAppendToTarget(testString, plusString)
	AND StringHelper.TryAppendToTarget(testString, stringToAppend_1)
	AND StringHelper.TryAppendToTarget(testString, plusString)
	AND StringHelper.TryAppendToTarget(testString, stringToAppend_2);
AssertEquals_STRING(expectedString, testString, 'Invalid StringHelper.TryAppendToTarget method result');
AssertEquals_BOOL(result, TRUE, 'Invalid StringHelper.TryAppendToTarget method returned result');

result := StringHelper.TryAppendToTarget(testString, plusString)
	AND StringHelper.TryAppendToTarget(testString, stringToAppend_1);
AssertEquals_BOOL(result, FALSE, 'Invalid StringHelper.TryAppendToTarget method returned result');

TEST_FINISHED();]]></ST>
      </Implementation>
    </Method>
    <Property Name="ClassName" Id="{b8985102-dc15-41e1-ae23-7d62e6866a91}">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
PROPERTY ClassName : STRING]]></Declaration>
      <Get Name="Get" Id="{03ded19b-ca6c-4934-9ab2-2e75c95c0490}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[ClassName := _ClassName;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Property Name="NamespaceName" Id="{02163062-dd40-4429-a0a8-5c360f5da8fb}">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
PROPERTY NamespaceName : STRING]]></Declaration>
      <Get Name="Get" Id="{459ad87f-22cb-4069-bbe1-8915f5177a12}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[NamespaceName := CurrentNamespace.Name;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Method Name="ReplaceInSource" Id="{acab7dc3-475e-41e3-9ac6-f109a2619017}">
      <Declaration><![CDATA[METHOD ReplaceInSource
VAR
	testString : STRING(18);
	
	replacementCount : UINT;
	result : BOOL;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[TEST(__POUNAME());

testString := 'abcd_abcd_abcd';
StringHelper.ReplaceInSource(testString, 'bc', '0', replacementCount => replacementCount);
AssertEquals_STRING('a0d_a0d_a0d', testString, 'Invalid StringHelper.ReplaceInSource result string');
AssertEquals_UINT(3, replacementCount, 'Invalid StringHelper.ReplaceInSource replacingCount count');

testString := 'abcd_abcd_abcd';
StringHelper.ReplaceInSource(testString, 'bc', '123', replacementCount => replacementCount);
AssertEquals_STRING('a123d_a123d_a123d', testString, 'Invalid StringHelper.ReplaceInSource result string');
AssertEquals_UINT(3, replacementCount, 'Invalid StringHelper.ReplaceInSource replacingCount count');

testString := 'abcd_abcd_abcd';
result := StringHelper.TryReplaceInSource(testString, 'bc', '0', replacementCount => replacementCount);
AssertEquals_BOOL(TRUE, result, 'Invalid StringHelper.TryReplaceInSource result');
AssertEquals_STRING('a0d_a0d_a0d', testString, 'Invalid StringHelper.TryReplaceInSource result string');
AssertEquals_UINT(3, replacementCount, 'Invalid StringHelper.TryReplaceInSource replacingCount count');

testString := 'abcd_abcd_abcd';
result := StringHelper.TryReplaceInSource(testString, 'bc', '123', 1, 1, replacementCount => replacementCount);
AssertEquals_BOOL(TRUE, result, 'Invalid StringHelper.TryReplaceInSource result');
AssertEquals_STRING('abcd_a123d_abcd', testString, 'Invalid StringHelper.TryReplaceInSource result string');
AssertEquals_UINT(1, replacementCount, 'Invalid StringHelper.TryReplaceInSource replacingCount count');

testString := 'abcd_abcd_abcd';
result := StringHelper.TryReplaceInSource(testString, 'bc', '1234', replacementCount => replacementCount);
AssertEquals_BOOL(FALSE, result, 'Invalid StringHelper.TryReplaceInSource memory handling');

TEST_FINISHED();]]></ST>
      </Implementation>
    </Method>
    <Method Name="Split" Id="{afcb1a60-4677-4461-950d-5644dc25733b}">
      <Declaration><![CDATA[METHOD Split
VAR
	stringToSplit : STRING := '123 , , 456, 789 ,0';
	separator : STRING := ',';
	
	splitter : TOF_Core.StringSplittingEnumerator;
	currentEntry : STRING;
	i : BYTE;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[TEST(__POUNAME());

// simple splitting
splitter := TOF_Core.StringHelper.CreateSplitter(stringToSplit, separator);

i := 0;

WHILE splitter.MoveNext() DO
	currentEntry := splitter.CurrentEntry;
	
	CASE i OF
		0:
			AssertEquals_STRING('123 ', currentEntry, 'Invalid StringSplitter entry #0');			
		1:
			AssertEquals_STRING(' ', currentEntry, 'Invalid StringSplitter entry #3');
		2:
			AssertEquals_STRING(' 456', currentEntry, 'Invalid StringSplitter entry #1');
		3:
			AssertEquals_STRING(' 789 ', currentEntry, 'Invalid StringSplitter entry #2');
		4:
			AssertEquals_STRING('0', currentEntry, 'Invalid StringSplitter entry #4');
	END_CASE	
	
	i := i + 1;
END_WHILE

// splitting with MaxCount, trimming and empty string removing 
splitter := TOF_Core.StringHelper.CreateSplitter(stringToSplit, separator, 3, TRUE, TRUE);

i := 0;

WHILE splitter.MoveNext() DO
	currentEntry := splitter.CurrentEntry;
	
	CASE i OF
		0:
			AssertEquals_STRING('123', currentEntry, 'Invalid StringSplitter entry #0');			
		1:
			AssertEquals_STRING('456', currentEntry, 'Invalid StringSplitter entry #1');
		2:
			AssertEquals_STRING('789', currentEntry, 'Invalid StringSplitter entry #2');
		ELSE
			AssertFalse(i > 3, 'Invalid StringSplitter MaxCount handling');
	END_CASE	
	
	i := i + 1;
END_WHILE

TEST_FINISHED();]]></ST>
      </Implementation>
    </Method>
    <Method Name="Trim" Id="{4efc06d8-47aa-41ec-86fa-bcfd7ab44822}">
      <Declaration><![CDATA[METHOD Trim
VAR
	trimString : STRING;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[TEST(__POUNAME());

trimString := '  123   ';
TOF_Core.StringHelper.TrimChar(trimString, trimMode := TRIM_MODE.LEFT_AND_RIGHT);
AssertEquals_STRING('123', trimString, 'Invalid string trimming result (TRIM_MODE = LEFT_AND_RIGHT)');			

trimString := '  123   ';
TOF_Core.StringHelper.TrimChar(trimString, trimMode := TRIM_MODE.LEFT);
AssertEquals_STRING('123   ', trimString, 'Invalid string trimming result (TRIM_MODE = LEFT)');			

trimString := '  123   ';
TOF_Core.StringHelper.TrimChar(trimString, trimMode := TRIM_MODE.RIGHT);
AssertEquals_STRING('  123', trimString, 'Invalid string trimming result (TRIM_MODE = RIGHT)');			

TEST_FINISHED();]]></ST>
      </Implementation>
    </Method>
  </POU>
</TcPlcObject>