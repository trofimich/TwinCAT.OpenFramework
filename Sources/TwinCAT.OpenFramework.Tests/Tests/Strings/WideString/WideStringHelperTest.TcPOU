<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4026.17">
  <POU Name="WideStringHelperTest" Id="{e08217c0-d613-42f5-a56c-21332ab5cf57}" SpecialFunc="None">
    <Declaration><![CDATA[(*

.. <legal notes>

legal notes
=================
| SPDX-FileCopyrightText: © 2026 Oleksandr Tiutyk <trofimich@gmail.com>
| SPDX-License-Identifier: LGPL-3.0-only
| For details check: LGPL-3.0-only_

.. _LGPL-3.0-only: https://www.gnu.org/licenses/lgpl-3.0.en.html

.. </legal notes>

*)

FUNCTION_BLOCK WideStringHelperTest EXTENDS TcUnit.FB_TestSuite
VAR
	_ClassName : STRING := __POUNAME();
END_VAR]]></Declaration>
    <Implementation>
      <ST><![CDATA[AppendToTarget();
ReplaceInSource();
Split();
Trim();]]></ST>
    </Implementation>
    <Method Name="AppendToTarget" Id="{88a48147-6775-40ec-9463-87ab1f92288c}">
      <Declaration><![CDATA[METHOD AppendToTarget
VAR
	testString : WSTRING;

	expectedString : WSTRING := "Original string + first appended string + second appended string";
	
	stringToAppend_1 : WSTRING := "first appended string";
	stringToAppend_2 : WSTRING := "second appended string";
	plusString : WSTRING := " + ";
	
	result : BOOL;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[TEST(__POUNAME());

testString := "Original string";
WideStrings.AppendToTarget255(testString, " + ", stringToAppend_1, " + ", stringToAppend_2);
AssertEquals_WSTRING(expectedString, testString, 'Invalid WideStrings.AppendToTarget255 method result');

testString := "Original string";
result := WideStrings.TryAppendToTarget255(testString, " + ", stringToAppend_1, " + ", stringToAppend_2);
AssertEquals_WSTRING(expectedString, testString, 'Invalid WideStrings.TryAppendToTarget255 method result');
AssertEquals_BOOL(result, TRUE, 'Invalid WideStrings.TryAppendToTarget255 method returned result');

result := WideStrings.TryAppendToTarget255(testString, " + ", stringToAppend_1);
AssertEquals_BOOL(result, FALSE, 'Invalid WideStrings.TryAppendToTarget255 method returned result');

testString := "Original string";
WideStrings.AppendToTarget(testString, plusString);
WideStrings.AppendToTarget(testString, stringToAppend_1);
WideStrings.AppendToTarget(testString, plusString);
WideStrings.AppendToTarget(testString, stringToAppend_2);
AssertEquals_WSTRING(expectedString, testString, 'Invalid WideStrings.AppendToTarget method result');

testString := "Original string";
result := WideStrings.TryAppendToTarget(testString, plusString)
	AND WideStrings.TryAppendToTarget(testString, stringToAppend_1)
	AND WideStrings.TryAppendToTarget(testString, plusString)
	AND WideStrings.TryAppendToTarget(testString, stringToAppend_2);
AssertEquals_WSTRING(expectedString, testString, 'Invalid WideStrings.TryAppendToTarget method result');
AssertEquals_BOOL(result, TRUE, 'Invalid WideStrings.TryAppendToTarget METHOD returned result');

result := WideStrings.TryAppendToTarget(testString, plusString)
	AND WideStrings.TryAppendToTarget(testString, stringToAppend_1);
AssertEquals_BOOL(result, FALSE, 'Invalid WideStrings.TryAppendToTarget method returned result');

TEST_FINISHED();]]></ST>
      </Implementation>
    </Method>
    <Property Name="ClassName" Id="{d08c00e3-4480-409e-af2c-b29fec1ed9d7}">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
PROPERTY ClassName : STRING]]></Declaration>
      <Get Name="Get" Id="{2a897220-4c8c-44b8-92e9-6f44af874c07}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[ClassName := _ClassName;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Property Name="NamespaceName" Id="{f4f5aa7a-86c5-40a5-836c-18725c9fbee1}">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
PROPERTY NamespaceName : STRING]]></Declaration>
      <Get Name="Get" Id="{0227b39f-abc8-45ee-8a3f-72373f229f87}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[NamespaceName := CurrentNamespace.Name;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Method Name="ReplaceInSource" Id="{c1fd0c57-57d3-4910-a593-eb3ffa46d556}">
      <Declaration><![CDATA[METHOD ReplaceInSource
VAR
	testString : WSTRING(18);
	
	replacementCount : UINT;
	result : BOOL;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[TEST(__POUNAME());

testString := "abcd_abcd_abcd";
WideStrings.ReplaceInSource(testString, "bc", "0", replacementCount => replacementCount);
AssertEquals_WSTRING("a0d_a0d_a0d", testString, 'Invalid WideStrings.ReplaceInSource result string');
AssertEquals_UINT(3, replacementCount, 'Invalid WideStrings.ReplaceInSource replacingCount count');

testString := "abcd_abcd_abcd";
WideStrings.ReplaceInSource(testString, "bc", "123", replacementCount => replacementCount);
AssertEquals_WSTRING("a123d_a123d_a123d", testString, 'Invalid WideStrings.ReplaceInSource result string');
AssertEquals_UINT(3, replacementCount, 'Invalid WideStrings.ReplaceInSource replacingCount count');

testString := "abcd_abcd_abcd";
result := WideStrings.TryReplaceInSource(testString, "bc", "0", replacementCount => replacementCount);
AssertEquals_BOOL(TRUE, result, 'Invalid WideStrings.TryReplaceInSource result');
AssertEquals_WSTRING("a0d_a0d_a0d", testString, 'Invalid WideStrings.TryReplaceInSource result string');
AssertEquals_UINT(3, replacementCount, 'Invalid WideStrings.TryReplaceInSource replacingCount count');

testString := "abcd_abcd_abcd";
result := WideStrings.TryReplaceInSource(testString, "bc", "123", 1, 1, replacementCount => replacementCount);
AssertEquals_BOOL(TRUE, result, 'Invalid WideStrings.TryReplaceInSource result');
AssertEquals_WSTRING("abcd_a123d_abcd", testString, 'Invalid WideStrings.TryReplaceInSource result string');
AssertEquals_UINT(1, replacementCount, 'Invalid WideStrings.TryReplaceInSource replacingCount count');

testString := "abcd_abcd_abcd";
result := WideStrings.TryReplaceInSource(testString, "bc", "1234", replacementCount => replacementCount);
AssertEquals_BOOL(FALSE, result, 'Invalid WideStrings.TryReplaceInSource memory handling');

TEST_FINISHED();]]></ST>
      </Implementation>
    </Method>
    <Method Name="Split" Id="{71d1bd73-69e5-47d2-9c99-6ebd1c10e55b}">
      <Declaration><![CDATA[METHOD Split
VAR
	stringToSplit : WSTRING := "123 , 456, 789 ,,0";
	separator : WSTRING := ",";
	
	splitter : TOF_Core.WideStringSplittingEnumerator;
	currentEntry : WSTRING;
	i : BYTE;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[TEST(__POUNAME());

// simple splitting
splitter := TOF_Core.WideStrings.CreateSplitter(stringToSplit, separator);

i := 0;

WHILE splitter.MoveNext() DO
	currentEntry := splitter.CurrentEntry;
	
	CASE i OF
		0:
			AssertEquals_WSTRING("123 ", currentEntry, 'Invalid WideStringSplitter entry #0');			
		1:
			AssertEquals_WSTRING(" 456", currentEntry, 'Invalid WideStringSplitter entry #1');
		2:
			AssertEquals_WSTRING(" 789 ", currentEntry, 'Invalid WideStringSplitter entry #2');
		3:
			AssertEquals_WSTRING("", currentEntry, 'Invalid WideStringSplitter entry #3');
		4:
			AssertEquals_WSTRING("0", currentEntry, 'Invalid WideStringSplitter entry #4');
	END_CASE	
	
	i := i + 1;
END_WHILE

// splitting with MaxCount, trimming and empty string removing 
splitter := TOF_Core.WideStrings.CreateSplitter(stringToSplit, separator, 3, TRUE, TRUE);

i := 0;

WHILE splitter.MoveNext() DO
	currentEntry := splitter.CurrentEntry;
	
	CASE i OF
		0:
			AssertEquals_WSTRING("123", currentEntry, 'Invalid WideStringSplitter entry #0');			
		1:
			AssertEquals_WSTRING("456", currentEntry, 'Invalid WideStringSplitter entry #1');
		2:
			AssertEquals_WSTRING("789", currentEntry, 'Invalid WideStringSplitter entry #2');
		ELSE
			AssertFalse(i > 3, 'Invalid WideStringSplitter MaxCount handling');
	END_CASE	
	
	i := i + 1;
END_WHILE

TEST_FINISHED();]]></ST>
      </Implementation>
    </Method>
    <Method Name="Trim" Id="{a9bd8a2b-d5f3-4fc9-867e-a9c2e4a29101}">
      <Declaration><![CDATA[METHOD Trim
VAR
	trimString : WSTRING;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[TEST(__POUNAME());

trimString := "  123   ";
TOF_Core.WideStrings.TrimChar(trimString, trimMode := TRIM_MODE.LEFT_AND_RIGHT);
AssertEquals_WSTRING("123", trimString, 'Invalid wide string trimming result (TRIM_MODE = LEFT_AND_RIGHT)');			

trimString := "  123   ";
TOF_Core.WideStrings.TrimChar(trimString, trimMode := TRIM_MODE.LEFT);
AssertEquals_WSTRING("123   ", trimString, 'Invalid wide string trimming result (TRIM_MODE = LEFT)');			

trimString := "  123   ";
TOF_Core.WideStrings.TrimChar(trimString, trimMode := TRIM_MODE.RIGHT);
AssertEquals_WSTRING("  123", trimString, 'Invalid wide string trimming result (TRIM_MODE = RIGHT)');			

TEST_FINISHED();]]></ST>
      </Implementation>
    </Method>
  </POU>
</TcPlcObject>