<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1">
  <POU Name="TaskSequence" Id="{c84a1c13-6ab4-4bd3-943b-ca59b71cb2eb}" SpecialFunc="None">
    <Declaration><![CDATA[(*

.. <legal notes>

legal notes
=================
| SPDX-FileCopyrightText: © 2025 Oleksandr Tiutyk <trofimich@gmail.com>
| SPDX-License-Identifier: LGPL-3.0-only
| For details check: LGPL-3.0-only_

.. _LGPL-3.0-only: https://www.gnu.org/licenses/lgpl-3.0.en.html

.. </legal notes>

*)

{attribute 'no_explicit_call' := 'do not call this function block directly'} 
FUNCTION_BLOCK TaskSequence EXTENDS Task IMPLEMENTS ITaskSequence
VAR
	_ClassName : STRING := __POUNAME();

	_Tasks : TOF_Collections.List(0);
	
	_TaskSwitchingHandler : ITaskSwitchingHandler;
	
	_CurrentTaskIndex : DINT := -1;
END_VAR]]></Declaration>
    <Implementation>
      <ST><![CDATA[]]></ST>
    </Implementation>
    <Folder Name="Overriden members" Id="{204cba1d-9fdf-40c4-8089-a1dc02e05554}" />
    <Folder Name="Service members" Id="{c9f64467-fc60-4085-b0a7-19143674f53b}" />
    <Method Name="AddTask" Id="{1f869055-a3f9-4ad5-bf83-e32f5bd60feb}">
      <Declaration><![CDATA[METHOD AddTask : DINT
VAR_INPUT
	task : ITask;
END_VAR
VAR
	taskTypeNotSpecifiedException : TOF_Core.ArgumentNotSpecifiedException;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF task = 0 THEN
	taskTypeNotSpecifiedException.Throw(Namespacename, ClassName, __POUNAME(), __POSITION(), 'task');
END_IF

_Tasks.AppendGeneric(GenericValueFactory.FromIObjectReference(task), index => AddTask);]]></ST>
      </Implementation>
    </Method>
    <Property Name="ClassName" Id="{aeebd0a5-cbe2-4ed0-a31a-379cffd13f4e}">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
PROPERTY ClassName : STRING]]></Declaration>
      <Get Name="Get" Id="{901c7376-eeaa-47e5-ba82-a08c8cc60015}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[ClassName := _ClassName;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Method Name="Clear" Id="{51a2295d-8daa-4f1b-9e95-95f27e4193ef}">
      <Declaration><![CDATA[METHOD FINAL Clear]]></Declaration>
      <Implementation>
        <ST><![CDATA[_Tasks.Clear();]]></ST>
      </Implementation>
    </Method>
    <Method Name="getTaskByIndex" Id="{f22772fd-8247-402b-9d6f-aba19107bb0f}" FolderPath="Service members\">
      <Declaration><![CDATA[METHOD getTaskByIndex : ITask
VAR_INPUT
	taskIndex : DINT;
END_VAR
VAR
	taskInterface : TOF_Core.IObject;
	
	taskIndexOutOfRangeException : TOF_Core.ArgumentOutOfRangeException;
	
	toITaskConversionNotSupportedException : TOF_Core.StandardException;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF taskIndex < 0 OR_ELSE taskIndex >= _Tasks.Count THEN
	taskIndexOutOfRangeException.Throw(NamespaceName, ClassName, __POUNAME(), __POSITION(), 'taskIndex', taskIndex);
END_IF

taskInterface := _Tasks.Get(taskIndex).AsIObject();

IF NOT __QUERYINTERFACE(taskInterface, getTaskByIndex) THEN
	toITaskConversionNotSupportedException.Throw(NamespaceName, ClassName, __POUNAME(), __POSITION(), "Can't convert task from _Tasks collection to ITask type");
END_IF]]></ST>
      </Implementation>
    </Method>
    <Property Name="NamespaceName" Id="{8ed36986-59db-4fde-89c4-7a7d3055a87c}">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
PROPERTY NamespaceName : STRING]]></Declaration>
      <Get Name="Get" Id="{b92871ae-cf43-4ccf-b930-5c2aeae7a66a}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[NamespaceName := CurrentNamespace.Name;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Method Name="OnCancel" Id="{b597c48a-4efe-4e7a-a9b9-9f7cb18fd13f}" FolderPath="Overriden members\">
      <Declaration><![CDATA[METHOD PROTECTED OnCancel
VAR
	currentTask : ITask;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF _CurrentTaskIndex > 0 AND_THEN _CurrentTaskIndex < _Tasks.Count THEN
	IF __QUERYINTERFACE(_Tasks.Get(_CurrentTaskIndex).AsIObject(), currentTask) THEN
		currentTask.Cancel();		
	END_IF
END_IF]]></ST>
      </Implementation>
    </Method>
    <Method Name="OnRunning" Id="{6724b204-17ef-4972-b8a2-0e170858adcd}" FolderPath="Overriden members\">
      <Declaration><![CDATA[METHOD PROTECTED OnRunning 
VAR_INPUT
	startRequest : BOOL;
END_VAR
VAR_OUTPUT
	state : TASK_RUNNING_STATE;
	abortReason : WSTRING(255);
END_VAR
VAR
	currentTask, nextTask : ITask;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF startRequest THEN	
	TOF_Core.LogManager.TryLogMessage("Task sequence starting", LOG_CATEGORY.INFORMATION, "TaskSequence", "TaskSequence.Starting");

	IF _Tasks.Count > 0 THEN
		_CurrentTaskIndex := 0;
		
		currentTask := getTaskByIndex(_CurrentTaskIndex);
		
		IF _TaskSwitchingHandler <> 0 THEN
			_TaskSwitchingHandler.OnNextTask(0, currentTask);
		END_IF
		
		currentTask.Start();
	ELSE
		state := TASK_RUNNING_STATE.DONE;
		RETURN;	
	END_IF
ELSE
	currentTask := getTaskByIndex(_CurrentTaskIndex);
	
	currentTask.Execute();	
END_IF

CHECK_TASK_COMPLETION:

IF currentTask.Aborted THEN
	state := TASK_RUNNING_STATE.ABORTED;
	abortReason := WideStringHelper.ConcatStrings255("Task '", currentTask.Name, "' aborted in queue");	

	TOF_Core.LogManager.TryLogMessage(abortReason, LOG_CATEGORY.INFORMATION, "TaskQueue", "TaskQueue.Task", "TaskQueue.Task.Aborted");	
ELSIF currentTask.Done THEN
	TOF_Core.LogManager.TryLogMessage(WideStringHelper.ConcatStrings255("Task '", currentTask.Name, "' done in sequence"), LOG_CATEGORY.INFORMATION, "TaskSequence", "TaskSequence.Task", "TaskSequence.Task.Done");

	IF _CurrentTaskIndex + 1 < _Tasks.Count THEN
		_CurrentTaskIndex := _CurrentTaskIndex + 1;
		
		nextTask := getTaskByIndex(_CurrentTaskIndex);
		
		IF _TaskSwitchingHandler <> 0 THEN			
			_TaskSwitchingHandler.OnNextTask(currentTask, nextTask);
		END_IF
		
		currentTask := nextTask;
		
		currentTask.Start();
		
		JMP CHECK_TASK_COMPLETION;
	ELSE
		IF _TaskSwitchingHandler <> 0 THEN			
			_TaskSwitchingHandler.OnNextTask(currentTask, 0);
		END_IF

		state := TASK_RUNNING_STATE.DONE;

		TOF_Core.LogManager.TryLogMessage("Task sequence done", LOG_CATEGORY.INFORMATION, "TaskSequence", "TaskSequence.Done");
	END_IF 
END_IF]]></ST>
      </Implementation>
    </Method>
    <Property Name="Size" Id="{bc4b28bd-b57b-4eac-b945-ba350b194deb}">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
PROPERTY Size : ULINT]]></Declaration>
      <Get Name="Get" Id="{4802072c-3178-4447-aca5-73289a8ee14f}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[Size := XSIZEOF(THIS^);]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Property Name="TaskCount" Id="{ccd93825-a328-4178-8d23-ed41abf354e7}">
      <Declaration><![CDATA[PROPERTY FINAL TaskCount : DINT]]></Declaration>
      <Get Name="Get" Id="{a942dfff-e13d-467c-9273-6c3e20d2817e}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[TaskCount := _Tasks.Count;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Property Name="TaskSwitchingHandler" Id="{44668380-10ad-4359-9b30-698a0847830d}">
      <Declaration><![CDATA[PROPERTY FINAL TaskSwitchingHandler : ITaskSwitchingHandler]]></Declaration>
      <Get Name="Get" Id="{9e9ca59d-88e1-4e63-ab4d-e8f875cbfc7e}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[TaskSwitchingHandler := _TaskSwitchingHandler;]]></ST>
        </Implementation>
      </Get>
      <Set Name="Set" Id="{b99cb2bb-3bc8-4a28-aaae-c07b9e4cbaad}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[_TaskSwitchingHandler := TaskSwitchingHandler;]]></ST>
        </Implementation>
      </Set>
    </Property>
  </POU>
</TcPlcObject>