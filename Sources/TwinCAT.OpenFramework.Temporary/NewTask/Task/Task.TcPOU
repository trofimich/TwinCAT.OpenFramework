<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1">
  <POU Name="Task" Id="{6245b891-dd47-48e1-9889-f39682fa0f21}" SpecialFunc="None">
    <Declaration><![CDATA[(*

.. <legal notes>

legal notes
=================
| SPDX-FileCopyrightText: © 2025 Oleksandr Tiutyk <trofimich@gmail.com>
| SPDX-License-Identifier: LGPL-3.0-only
| For details check: LGPL-3.0-only_

.. _LGPL-3.0-only: https://www.gnu.org/licenses/lgpl-3.0.en.html

.. </legal notes>

*)

{attribute 'no_explicit_call' := 'do not call this function block directly'} 
FUNCTION_BLOCK ABSTRACT Task EXTENDS TOF_Core.Object IMPLEMENTS ITask
VAR
	_Name : WSTRING;
	
	_State : TASK_STATE;
	_PreviousState : TASK_STATE;
	
	_TransitionState : TASK_TRANSITION_STATE;
	
	_AbortReason : WSTRING(255);
	_ErrorMessage : ERROR_MESSAGE;

	_Subscribers : TOF_Collections.UniqueSet(0);
END_VAR]]></Declaration>
    <Implementation>
      <ST><![CDATA[]]></ST>
    </Implementation>
    <Folder Name="Members to override" Id="{f8db3690-b070-4bb5-bca0-cbb179e9b9bf}" />
    <Folder Name="Service members" Id="{656a3018-e8d1-419a-8485-9105635c8d24}" />
    <Property Name="AbortReason" Id="{d4e5d12f-0cb4-4bef-be53-2d031e72c50c}">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
PROPERTY FINAL AbortReason : REFERENCE TO WSTRING(255)]]></Declaration>
      <Get Name="Get" Id="{4ef99c87-cf1a-4d10-8e18-c31cea038f1c}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[AbortReason REF= _AbortReason;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Method Name="completeTransition" Id="{9ce156a7-1b05-432f-85fb-0435dbd4c1c6}" FolderPath="Service members\">
      <Declaration><![CDATA[METHOD PRIVATE completeTransition]]></Declaration>
      <Implementation>
        <ST><![CDATA[LogManager.TryLogMessage(
	WideStringHelper.ConcatStrings255("Task '", Name, "' state '", TO_WSTRING(_State), "' transition complete"), 
	LOG_CATEGORY.SUCCESS, 
	TO_WSTRING(ClassName), 
	"Task", 
	WideStringHelper.ConcatStrings255("Task", TO_WSTRING(_State), ".EndTransition")
);

_TransitionState := TASK_TRANSITION_STATE.COMPETE;]]></ST>
      </Implementation>
    </Method>
    <Property Name="ErrorMessage" Id="{89da0f2b-d4e9-46c0-b057-1da215fd6232}">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
PROPERTY FINAL ErrorMessage : REFERENCE TO ERROR_MESSAGE]]></Declaration>
      <Get Name="Get" Id="{037ff6fc-f498-411a-9674-0d1567143338}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[ErrorMessage REF= _ErrorMessage;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Method Name="Execute" Id="{12078d04-bc7d-4669-9e03-3fb65613f3c9}">
      <Declaration><![CDATA[METHOD FINAL Execute 
VAR
	processingResult : TASK_PROCESSING_RESULT;
	abortReason : WSTRING(255);

	done : BOOL;
	
	exceptionCode : __SYSTEM.ExceptionCode;
	
	exception : IException;	
	
	errorMessage : ERROR_MESSAGE;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[_PreviousState := _State;

__TRY
	CASE _State OF
		TASK_STATE.START:
			OnStart(_TransitionState = TASK_TRANSITION_STATE.START, processingResult => processingResult, abortReason => abortReason);			

			CASE processingResult OF
				TASK_PROCESSING_RESULT.IN_PROGRESS:
					processTransition();
				
				TASK_PROCESSING_RESULT.DONE:
					completeTransition();		
					setRunState();	
				
				TASK_PROCESSING_RESULT.ABORTED:
					setAbortState(abortReason);
			END_CASE
		
		TASK_STATE.RUN:
			OnStart(_TransitionState = TASK_TRANSITION_STATE.START, processingResult => processingResult, abortReason => abortReason);			

			CASE processingResult OF
				TASK_PROCESSING_RESULT.IN_PROGRESS:
					processTransition();
				
				TASK_PROCESSING_RESULT.DONE:
					completeTransition();		
					setCompleteState();	
				
				TASK_PROCESSING_RESULT.ABORTED:
					setAbortState(abortReason);
			END_CASE
		
		TASK_STATE.COMPETE:
			OnComplete(_TransitionState = TASK_TRANSITION_STATE.START, done => done);
			
			IF done THEN
				completeTransition();		
			ELSE				
				processTransition();
			END_IF			
		
		TASK_STATE.CANCEL:
			super^.OnCancel(_TransitionState = TASK_TRANSITION_STATE.START, done => done);
			
			IF done THEN
				completeTransition();		
			ELSE				
				processTransition();
			END_IF			
		
		TASK_STATE.ABORT:
			OnAbort(_TransitionState = TASK_TRANSITION_STATE.START, done => done);
			
			IF done THEN
				completeTransition();		
			ELSE				
				processTransition();
			END_IF			
		
		TASK_STATE.FAULT:
			OnFault(_TransitionState = TASK_TRANSITION_STATE.START, done => done);
			
			IF done THEN
				completeTransition();		
			ELSE				
				processTransition();
			END_IF			
	END_CASE
__CATCH(exceptionCode)
	setFaultState(exceptionCode);
__ENDTRY]]></ST>
      </Implementation>
    </Method>
    <Method Name="HandleException" Id="{80f84259-ac32-4670-8146-e752e93af763}" FolderPath="Members to override\">
      <Declaration><![CDATA[METHOD PROTECTED HandleException
VAR_INPUT
	exception : TOF_Core.IException;
END_VAR
VAR_OUTPUT
	handled : BOOL;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[]]></ST>
      </Implementation>
    </Method>
    <Property Name="InTransition" Id="{bf6a3e90-189a-4bba-b0e9-8167ddbd2dab}">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
PROPERTY FINAL InTransition : BOOL
]]></Declaration>
      <Get Name="Get" Id="{48ac9f22-cf84-4f94-b82e-04dbd726cd17}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[InTransition := _TransitionState = TASK_TRANSITION_STATE.START OR_ELSE _TransitionState = TASK_TRANSITION_STATE.IN_PROGRES;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Property Name="Name" Id="{6610eaa6-946c-4a60-bb63-40fcf9be8ee5}">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
PROPERTY FINAL Name : WSTRING]]></Declaration>
      <Get Name="Get" Id="{6dea95ae-cc79-492b-8cf4-9df3bbdf69e3}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[Name := SEL(_Name = "", _Name, SUPER^.Name);]]></ST>
        </Implementation>
      </Get>
      <Set Name="Set" Id="{94ebd014-5d63-4146-9316-baa8b226f094}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[_Name := Name;]]></ST>
        </Implementation>
      </Set>
    </Property>
    <Method Name="OnAbort" Id="{70867ebf-1caa-4ce6-9fc8-91fe4b50da16}" FolderPath="Members to override\">
      <Declaration><![CDATA[METHOD PROTECTED OnAbort
VAR_INPUT
	startRequest : BOOL;
END_VAR
VAR_OUTPUT
	done : BOOL;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[done := TRUE;]]></ST>
      </Implementation>
    </Method>
    <Method Name="OnCancel" Id="{4bd8e5b5-cac7-4b00-be8d-5f31c96552c8}" FolderPath="Members to override\">
      <Declaration><![CDATA[METHOD PROTECTED OnCancel
VAR_INPUT
	startRequest : BOOL;
END_VAR
VAR_OUTPUT
	done : BOOL;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[done := TRUE;]]></ST>
      </Implementation>
    </Method>
    <Method Name="OnComplete" Id="{894de31c-7f3b-404b-bb95-e877fc5549a8}" FolderPath="Members to override\">
      <Declaration><![CDATA[METHOD PROTECTED OnComplete
VAR_INPUT
	startRequest : BOOL;
END_VAR
VAR_OUTPUT
	done : BOOL;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[done := TRUE;]]></ST>
      </Implementation>
    </Method>
    <Method Name="OnFault" Id="{53fc1cad-83ce-4baf-b44d-cf14d16daffb}" FolderPath="Members to override\">
      <Declaration><![CDATA[METHOD PROTECTED OnFault 
VAR_INPUT
	startRequest : BOOL;
END_VAR
VAR_OUTPUT
	done : BOOL;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[done := TRUE;]]></ST>
      </Implementation>
    </Method>
    <Method Name="OnReset" Id="{d56bfe69-dd13-41b2-bb70-e66bcd2fd211}" FolderPath="Members to override\">
      <Declaration><![CDATA[METHOD PROTECTED OnReset
]]></Declaration>
      <Implementation>
        <ST><![CDATA[]]></ST>
      </Implementation>
    </Method>
    <Method Name="OnRun" Id="{86189e8d-6388-4684-bde6-5b6699ab994a}" FolderPath="Members to override\">
      <Declaration><![CDATA[METHOD PROTECTED ABSTRACT OnRun 
VAR_INPUT
	startRequest : BOOL;
END_VAR
VAR_OUTPUT
	processingResult : TASK_PROCESSING_RESULT;
	abortReason : WSTRING(255);
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[]]></ST>
      </Implementation>
    </Method>
    <Method Name="OnStart" Id="{fe010325-e49e-4ef1-b4a8-6ee6cb5afa9f}" FolderPath="Members to override\">
      <Declaration><![CDATA[METHOD PROTECTED ABSTRACT OnStart 
VAR_INPUT
	startRequest : BOOL;
END_VAR
VAR_OUTPUT
	processingResult : TASK_PROCESSING_RESULT;
	abortReason : WSTRING(255);
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[processingResult := TASK_PROCESSING_RESULT.DONE;]]></ST>
      </Implementation>
    </Method>
    <Method Name="processTransition" Id="{f687f09b-9482-4d87-85d6-49f44256ff75}" FolderPath="Service members\">
      <Declaration><![CDATA[METHOD PRIVATE processTransition]]></Declaration>
      <Implementation>
        <ST><![CDATA[_TransitionState := TASK_TRANSITION_STATE.IN_PROGRES;]]></ST>
      </Implementation>
    </Method>
    <Method Name="RequestCancel" Id="{af25ecfe-0328-4cb1-9bd1-f45cdb3e7c9a}">
      <Declaration><![CDATA[METHOD FINAL RequestCancel : BOOL
VAR_OUTPUT
	refuseReason : WSTRING(255);
END_VAR
VAR
	done : BOOL;
	
	exceptionCode : __SYSTEM.ExceptionCode;
	
	exception : IException;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF _State <> TASK_STATE.RUN THEN
	refuseReason := TOF_Core.WideStringHelper.ConcatStrings255("Expected state is '", TO_STRING(TASK_STATE.RUN), "' but current state is '", TO_STRING(_State), "'");
	RETURN;
END_IF

setCancelState();

RequestCancel := TRUE;]]></ST>
      </Implementation>
    </Method>
    <Method Name="RequestStart" Id="{e42cad17-9729-4058-a679-2df1935c0c5e}">
      <Declaration><![CDATA[METHOD FINAL RequestStart : BOOL
VAR_OUTPUT
	refuseReason : WSTRING(255);
END_VAR
VAR
	processingResult : TASK_PROCESSING_RESULT;
	abortReason : WSTRING(255);
	done : BOOL;

	exceptionCode : __SYSTEM.ExceptionCode;
	
	exception : IException;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF _State <> TASK_STATE.IDLE THEN
	refuseReason := TOF_Core.WideStringHelper.ConcatStrings255("Expected state is '", TO_STRING(TASK_STATE.IDLE), "' but current state is '", TO_STRING(_State), "'");
	RETURN;
END_IF

setStartState();

RequestStart := TRUE;]]></ST>
      </Implementation>
    </Method>
    <Method Name="Reset" Id="{094fadc9-7c01-48d6-8075-016e452103ec}">
      <Declaration><![CDATA[METHOD FINAL Reset : BOOL
VAR_OUTPUT
	refuseReason : WSTRING(255);
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF InTransition OR_ELSE (_State <> TASK_STATE.COMPETE AND_THEN _State <> TASK_STATE.ABORT AND_THEN _State <> TASK_STATE.CANCEL AND_THEN _State <> TASK_STATE.FAULT) THEN
	RETURN;
END_IF

OnReset();

setIdleState();]]></ST>
      </Implementation>
    </Method>
    <Method Name="setAbortState" Id="{c359efeb-05d2-4dbe-badd-076a1cdb69f5}" FolderPath="Service members\">
      <Declaration><![CDATA[METHOD PRIVATE FINAL setAbortState
VAR_IN_OUT CONSTANT
	abortReason : WSTRING(255);
END_VAR
VAR CONSTANT
	NEW_STATE : TASK_STATE := TASK_STATE.ABORT;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[LogManager.TryLogMessage(
	WideStringHelper.ConcatStrings255("Task '", Name, "' changed state from '", TO_WSTRING(_State), "' to '", TO_WSTRING(NEW_STATE), "'"), 
	LOG_CATEGORY.SUCCESS, 
	TO_WSTRING(ClassName), 
	"Task", 
	WideStringHelper.ConcatStrings255("Task", TO_WSTRING(NEW_STATE), ".Started")
);

_State := NEW_STATE;
	
_TransitionState := TASK_TRANSITION_STATE.START;
	
_AbortReason := abortReason;
_ErrorMessage := "";]]></ST>
      </Implementation>
    </Method>
    <Method Name="setCancelState" Id="{da900785-58db-4f6b-bb02-5039780ca4c3}" FolderPath="Service members\">
      <Declaration><![CDATA[METHOD PRIVATE FINAL setCancelState
VAR CONSTANT
	NEW_STATE : TASK_STATE := TASK_STATE.CANCEL;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[LogManager.TryLogMessage(
	WideStringHelper.ConcatStrings255("Task '", Name, "' changed state from '", TO_WSTRING(_State), "' to '", TO_WSTRING(NEW_STATE), "'"), 
	LOG_CATEGORY.SUCCESS, 
	TO_WSTRING(ClassName), 
	"Task", 
	WideStringHelper.ConcatStrings255("Task", TO_WSTRING(NEW_STATE), ".Started")
);

_State := NEW_STATE;
	
_TransitionState := TASK_TRANSITION_STATE.START;
	
_AbortReason := "";
_ErrorMessage := "";]]></ST>
      </Implementation>
    </Method>
    <Method Name="setCompleteState" Id="{5d38cc57-b0a6-4044-b830-12672c8ca4de}" FolderPath="Service members\">
      <Declaration><![CDATA[METHOD PRIVATE FINAL setCompleteState
VAR CONSTANT
	NEW_STATE : TASK_STATE := TASK_STATE.COMPETE;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[LogManager.TryLogMessage(
	WideStringHelper.ConcatStrings255("Task '", Name, "' changed state from '", TO_WSTRING(_State), "' to '", TO_WSTRING(NEW_STATE), "'"), 
	LOG_CATEGORY.SUCCESS, 
	TO_WSTRING(ClassName), 
	"Task", 
	WideStringHelper.ConcatStrings255("Task", TO_WSTRING(NEW_STATE), ".Started")
);

_State := NEW_STATE;

_TransitionState := TASK_TRANSITION_STATE.START;
	
_AbortReason := "";
_ErrorMessage := "";]]></ST>
      </Implementation>
    </Method>
    <Method Name="setFaultState" Id="{94e579ea-e3db-47c6-b4ef-29a861d65743}" FolderPath="Service members\">
      <Declaration><![CDATA[METHOD PRIVATE FINAL setFaultState
VAR_INPUT
	exceptionCode : __SYSTEM.ExceptionCode;
END_VAR
VAR CONSTANT
	NEW_STATE : TASK_STATE := TASK_STATE.FAULT;
END_VAR
VAR
	exception : IException;
	
	exceptionHandled : BOOL;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[LogManager.TryLogMessage(
	WideStringHelper.ConcatStrings255("Task '", Name, "' changed state from '", TO_WSTRING(_State), "' to '", TO_WSTRING(NEW_STATE), "'"), 
	LOG_CATEGORY.SUCCESS, 
	TO_WSTRING(ClassName), 
	"Task", 
	WideStringHelper.ConcatStrings255("Task", TO_WSTRING(NEW_STATE), ".Started")
);

_State := NEW_STATE;

_TransitionState := TASK_TRANSITION_STATE.START;
	
_AbortReason := "";

exception := TOF_Core.ExceptionManager.GetLastException(exceptionCode);

__TRY
	HandleException(exception, handled => exceptionHandled);
__CATCH
__ENDTRY

_ErrorMessage := exception.GetMessage();

IF NOT exceptionHandled THEN
	TOF_Core.ExceptionManager.Throw(exception);
END_IF]]></ST>
      </Implementation>
    </Method>
    <Method Name="setIdleState" Id="{17df136c-ad19-4e8f-8d77-17587caaee42}" FolderPath="Service members\">
      <Declaration><![CDATA[METHOD PRIVATE FINAL setIdleState]]></Declaration>
      <Implementation>
        <ST><![CDATA[LogManager.TryLogMessage(WideStringHelper.ConcatStrings255("Task '", Name, "' changed state from '", TO_WSTRING(_State), "' to '", TO_WSTRING(TASK_STATE.IDLE), "'"), LOG_CATEGORY.SUCCESS, TO_WSTRING(ClassName), "Task", "Task.IDLE");

_State := TASK_STATE.IDLE;

_TransitionState := TASK_TRANSITION_STATE.IDLE;
	
_AbortReason := "";
_ErrorMessage := "";]]></ST>
      </Implementation>
    </Method>
    <Method Name="setRunState" Id="{de88a65b-c153-4c8f-be4b-ad3c355eebda}" FolderPath="Service members\">
      <Declaration><![CDATA[METHOD PRIVATE FINAL setRunState
VAR CONSTANT
	NEW_STATE : TASK_STATE := TASK_STATE.RUN;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[LogManager.TryLogMessage(
	WideStringHelper.ConcatStrings255("Task '", Name, "' changed state from '", TO_WSTRING(_State), "' to '", TO_WSTRING(NEW_STATE), "'"), 
	LOG_CATEGORY.SUCCESS, 
	TO_WSTRING(ClassName), 
	"Task", 
	WideStringHelper.ConcatStrings255("Task", TO_WSTRING(NEW_STATE), ".Started")
);

_State := NEW_STATE;

_TransitionState := TASK_TRANSITION_STATE.START;
	
_AbortReason := "";
_ErrorMessage := "";]]></ST>
      </Implementation>
    </Method>
    <Method Name="setStartState" Id="{87ba9a10-854b-42d2-a741-7cd6a26e92de}" FolderPath="Service members\">
      <Declaration><![CDATA[METHOD PRIVATE FINAL setStartState
VAR CONSTANT
	NEW_STATE : TASK_STATE := TASK_STATE.START;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[LogManager.TryLogMessage(
	WideStringHelper.ConcatStrings255("Task '", Name, "' changed state from '", TO_WSTRING(_State), "' to '", TO_WSTRING(NEW_STATE), "'"), 
	LOG_CATEGORY.SUCCESS, 
	TO_WSTRING(ClassName), 
	"Task", 
	WideStringHelper.ConcatStrings255("Task", TO_WSTRING(NEW_STATE), ".Started")
);

_State := NEW_STATE;
	
_TransitionState := TASK_TRANSITION_STATE.START;
	
_AbortReason := "";
_ErrorMessage := "";]]></ST>
      </Implementation>
    </Method>
    <Property Name="State" Id="{3ca93549-ab64-4b84-8440-75c4fb80dd75}">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
PROPERTY FINAL State : TASK_STATE
]]></Declaration>
      <Get Name="Get" Id="{def003e6-3524-4c7a-834d-2441052dfef4}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[State := _State;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Property Name="StateChanged" Id="{45a7c0ed-3b2b-4bfd-97a7-7f55d6a357ac}">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
PROPERTY FINAL StateChanged : BOOL]]></Declaration>
      <Get Name="Get" Id="{a133175e-31bb-46ec-a64e-df469d326cf4}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[StateChanged := _State <> _PreviousState;]]></ST>
        </Implementation>
      </Get>
    </Property>
  </POU>
</TcPlcObject>