<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1">
  <POU Name="PathHelper" Id="{0dca2365-65a2-482c-a73e-d706575b93b9}" SpecialFunc="None">
    <Declaration><![CDATA[PROGRAM PathHelper]]></Declaration>
    <Implementation>
      <ST><![CDATA[]]></ST>
    </Implementation>
    <Method Name="IsValidDirectoryPath" Id="{74a38896-f3ee-456c-865c-7f0b909dbd4e}">
      <Declaration><![CDATA[METHOD IsValidDirectoryPath : BOOL
VAR_IN_OUT CONSTANT
	directoryPath : Tc2_System.T_MaxString;
END_VAR
VAR_OUTPUT
	errorMessage : WSTRING(255);
END_VAR
VAR
    i, j : UDINT;
    stringLength : UDINT;
    character : BYTE;
    invalidChars : STRING(8) := '<>"|?*^:';
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[stringLength := StringHelper.GetLength(directoryPath);

IF stringLength = 0 THEN
	errorMessage := "Path is empty";
    RETURN;
END_IF

FOR i := 0 TO stringLength - 1 DO
    character := directoryPath[i];
	
	// special case with Windows drive path
	IF i = 1 AND_THEN character = Tc2_System.F_ToASC(':') THEN
		IF stringLength >= 3 
			AND_THEN (directoryPath[2] = Tc2_System.F_ToASC('\') OR_ELSE directoryPath[2] = Tc2_System.F_ToASC('/')) 
			AND_THEN ((directoryPath[0] >= Tc2_System.F_ToASC('A') AND_THEN directoryPath[0] <= Tc2_System.F_ToASC('Z')) OR_ELSE (directoryPath[0] >= Tc2_System.F_ToASC('a') AND_THEN directoryPath[0] <= Tc2_System.F_ToASC('z')))
		THEN
			CONTINUE;
		END_IF		
	END_IF
    
    // check for invalid characters
    FOR j := 0 TO StringHelper.GetLength(invalidChars) - 1 DO
        IF character = invalidChars[j] THEN			
			errorMessage := WideStringHelper.ConcatStrings255("The forbidden character '", TO_WSTRING(Tc2_System.F_ToCHR(character)), "' at position ", TO_WSTRING(i + 1));
            RETURN;
        END_IF
    END_FOR
    
	// check for special ascii characters
    IF character < 32 THEN
		errorMessage := WideStringHelper.ConcatStrings255("The forbidden character with code ", TO_WSTRING(character), " at position ", TO_WSTRING(i + 1));
        RETURN;
    END_IF
	
	// two slashes one after other are not allowed (except on start)
	IF i > 1 
		AND_THEN (character = Tc2_System.F_ToASC('\') OR_ELSE character = Tc2_System.F_ToASC('/')) 
		AND_THEN (directoryPath[i - 1] = Tc2_System.F_ToASC('\') OR_ELSE directoryPath[i - 1] = Tc2_System.F_ToASC('/')) 
	THEN
		errorMessage := WideStringHelper.ConcatStrings255("Several slashes one by one at position ", TO_WSTRING(i + 1));	
		RETURN;
	END_IF
END_FOR

IsValidDirectoryPath := TRUE;]]></ST>
      </Implementation>
    </Method>
    <Method Name="IsValidFilePath" Id="{c79d4e63-0f05-42ae-918f-7c9d81a28bdc}">
      <Declaration><![CDATA[METHOD IsValidFilePath : BOOL
VAR_IN_OUT CONSTANT
	filePath : Tc2_System.T_MaxString;
END_VAR
VAR_OUTPUT
	errorMessage : WSTRING(255);
END_VAR
VAR
    i, j : UDINT;
    stringLength : UDINT;
    character : BYTE;
    invalidChars : STRING(8) := '<>"|?*^:';
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[stringLength := StringHelper.GetLength(filePath);

IF stringLength = 0 THEN
	errorMessage := "Path is empty";
    RETURN;
END_IF

FOR i := 0 TO stringLength - 1 DO
    character := filePath[i];
	
	// special case with Windows drive path
	IF i = 1 AND_THEN character = Tc2_System.F_ToASC(':') THEN
		IF stringLength >= 3 
			AND_THEN (filePath[2] = Tc2_System.F_ToASC('\') OR_ELSE filePath[2] = Tc2_System.F_ToASC('/')) 
			AND_THEN ((filePath[0] >= Tc2_System.F_ToASC('A') AND_THEN filePath[0] <= Tc2_System.F_ToASC('Z')) OR_ELSE (filePath[0] >= Tc2_System.F_ToASC('a') AND_THEN filePath[0] <= Tc2_System.F_ToASC('z')))
		THEN
			CONTINUE;
		END_IF		
	END_IF
    
    // check for invalid characters
    FOR j := 0 TO StringHelper.GetLength(invalidChars) - 1 DO
        IF character = invalidChars[j] THEN
			errorMessage := WideStringHelper.ConcatStrings255("The forbidden character '", TO_WSTRING(Tc2_System.F_ToCHR(character)), "' at position ", TO_WSTRING(i + 1));
            RETURN;
        END_IF
    END_FOR
    
	// check for special ascii characters
    IF character < 32 THEN
		errorMessage := WideStringHelper.ConcatStrings255("The forbidden character with code ", TO_WSTRING(character), " at position ", TO_WSTRING(i + 1));
        RETURN;
    END_IF
	
	// two slashes one after other are not allowed (except on start)
	IF i > 1 
		AND_THEN (character = Tc2_System.F_ToASC('\') OR_ELSE character = Tc2_System.F_ToASC('/')) 
		AND_THEN (filePath[i - 1] = Tc2_System.F_ToASC('\') OR_ELSE filePath[i - 1] = Tc2_System.F_ToASC('/')) 
	THEN
		errorMessage := WideStringHelper.ConcatStrings255("Several slashes one by one at position ", TO_WSTRING(i + 1));	
		RETURN;
	END_IF
	
	// slash can't be the last char of the file path
	IF i = stringLength AND_THEN (character = Tc2_System.F_ToASC('\') OR_ELSE character = Tc2_System.F_ToASC('/')) THEN
		errorMessage := "Slash at the end of the path";	
		RETURN;
	END_IF
END_FOR

IsValidFilePath := TRUE;]]></ST>
      </Implementation>
    </Method>
  </POU>
</TcPlcObject>