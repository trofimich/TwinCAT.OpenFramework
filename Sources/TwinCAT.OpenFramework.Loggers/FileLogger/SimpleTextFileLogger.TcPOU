<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1">
  <POU Name="SimpleTextFileLogger" Id="{c9d8643e-1245-46dc-b854-d67cd09f9849}" SpecialFunc="None">
    <Declaration><![CDATA[(*

.. <legal notes>

legal notes
=================
| SPDX-FileCopyrightText: © 2026 Oleksandr Tiutyk <trofimich@gmail.com>
| SPDX-License-Identifier: LGPL-3.0-only
| For details check: LGPL-3.0-only_

.. _LGPL-3.0-only: https://www.gnu.org/licenses/lgpl-3.0.en.html

.. </legal notes>

*)

{attribute 'no_explicit_call' := 'do not call this function block directly'} 
FUNCTION_BLOCK SimpleTextFileLogger EXTENDS TOF_Core.Object IMPLEMENTS TOF_Core.ILogger
VAR
	_ClassName : STRING := __POUNAME();
	
	_State : SIMPLE_TEXT_FILE_LOGGER_STATE;
	_ErrorMessage : TOF_Core.ERROR_MESSAGE;
	
	_LogFilePrefix : STRING(32);

	_CreateDirectoryIfNotExists : TOF_FileSystem.CreateDirectoryIfNotExists;	
	
	_FileContentManager : TOF_FileSystem.FileContentManager;
	_RecordsPerFile : UINT;
	_WriteBytesTask : TOF_FileSystem.WriteBytesToFile;
	
	_MaxRecordsPerFile : UINT;
	
	_MessageBuffer : TOF_Collections.List(0);
	_SendBuffer : TOF_Collections.ListOfBytes;
END_VAR]]></Declaration>
    <Implementation>
      <ST><![CDATA[]]></ST>
    </Implementation>
    <Folder Name="Service memebers" Id="{23f3c168-aa3e-41c9-8656-467cb2b8262a}" />
    <Folder Name="Settings" Id="{41715161-e020-4788-b208-1e23635c7828}" />
    <Property Name="AmsNetId" Id="{c7aad8b5-3ec3-4d0c-bcf3-a82db314ba1d}" FolderPath="Settings\">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
PROPERTY AmsNetId : Tc2_System.T_AmsNetID]]></Declaration>
      <Get Name="Get" Id="{7ac92ac9-d7f3-4973-8fca-c632f923d55e}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[AmsNetId := _CreateDirectoryIfNotExists.AmsNetId;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Property Name="ClassName" Id="{1c851d02-4ca2-4f25-a849-839385a5dbab}">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
PROPERTY ClassName : STRING]]></Declaration>
      <Get Name="Get" Id="{83d1e7be-893c-4f7b-b733-e85ea11d30f5}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[ClassName := _ClassName;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Method Name="createCurrentFilePath" Id="{ca6e22e2-90b3-499d-a72f-e99009f1b4ec}" FolderPath="Service memebers\">
      <Declaration><![CDATA[METHOD PRIVATE createCurrentFilePath : Tc2_System.T_MaxString
VAR
	stringBuilder : TOF_Core.StringBuilder;
END_VAR
VAR
	filePathOverflowException : TOF_Core.StringOwerflowException;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[stringBuilder
	.AppendString255(DirectoryPath)
	.AppendString255('\')
	.Append(_LogFilePrefix)
	.AppendString255('_')
	.AppendString255(DateAndTimeHelper.ToString(LDT_TO_DT(SystemDateTimeManager.SystemLocalDateTime)))
	.AppendString255('.log');
	
IF stringBuilder.StringLength > 255 THEN
	filePathOverflowException.Throw(NamespaceName, ClassName, __POUNAME(), __POSITION(), 'CurrentFilePath', stringBuilder.StringLength, 255);  
END_IF

stringBuilder.AssignToString(createCurrentFilePath); ]]></ST>
      </Implementation>
    </Method>
    <Property Name="DirectoryPath" Id="{94bd0435-0d84-474e-a1ec-237ce1293544}" FolderPath="Settings\">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
PROPERTY DirectoryPath : Tc2_System.T_MaxString]]></Declaration>
      <Get Name="Get" Id="{d9e78a19-b81b-4e9b-b0b9-97faece19c52}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[DirectoryPath := _CreateDirectoryIfNotExists.DirectoryPath;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Property Name="DirectoryPathType" Id="{54324f5d-1050-491f-bf7e-c62d1b9d586c}" FolderPath="Settings\">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
PROPERTY DirectoryPathType : Tc2_System.E_OpenPath]]></Declaration>
      <Get Name="Get" Id="{d33548de-ffe1-4d4e-afe8-be9289c99711}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[DirectoryPathType := _CreateDirectoryIfNotExists.PathType;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Property Name="ErrorMessage" Id="{a0ecd76a-1fbd-413a-b471-4f1bc6db4cb3}">
      <Declaration><![CDATA[PROPERTY ErrorMessage : REFERENCE TO TOF_Core.ERROR_MESSAGE]]></Declaration>
      <Get Name="Get" Id="{d483655a-840c-48a1-b4b0-779eaaf75b0f}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[ErrorMessage REF= _ErrorMessage;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Method Name="Execute" Id="{fb5ae989-a335-4d00-9822-2d51d9b09e57}">
      <Declaration><![CDATA[METHOD Execute
VAR
	createDirectoryException : TOF_Core.StandardException;
	
	exceptionCode : __SYSTEM.ExceptionCode;
	exception : IException;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[__TRY
	CASE _State OF
		SIMPLE_TEXT_FILE_LOGGER_STATE.INITIAL:				
			_CreateDirectoryIfNotExists.Start();
			
			_State := SIMPLE_TEXT_FILE_LOGGER_STATE.CREATING_DIRECTORY;
		
		SIMPLE_TEXT_FILE_LOGGER_STATE.CREATING_DIRECTORY:	
			_CreateDirectoryIfNotExists.Execute();
			
			IF _CreateDirectoryIfNotExists.State.Stopped THEN
				IF _CreateDirectoryIfNotExists.StopReason.Success THEN					
					_State := SIMPLE_TEXT_FILE_LOGGER_STATE.LOGGING;
				ELSE			
					createDirectoryException.Throw(NamespaceName, ClassName, __POUNAME(), __POSITION(), "Log directory not created");
				END_IF
			END_IF	
			
		SIMPLE_TEXT_FILE_LOGGER_STATE.LOGGING:
			_FileContentManager.Execute();
			
			tryLogNewMessages();
	END_CASE
__CATCH(exceptionCode)
	_State := SIMPLE_TEXT_FILE_LOGGER_STATE.FAILED;
	
	exception := ExceptionManager.GetLastException(exceptionCode);
	
	_ErrorMessage := exception.GetMessage();
	
	ExceptionManager.Throw(exception);
__ENDTRY]]></ST>
      </Implementation>
    </Method>
    <Method Name="FB_init" Id="{486e1077-8528-461f-96dd-eb258fc0335f}">
      <Declaration><![CDATA[METHOD FB_init : BOOL
VAR_INPUT
	(* if TRUE, the retain variables are initialized (warm start / cold start)*)
	bInitRetains	: BOOL;
	(* if TRUE, the instance afterwards gets moved into the copy code (online change)*)
	bInCopyCode	: BOOL;
	directoryPath : Tc2_System.T_MaxString;
	logFilePrefix : STRING(32);
	maxRecordsPerFile : UINT;
	directoryPathType : Tc2_System.E_OpenPath := Tc2_System.E_OpenPath.PATH_GENERIC; // use Tc2_System.E_OpenPath.PATH_GENERIC by default
	timeout : TIME := Tc2_System.Global_Variables.DEFAULT_ADS_TIMEOUT; // use Tc2_System.Global_Variables.DEFAULT_ADS_TIMEOUT by default
	amsNetId : Tc2_System.T_AmsNetID := ''; // use '' for local system
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[_CreateDirectoryIfNotExists.DirectoryPath := directoryPath;
_CreateDirectoryIfNotExists.PathType := directoryPathType;
_CreateDirectoryIfNotExists.Timeout := timeout;
_CreateDirectoryIfNotExists.AmsNetId := amsNetId;

_FileContentManager.PathType := directoryPathType;
_FileContentManager.ModeFlags := Tc2_System.Global_Variables.FOPEN_MODEWRITE OR Tc2_System.Global_Variables.FOPEN_MODEPLUS OR Tc2_System.Global_Variables.FOPEN_MODETEXT;
_FileContentManager.Timeout := timeout;
_FileContentManager.AmsNetId := amsNetId;

_LogFilePrefix := logFilePrefix;

_MaxRecordsPerFile := maxRecordsPerFile; ]]></ST>
      </Implementation>
    </Method>
    <Property Name="LogFilePrefix" Id="{40add88b-d2fb-4d3b-98cc-bad1e64da312}" FolderPath="Settings\">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
PROPERTY LogFilePrefix : STRING(32)]]></Declaration>
      <Get Name="Get" Id="{4c3e6ef5-a4e7-48c3-a7a6-4379c4a25fd2}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[LogFilePrefix := _LogFilePrefix;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Property Name="MaxRecordsPerFile" Id="{0dfa9c35-c9c2-4980-a810-2983eca9e012}" FolderPath="Settings\">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
PROPERTY MaxRecordsPerFile : UINT]]></Declaration>
      <Get Name="Get" Id="{98d5b626-1240-45cb-ab68-cadf61864d2d}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[MaxRecordsPerFile := _MaxRecordsPerFile;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Property Name="NamespaceName" Id="{a2891bf6-e5a5-4439-9b48-a1ce3b9fe7e5}">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
PROPERTY NamespaceName : STRING]]></Declaration>
      <Get Name="Get" Id="{5ff3dc63-4bd4-4d08-af93-e0e18171ffa3}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[NamespaceName := CurrentNamespace.Name;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Property Name="SelfSize" Id="{26a805fc-69de-4218-9d0a-a6159c2e8b1c}">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
PROPERTY SelfSize : ULINT]]></Declaration>
      <Get Name="Get" Id="{a575e209-b49d-4b51-b2b7-52dee844fac2}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[SelfSize := XSIZEOF(THIS^);]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Property Name="State" Id="{b977ac9f-344e-4250-abf4-1d517085a4b0}">
      <Declaration><![CDATA[PROPERTY State : SIMPLE_TEXT_FILE_LOGGER_STATE]]></Declaration>
      <Get Name="Get" Id="{e0b36e8f-e7ca-4443-9251-c57892a0b995}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[State := _State;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Property Name="Timeout" Id="{89f222c3-c94b-47cc-8b8a-ef225ee77452}" FolderPath="Settings\">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
PROPERTY Timeout : TIME]]></Declaration>
      <Get Name="Get" Id="{40e786b0-9978-446c-83ae-0f6637903d47}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[Timeout := _CreateDirectoryIfNotExists.Timeout;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Method Name="TryLogException" Id="{b99885cc-2056-4f4b-ba06-145c472d92b5}">
      <Declaration><![CDATA[METHOD TryLogException : BOOL
VAR_INPUT
	exception : TOF_Core.IException;
END_VAR
VAR
	message : TOF_Core.ERROR_MESSAGE;
	aggregateException : TOF_Core.IAggregateException;
	innerException : TOF_Core.IException;
	i : DINT;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF exception = 0 THEN
	RETURN;
END_IF

message := exception.GetMessage(TRUE, FALSE, TRUE);

TryLogException := TryLogMessage(LOG_CATEGORY.ERROR, message);]]></ST>
      </Implementation>
    </Method>
    <Method Name="TryLogMessage" Id="{350b4112-ef62-44cb-8cce-fcf9841762dc}">
      <Declaration><![CDATA[METHOD TryLogMessage : BOOL
VAR_INPUT
	category : TOF_Core.LOG_CATEGORY;	
END_VAR
VAR_IN_OUT CONSTANT
	message : TOF_Core.ERROR_MESSAGE;
END_VAR
VAR
	textToLog : TOF_Core.ERROR_MESSAGE;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[textToLog := WideStringHelper.ConcatStrings255("#", TO_WSTRING(category), ": ");

TOF_Core.WideStringHelper.AppendToTarget(textToLog, message);
TOF_Core.WideStringHelper.AppendToTarget(textToLog, TOF_Core.WideStringHelper.NewLine);
TOF_Core.WideStringHelper.AppendToTarget(textToLog, TOF_Core.WideStringHelper.NewLine);

_MessageBuffer.AppendGeneric(TOF_Core.GenericValueFactory.FromWStringValue(textToLog));]]></ST>
      </Implementation>
    </Method>
    <Method Name="tryLogNewMessages" Id="{cca9aac9-2d4b-4e92-aa8f-79b455184bb9}" FolderPath="Service memebers\">
      <Declaration><![CDATA[METHOD PRIVATE tryLogNewMessages
VAR
	message : TOF_Core.GENERIC_VALUE;
	
	exception : TOF_Core.StandardException;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF _MessageBuffer.Count = 0 OR_ELSE _FileContentManager.PendingTaskCount = 0 THEN
	RETURN;
END_IF

IF NOT _WriteBytesTask.State.Stopped THEN
	exception.Throw(Namespacename, ClassName, __POUNAME(), __POSITION(), WideStringHelper.ConcatStrings255("WriteBytesTask has invalid state '", TO_WSTRING(_WriteBytesTask.State.Value), "'"));
END_IF
 
IF NOT _WriteBytesTask.StopReason.Success THEN
	exception.Throw(Namespacename, ClassName, __POUNAME(), __POSITION(), WideStringHelper.ConcatStrings255("WriteBytesTask has invalid stop reason '", TO_WSTRING(_WriteBytesTask.StopReason.Value), "'"));	
END_IF

_SendBuffer.Clear();

IF _FileContentManager.FilePath = '' OR_ELSE _RecordsPerFile >= _MaxRecordsPerFile THEN
	_FileContentManager.Reset();
	
	_FileContentManager.FilePath := createCurrentFilePath();
END_IF

WHILE _MessageBuffer.Count > 0 DO
	message := _MessageBuffer.Get(0);
	
	_SendBuffer.AppendGeneric(message);
	
	_RecordsPerFile := _RecordsPerFile + 1;
	
	IF _RecordsPerFile >= _MaxRecordsPerFile THEN
		EXIT;
	END_IF
END_WHILE

_WriteBytesTask.SourceBuffer := _SendBuffer.DataAddress;
_WriteBytesTask.BytesCountToWrite := TO_UDINT(_SendBuffer.Count);

_FileContentManager.Enqueue(_WriteBytesTask);]]></ST>
      </Implementation>
    </Method>
  </POU>
</TcPlcObject>