<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1">
  <POU Name="SimpleTextFileLogger" Id="{83e72a06-d888-49e4-a1fa-5926cd0560d7}" SpecialFunc="None">
    <Declaration><![CDATA[(*

.. <legal notes>

legal notes
=================
| SPDX-FileCopyrightText: © 2026 Oleksandr Tiutyk <trofimich@gmail.com>
| SPDX-License-Identifier: LGPL-3.0-only
| For details check: LGPL-3.0-only_

.. _LGPL-3.0-only: https://www.gnu.org/licenses/lgpl-3.0.en.html

.. </legal notes>

*)

{attribute 'no_explicit_call' := 'do not call this function block directly'} 
FUNCTION_BLOCK SimpleTextFileLogger EXTENDS TOF_Core.Object IMPLEMENTS TOF_Core.ILogger
VAR
	_ClassName : STRING := __POUNAME();
	
	_State : SIMPLE_TEXT_FILE_LOGGER_STATE;
	_ErrorMessage : TOF_Core.ERROR_MESSAGE;
	
	_LogFilePrefix : STRING(32);

	_CreateDirectoryIfNotExists : TOF_FileSystem.CreateDirectoryIfNotExists;	
	
	_FileContentManager : TOF_FileSystem.FileContentManager;
	_RecordsPerFile : UINT;
	_WriteBytesTask : TOF_FileSystem.WriteBytesToFile;
	
	_MaxRecordsPerFile : UINT := 100;
	
	_MessageBuffer : TOF_Collections.List(0);
	_SendBuffer : TOF_Collections.ListOfBytes;
	
	_LogFileCounter : UDINT;
END_VAR]]></Declaration>
    <Implementation>
      <ST><![CDATA[]]></ST>
    </Implementation>
    <Folder Name="Service memebers" Id="{31f4dc51-0481-47a0-9f27-e4a804e1bc51}" />
    <Folder Name="Settings" Id="{a5c9b4b6-8ecf-4d2a-aad7-cca7912de4af}" />
    <Property Name="AmsNetId" Id="{db9518e6-7a39-4ac8-867a-92c65be113ea}" FolderPath="Settings\">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
PROPERTY AmsNetId : Tc2_System.T_AmsNetID]]></Declaration>
      <Get Name="Get" Id="{530d554b-478f-4d22-919b-d8853b880ece}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[AmsNetId := _CreateDirectoryIfNotExists.AmsNetId;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Property Name="ClassName" Id="{6c5a9607-3c00-4c0a-8401-629f2e4ee076}">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
PROPERTY ClassName : STRING]]></Declaration>
      <Get Name="Get" Id="{0de335d9-e75a-41b2-898b-58ea3af1863b}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[ClassName := _ClassName;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Method Name="createCurrentFilePath" Id="{6d3ac00a-486a-4f00-87d7-8b126f05bc66}" FolderPath="Service memebers\">
      <Declaration><![CDATA[METHOD PRIVATE createCurrentFilePath : Tc2_System.T_MaxString
VAR
	stringBuilder : TOF_Core.StringBuilder;
END_VAR
VAR
	filePathOverflowException : TOF_Core.StringOwerflowException;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[stringBuilder
	.AppendString255(DirectoryPath)
	.AppendString255('\')
	.Append(_LogFilePrefix)
	.AppendString255('_')
	.AppendString255(DateAndTimeHelper.ToString(LDT_TO_DT(SystemDateTimeManager.SystemLocalDateTime)))
	.AppendString255('_')
	.AppendString255(TO_STRING(_LogFileCounter))
	.AppendString255('.log');
	
IF stringBuilder.StringLength > 255 THEN
	filePathOverflowException.Throw(NamespaceName, ClassName, __POUNAME(), __POSITION(), 'CurrentFilePath', stringBuilder.StringLength, 255);  
END_IF

stringBuilder.AssignToString(createCurrentFilePath); ]]></ST>
      </Implementation>
    </Method>
    <Property Name="DirectoryPath" Id="{28aea964-b2f7-4d34-9438-82c3e54285f7}" FolderPath="Settings\">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
PROPERTY DirectoryPath : Tc2_System.T_MaxString]]></Declaration>
      <Get Name="Get" Id="{4d97eb27-ef49-4b43-a312-0269a99b3764}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[DirectoryPath := _CreateDirectoryIfNotExists.DirectoryPath;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Property Name="DirectoryPathType" Id="{e71793a7-aadb-4558-8862-be98618eea47}" FolderPath="Settings\">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
PROPERTY DirectoryPathType : Tc2_System.E_OpenPath]]></Declaration>
      <Get Name="Get" Id="{d6c43e3e-9d0c-4541-99cd-2e8c4518ca4c}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[DirectoryPathType := _CreateDirectoryIfNotExists.PathType;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Property Name="ErrorMessage" Id="{5a7d9063-33f0-46e9-a8be-787b9c0b94fb}">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
PROPERTY ErrorMessage : REFERENCE TO TOF_Core.ERROR_MESSAGE]]></Declaration>
      <Get Name="Get" Id="{8798ddfa-1ff2-4292-a2e0-f047127f13ab}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[ErrorMessage REF= _ErrorMessage;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Method Name="Execute" Id="{17cdfc70-e5ab-42fe-ab86-0d76e3d26166}">
      <Declaration><![CDATA[METHOD Execute
VAR
	createDirectoryException : TOF_Core.StandardException;
	
	exceptionCode : __SYSTEM.ExceptionCode;
	exception : IException;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[__TRY
	CASE _State OF
		SIMPLE_TEXT_FILE_LOGGER_STATE.INITIAL:				
			_CreateDirectoryIfNotExists.Start();
			
			_State := SIMPLE_TEXT_FILE_LOGGER_STATE.CREATING_DIRECTORY;
		
		SIMPLE_TEXT_FILE_LOGGER_STATE.CREATING_DIRECTORY:	
			_CreateDirectoryIfNotExists.Execute();
			
			IF _CreateDirectoryIfNotExists.State.Stopped THEN
				IF _CreateDirectoryIfNotExists.FinalStopReason.WorkDone THEN					
					_State := SIMPLE_TEXT_FILE_LOGGER_STATE.LOGGING;
				ELSE			
					createDirectoryException.Throw(NamespaceName, ClassName, __POUNAME(), __POSITION(), "Log directory not created");
				END_IF
			END_IF	
			
		SIMPLE_TEXT_FILE_LOGGER_STATE.LOGGING:
			_FileContentManager.Execute();
			
			IF _FileContentManager.State.Stopped AND_THEN _FileContentManager.FinalStopReason.Failure THEN					
				createDirectoryException.Throw(NamespaceName, ClassName, __POUNAME(), __POSITION(), _FileContentManager.FinalStopReason.Message);
			END_IF	
			
			tryLogNewMessages();
	END_CASE
__CATCH(exceptionCode)
	_State := SIMPLE_TEXT_FILE_LOGGER_STATE.FAILED;
	
	exception := ExceptionManager.GetLastException(exceptionCode);
	
	_ErrorMessage := exception.GetMessage();
	
	ExceptionManager.Throw(exception);
__ENDTRY]]></ST>
      </Implementation>
    </Method>
    <Method Name="FB_init" Id="{bc5368c5-4e8b-4212-a2e8-e686311d988e}">
      <Declaration><![CDATA[METHOD FB_init : BOOL
VAR_INPUT
	(* if TRUE, the retain variables are initialized (warm start / cold start)*)
	bInitRetains	: BOOL;
	(* if TRUE, the instance afterwards gets moved into the copy code (online change)*)
	bInCopyCode	: BOOL;
	directoryPath : Tc2_System.T_MaxString;
	logFilePrefix : STRING(32);
	maxRecordsPerFile : UINT;
	directoryPathType : Tc2_System.E_OpenPath := Tc2_System.E_OpenPath.PATH_GENERIC; // use Tc2_System.E_OpenPath.PATH_GENERIC by default
	timeout : TIME := Tc2_System.Global_Variables.DEFAULT_ADS_TIMEOUT; // use Tc2_System.Global_Variables.DEFAULT_ADS_TIMEOUT by default
	amsNetId : Tc2_System.T_AmsNetID := ''; // use '' for local system
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[_CreateDirectoryIfNotExists.DirectoryPath := directoryPath;
_CreateDirectoryIfNotExists.PathType := directoryPathType;
_CreateDirectoryIfNotExists.Timeout := timeout;
_CreateDirectoryIfNotExists.AmsNetId := amsNetId;

_FileContentManager.PathType := directoryPathType;
_FileContentManager.ModeFlags := Tc2_System.Global_Variables.FOPEN_MODEWRITE OR Tc2_System.Global_Variables.FOPEN_MODEPLUS OR Tc2_System.Global_Variables.FOPEN_MODETEXT;
_FileContentManager.Timeout := timeout;
_FileContentManager.AmsNetId := amsNetId;

_LogFilePrefix := logFilePrefix;

_MaxRecordsPerFile := maxRecordsPerFile; ]]></ST>
      </Implementation>
    </Method>
    <Property Name="LogFilePrefix" Id="{146ab0f3-3bee-4300-b03e-e0ac8f4dcd78}" FolderPath="Settings\">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
PROPERTY LogFilePrefix : STRING(32)]]></Declaration>
      <Get Name="Get" Id="{a928530e-14c6-4320-a490-9478edb8bdec}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[LogFilePrefix := _LogFilePrefix;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Property Name="Logging" Id="{592a2ef0-f70e-46be-b435-2b32d7b1865a}">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
PROPERTY Logging : BOOL]]></Declaration>
      <Get Name="Get" Id="{369a6e60-cede-41f5-8312-2b57e21b7316}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[Logging := _MessageBuffer.Count > 0 OR_ELSE _WriteBytesTask.State.Running;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Property Name="MaxRecordsPerFile" Id="{beb3d3d9-cff4-4690-9465-35ffe8503203}" FolderPath="Settings\">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
PROPERTY MaxRecordsPerFile : UINT]]></Declaration>
      <Get Name="Get" Id="{7fa1b082-aa9d-4ae5-b1bc-a908672be28a}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[MaxRecordsPerFile := _MaxRecordsPerFile;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Property Name="NamespaceName" Id="{4431f0cd-01ee-42e1-822b-47c557994012}">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
PROPERTY NamespaceName : STRING]]></Declaration>
      <Get Name="Get" Id="{629e0b94-319d-499b-9f3c-7a384bc3cd7b}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[NamespaceName := CurrentNamespace.Name;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Property Name="SelfSize" Id="{b9231c56-1285-4e63-ad5b-c9e11184018f}">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
PROPERTY SelfSize : ULINT]]></Declaration>
      <Get Name="Get" Id="{52d3ef31-002b-4f65-8047-d3dfbc84d9a8}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[SelfSize := XSIZEOF(THIS^);]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Property Name="State" Id="{3a575e6f-bb16-4315-8c99-6e30e5beab57}">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
PROPERTY State : SIMPLE_TEXT_FILE_LOGGER_STATE]]></Declaration>
      <Get Name="Get" Id="{3db3f673-1f63-4a8b-861c-f15fab2f1187}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[State := _State;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Property Name="Timeout" Id="{99ab8e8d-9dc9-4652-a5a8-5bf0cbecebc1}" FolderPath="Settings\">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
PROPERTY Timeout : TIME]]></Declaration>
      <Get Name="Get" Id="{c080a9e4-56eb-4735-8797-b6726b7c34d3}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[Timeout := _CreateDirectoryIfNotExists.Timeout;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Method Name="TryLogException" Id="{e7bb0a19-d38b-4050-ac16-1d13038e54cb}">
      <Declaration><![CDATA[METHOD TryLogException : BOOL
VAR_INPUT
	exception : TOF_Core.IException;
END_VAR
VAR
	message : TOF_Core.ERROR_MESSAGE;
	aggregateException : TOF_Core.IAggregateException;
	innerException : TOF_Core.IException;
	i : DINT;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF exception = 0 THEN
	RETURN;
END_IF

message := exception.GetMessage(TRUE, FALSE, TRUE);

TryLogException := TryLogMessage(LOG_CATEGORY.ERROR, message);]]></ST>
      </Implementation>
    </Method>
    <Method Name="TryLogMessage" Id="{f95238b0-2f26-4019-bd69-7afd783a5817}">
      <Declaration><![CDATA[METHOD TryLogMessage : BOOL
VAR_INPUT
	category : TOF_Core.LOG_CATEGORY;	
END_VAR
VAR_IN_OUT CONSTANT
	message : TOF_Core.ERROR_MESSAGE;
END_VAR
VAR
	textToLog : TOF_Core.ERROR_MESSAGE;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[textToLog := WideStringHelper.ConcatStrings255(TO_WSTRING(category), ": ");

TOF_Core.WideStringHelper.AppendToTarget(textToLog, message);

_MessageBuffer.AppendGeneric(TOF_Core.GenericValueFactory.FromWStringValue(textToLog));]]></ST>
      </Implementation>
    </Method>
    <Method Name="tryLogNewMessages" Id="{b3206f74-587e-4877-b310-0e0e64ffdebf}" FolderPath="Service memebers\">
      <Declaration><![CDATA[METHOD PRIVATE tryLogNewMessages
VAR
	message : TOF_Core.GENERIC_VALUE;
	
	exception : TOF_Core.StandardException;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF _MessageBuffer.Count = 0 OR_ELSE _FileContentManager.PendingTaskCount > 0 THEN
	RETURN;
END_IF

IF NOT _WriteBytesTask.State.Idle AND_THEN NOT _WriteBytesTask.State.Stopped THEN
	exception.Throw(Namespacename, ClassName, __POUNAME(), __POSITION(), WideStringHelper.ConcatStrings255("WriteBytesTask has invalid state '", TO_WSTRING(_WriteBytesTask.State.Value), "'"));
END_IF
 
IF _WriteBytesTask.State.Stopped AND_THEN NOT _WriteBytesTask.FinalStopReason.WorkDone THEN
	exception.Throw(Namespacename, ClassName, __POUNAME(), __POSITION(), WideStringHelper.ConcatStrings255("WriteBytesTask has invalid stop reason '", TO_WSTRING(_WriteBytesTask.FinalStopReason.Value), "'"));	
END_IF

IF _RecordsPerFile >= _MaxRecordsPerFile AND_THEN _FileContentManager.State.Running THEN
	IF  NOT _FileContentManager.RunningSubstate.ResourceReleasing THEN
		_FileContentManager.Cancel();
	END_IF

	IF _FileContentManager.State.Running THEN
		RETURN;
	END_IF	
END_IF

_SendBuffer.Clear();

IF _FileContentManager.FilePath = '' OR_ELSE _RecordsPerFile >= _MaxRecordsPerFile THEN
	_RecordsPerFile := 0;
	_LogFileCounter := _LogFileCounter + 1;
	
	_FileContentManager.Reset();
	
	_FileContentManager.FilePath := createCurrentFilePath();
	
	_FileContentManager.Start();
	
	_SendBuffer.AppendData(ADR(WideStringHelper.ByteOrderMask), SIZEOF(WideStringHelper.ByteOrderMask));
END_IF

WHILE _MessageBuffer.Count > 0 DO
	message := _MessageBuffer.Get(0);
	
	_SendBuffer.AppendData(message.Address, TO_UDINT(message.Size - WideStringHelper.CharSize));
	_SendBuffer.AppendData(ADR(WideStringHelper.NewLine), TO_UDINT(SIZEOF(WideStringHelper.NewLine) - WideStringHelper.CharSize));
	
	_MessageBuffer.RemoveAt(0);
	
	_RecordsPerFile := _RecordsPerFile + 1;
	
	IF _RecordsPerFile >= _MaxRecordsPerFile THEN
		EXIT;
	END_IF
END_WHILE

_WriteBytesTask.Reset();
_WriteBytesTask.SourceBuffer := _SendBuffer.DataAddress;
_WriteBytesTask.BytesCountToWrite := TO_UDINT(_SendBuffer.Count);

_FileContentManager.Enqueue(_WriteBytesTask);]]></ST>
      </Implementation>
    </Method>
  </POU>
</TcPlcObject>